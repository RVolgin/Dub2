    #####################
    #Глава 3. Отклонение#
    #####################


label dct_fluctuation:
    $ save_name = u"Дубликат. Монетка в фонтане -- Отклонение"
    $ day_time()
    $ persistent.sprite_time = "day"
    scene black
    show headline_text2 u"Глава III. Отклонение" at truecenter with dspr
    play ambience ambience_int_cabin_day fadein 2
    $ renpy.pause(3.0)
    hide headline_text2 with dissolve
    $ renpy.pause(0.2)
    
    scene int_house_of_sl_day with blinds
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound dct_sfx_horn_rise_through_loudspeaker fadein 1.5 #по-моему, длится слишком долго
    "Женя на этот раз первой услышала горн, непривычно громкий, прямо над ухом. А только потом уже щебетание соседки."
    show mi smile pioneer with dissolve
    mi "Женя, Женечка. Ты слышала? Все очень даже неплохо. Я так переживала, что не могу Максиму ничего толком показать, я ведь горн даже в руках никогда не держала. А еще Максима хотят в наш отряд перевести, Женечка, ты же не против?"
    mz "Не против я, не против."
    th "Господи, только помолчи ты хоть две минуты!"
    show mi cry_smile pioneer with dissolve
    mi "А еще, Женечка, вот, это тебе принесли. Ты знаешь, я прямо тебе завидую, мы третий день в лагере, а тебе уже цветы дарят. А мне никто... И никому из девочек еще... Никто..."
    "Женя, справившись с очками, посмотрела на расчесывающую волосы перед зеркалом Мику, а оттуда, повинуясь жесту соседки, на стол."
    mz "А ты уверена, что это мне?"
    "Женя спросила это подчеркнуто сухо."
    show mi normal pioneer with dissolve
    mi "Ну, Женечка, я утром встала пораньше, чтобы послушать, как будет играть Максим. Вышла на крыльцо. Еще подумала, не забыл ли он горн у меня в кружке?"
    mi "Потому что если забыл, значит ему придется срочно бежать в кружок, а ключи только у меня, вожатой и Алисы. А ты же знаешь вожатую и Алису, и Максим их знает, значит, он прибежит сюда. И я хотела сразу вернуться в домик за ключами, и тут увидела на крыльце..."
    mi "Женечка, я думала, это мне, но это тебе. Мне бы ноты положили..."
    "Действительно, на столе рядом, лежали букет ромашек и чистый бланк библиотечного формуляра."
    show mi smile pioneer with dissolve
    mi "Женечка, я умываться. Тебя подождать?"
    mz "Н-нет. Ты иди, я попозже."
    hide mi with dissolve
    "А как только Мику ушла, Женя начала рвать этот формуляр. Пополам, потом еще пополам, потом еще, пока хватало сил в пальцах. Хотела выкинуть букет, но остановилась - цветы ни в чем не были виноваты. Поэтому в пустую молочную бутылку была налита вода, и цветы заняли место на столе."
    th "Сейчас приду в библиотеку, а там еще один букет. А после обеда или после ужина, записка, приглашение на свидание. А вечером, вместо свидания, будут всеобщие смех и веселье над одиноко сидящей Женей, ждущей непонятно чего."
    "Женя подошла к открытой дверце шкафа и посмотрела на себя в зеркало." #возможно, нужно цг, можно в фш сделать
    show dct_mz_mirror bukal with squares
    th "Потому что кто будет звать такую как я на свидание? А если и будет, то с какой целью? Посмеяться? А если действительно на свидание? Кому я нужна такая? Сыроежкину?"
    show dct_mz_mirror normal with dspr
    th "Плохо, если так. Надо признаться самой себе - он мне нравится. Но здесь есть миллион девочек красивее и лучше меня. Нет, именно потому что он мне нравится, ничего у нас не будет."
    th "Или это чернявый. Юморист из среднего отряда? Как его, Витька, кажется, пошутить решил? Какая разница? Может, мальчики тут и вовсе не причем. Девочки издеваются еще изобретательнее. Проходили, знаем."
    show dct_mz_mirror bukal with dspr
    "То, что все эти «проходили, знаем», были воспоминаниями других людей, прочитанные, обработанные и записанные системой в память заведующего библиотекой микса, ничего, опять же, не меняло."
    show dct_mz_mirror smile with dspr
    th "Ну хорошо, книги у меня никто не отнимет, в жизни моей ничего не поменялось, так что жаловаться не на что. Стоит еще опасаться испачканных дверных ручек, кнопок на стуле, украденной одежды на пляже..."
    th "А с соседкой мне все же повезло. Хоть от нее подлостей можно не ждать, а ее болтливость я переживу."
    hide dct_mz_mirror with squares
    "На линейку идти не хотелось совершенно, но Женя, решив, что слишком много чести будет для окружающих, если она начнет прятаться, пошла, сжав зубы."
    th "Я жду, что люди будут относиться ко мне так, как я к ним отношусь. Мне не надо, чтобы лучше. Мне достаточно так же."
    scene black with dspr
    window hide
    stop ambience fadeout 1
    
    $ renpy.pause(1.0)
    
    
    play ambience ambience_camp_center_day fadein 1
    window show
    # scene bg ext_square_day with dissolve
    scene dct_ext_square_profile_view_day with dissolve # Возможно, этот ракурс площади более уместен?
    "Максим, сидя напротив памятника, ждал начала линейки. Из-за новых обязанностей приходилось уже второе утро вставать на полчаса раньше и постоянно следить за временем."
    "Но зато эти обязанности освободили его от всех отрядных мероприятий и обеспечили пропуск в старший отряд."
    "Максим, правда, не понял еще, последнее - это хорошо или плохо: с одной стороны, никто не контролировал, как он проводит время и не заставлял ходить строем, а с другой - он уже дважды за сутки успел получить втык от вожатой, и от Алисы."
    stop ambience fadeout 1
    
    play music cambo_coffee fadein 1
    scene anim prolog_1 with dissolve
    show mt angry pioneer
    show dct_dream_veil at shiver
    with dissolve
    "От первой за то, что бездельничал:"
    mt "Ты, кажется, горнист?"
    th "Будто не сама же назначила."
    mt "Вот и занимайся, а не валяйся в домике. Или можешь со своим отрядом идти и к смотру строя и песни готовиться!"
    hide mt with dissolve
    "Вот уж от чего избавь бог, так это от этого смотра. Пришлось взять кассету и отправляться в музыкальный кружок, к Мику. А в кружке, в присутствии Алисы, назвал Ульяну - Ракетой. А что? Ее все так зовут. Оказалось, не все."
    show dv normal pioneer behind dct_dream_veil with dissolve
    dv "Понимаешь, Максим..."
    "Oбъяснила Алиса уже потом."
    dv "Tы у себя в отряде мог называть нас как хочешь, это было смешно, это вот как маленькие дети пришивают себе на рубашку нарисованные погоны и воображают себя генералами."
    dv "А вот когда взрослый выдает себя за генерала, ему приходится соответствовать. Или его за это наказывают. У нас погон нет, но эти прозвища - это как знаки отличия, которые заслужить надо."
    dv "Помнишь, я тебе про Семена и Сеньку вчера говорила. Так что и тебе надо еще «Макса» заслужить, и право звать Ульяну иначе, чем Ульяной, заработать."
    hide dv with dissolve
    show mi normal pioneer behind dct_dream_veil with dissolve
    "Мику, вот та оказалась гораздо более приветливой девушкой. Правда, настояла, чтобы Максим обязательно попробовал сыграть на... на корнете, так, кажется, а других духовых инструментов в кружке не оказалось."
    "Был еще тромбон, но его столько раз роняли, что однажды он отказался собираться, и так и лежал в кладовке в виде двух половин."
    "Оказалось, правда, что подавать сигналы с помощью горна - это одно, а играть, чтобы получалась музыка - совсем другое. Разница примерно как между художником и мастером нанесения дорожной разметки."
    stop music fadeout 1

    
    play ambience ambience_camp_center_day fadein 1
    scene bg ext_square_day
    show d_vi smile pioneer:
        xcenter 0.39
    show d_ka smile pioneer2:
        zoom 1.1 xcenter 0.2 ycenter 0.87
    with dissolve
    "Со стороны клубов на площадь вышли Катерина и Витька - сосед по домику. Почти рядом, почти держась за руки. Катерина опять мазнула глазами по Максиму и повернулась к Витьке, о чем-то его спрашивая."
    show d_ka smile pioneer2:
        ease 0.4 xcenter 0.24
    show d_ka laugh pioneer2 with dspr:
        ease 0.4 xcenter 0.24
    "Витька ответил, глупо улыбаясь, а Катя засмеялась, прикрывая рот тремя пальцами, а другой рукой взяла Витьку под локоть."
    scene dct_ext_square_profile_view_day with dissolve
    show d_vi smile pioneer:
        zoom 0.75 xcenter -0.15 ycenter 0.642
        easein 2.2 xcenter 0.68
    show d_ka laugh pioneer2:
        zoom 0.825  xcenter -0.26 ycenter 0.76
        easein 2.2 xcenter 0.57
    "Сердце дернулось, когда Максим перехватил взгляд Кати, но тут же и успокоилось, никак не реагируя на ее спутника."
    show d_ka artificial_smile pioneer2 with dspr
    "А Катя внезапно перестав смеяться и только продолжая растягивать губы в улыбке, смотрела холодными глазами, куда-то в сторону столовой."
    show d_ka smile pioneer2 with dspr
    pause 0.2
    show d_vi shock pioneer with dspr
    show d_ka smile pioneer2:
        easeout 1.8 zoom 1.1 xcenter 1.2 ycenter 0.87
    show d_vi shock pioneer:
        pause 1.2
        easeout 1.1 zoom 1.0 xcenter 1.2 ycenter 0.713
    "А потом вдруг отвернулась, что-то сказав Витьке такое, что его лицо удивленно вытянулось, высвободила руку, опять мазнула глазами по Максиму и величественно удалилась."
    "Максим повернулся, чтобы посмотреть, из-за чего так дернулась девочка, и улыбнулся, со стороны столовой к нему быстрым шагом шла Алиса."
    show dv normal pioneer:
        xcenter -0.2
        easein 1.2 xcenter 0.3
    dv "Ну, что расселся? Цигель ай-лю-лю! Труби сбор на линейку!"
    show black:
        alpha 0
        pause 0.5
        linear 1.5 alpha 1
    stop ambience fadeout 2.5
    "Максим побежал к флагштокам на свое место, и думал, почему он совершенно не обижается на Алису за ее, казалось бы, грубость, и вчера, и сегодня? Что-то чувствовалось хорошее за показной резкостью помощницы вожатой."
    
    scene black with dspr
    window hide
    $ renpy.pause(1.0)
    
    
    window show
    play music music_list['your_bright_side'] fadein 1 #+звуки озера
    play ambience ambience_lake_shore_day fadein 1
    
    # scene dct_ext_cliff_day with dissolve #УТОЧНИТЬ МЕСТО
    
    scene dct_ext_between_beach_and_boathouse_day with dissolve # Пока сделаем так. И вряд ли потом будем переделывать
    
    "Уже не первый час Электроник грустил на своем ставшем привычном месте: на берегу между пляжем и пристанью. Сидя на наполовину занесенном песком бревне бросал в воду камушки."
    "Было плохо. Кажется, Женя ему сегодня дала четко и ясно понять, что его общество ей неинтересно."
    "Когда последний камушек из пригоршни, булькнув, оставил после себя только расходящиеся по воде круги, Электроник пошарил вокруг себя, в поисках новой партии боеприпасов, но ничего подходящего поблизости уже не нашел, а вставать не хотелось из-за слабости в ногах."
    $ set_mode_nvl() 
    "Как до берега-то добрел, вместо того, чтобы на ближайшую скамейку не шлепнуться, непонятно."
    "Начиналось утро даже и романтично: пришлось подняться в пять утра, пройти на концертную площадку и там, за сценой, нарвать ромашек; спрятаться с букетом около домика Жени и, дождавшись, когда внутри начнут ходить туда-сюда обитательницы, положить букет на крыльцо, приложив к нему украденный бланк библиотечного формуляра, чтобы девочки не гадали, кому букет."
    "Страшно было не то, чтобы просто подойти к Жене и заговорить с ней. Страшно было даже идти с букетом к ее домику. Вдруг увидят. Мысли: \"Ну увидят, и что?\" - даже не возникало."
    "Электроник переждал, спрятавшись в кустах, пробежавшую мимо Сашу, положил, практически кинул, букет на крыльцо и едва успел спрятаться обратно, когда из домика вышла Мику. После этого кибернетик, пятясь задом, отполз подальше, на тропинку, и там, уже не таясь, встал и направился к умывальникам. Сердце колотилось как бешеное, а в голове крутилась мысль, что он сделал все что мог."
    
    nvl hide dissolve
    nvl clear
    nvl show dissolve
    

    scene dct_lineup with blinds    # В моде используем касмтомную линейку
    "На линейку Шурик опять не пошел, сославшись на занятость, и Электронику пришлось одному представлять там кружок. Женя только холодно кивнула на его робкое приветствие, занимая своё место в строю, но в тот момент это кибернетика нисколько не обескуражило."
    "Потом вожатой было объявлено, что сегодня «День чистоты и порядка», что пионерам предстоит навести эти самые чистоту и порядок на территории лагеря, что кибернетики должны будут навести порядок в кружке, подмести дорожки и пройти с граблями газоны вокруг кружка."
    "Это Электроник слушал краем уха, фронт работ был ясен и так. Жене досталась библиотека и все вокруг нее. \"Вот и прекрасный повод. Пойду и помогу\", - думал влюбленный кибернетик."
    scene bg ext_library_day with dissolve
    pause 0.25
    scene bg int_library_day
    show mz normal pioneer
    with dissolve
    "И вот, когда Электроник, где-то через час после завтрака, в нервной трясучке прибежал к библиотеке, и... Да что теперь вспоминать? В общем, отлуп, бессмысленный и беспощадный. «За книгами - приходи, раз уж записывался», — сказано было вдогонку, равнодушно, как к стотысячному посетителю за сегодня. Это если без подробностей, которые вспоминать не хотелось."
    $ set_mode_adv()
    # scene dct_ext_cliff_day with dissolve
    scene dct_ext_between_beach_and_boathouse_day with dissolve # Переделано, как и ранее
    "Надо было чем-то занять себя. Идеальным было бы запереться в кружке и там что-то мастерить, пока не упадешь от усталости, но для этого надо было пересекать территорию лагеря, а в кружке был еще и Шурик, - людей же видеть не хотелось вовсе, никаких."
    "Под ногами валялся принесенный водой кусок сосновой коры. «Тоже занятие». Электроник достал из кармана перочинный нож и, примерившись, начал неспешно вырезать из коры кораблик."
    play sound dct_sfx_horn_dinner_through_loudspeaker  # Горн на обед отыгрываемый Максимом
    "Прозвучал где-то далеко сигнал горна, зовущий в столовую. На самом деле не «где-то», а здесь же, рядом, из рупора на пристани, но так показалось. Прозвучал и был проигнорирован - идти в столовую и встречаться там с Женей не хотелось."
    "Слишком больно. Никогда раньше в прошлых циклах ни Женя так себя не вела, ни Электроник так не переживал. Что-то сместилось в них, когда Рыжие в автобусе пересадили полусонного Электроника на освободившееся место рядом с Женей, а те проснулись на плечах друг у друга."
    "Сергей вздохнул, вспомнив Женю. Как она ровным, тихим и спокойным голосом, без обычных сварливо-ехидных ноток, вообще без всяких интонаций, попросила его уйти и не тратить свое время, и опять взялся за поделку."
    #scene int_house_of_sl_day
    scene int_house_of_un_day   # В этом лагере девочка Саша живёт с Леной
    show un grin pioneer far:   # Добавляем Лену, чтоб прикрыть немного кровать с чулком Мику
        xcenter 0.75
    show 3500_sl smile3 pioneer:    # Двигаем Сашу, чтоб разнести их с Леной
        xcenter 0.35 ycenter 0.833
    show dct_dream_veil at shiver
    with dissolve
    "Саша, однажды, в один из циклов, похвалила его, когда он помогал с какой-то мелочью девочкам в домике:"
    sa "Сережа, у тебя очень умные руки. Как бы мы тебя не отвлекали, они все делают сами, правильно и красиво."
    # scene dct_ext_cliff_day with dissolve
    scene dct_ext_between_beach_and_boathouse_day with dissolve # Переделано, как и ранее
    "Вот и сейчас из большого куска коры выходил не просто кораблик, какие дети пускают по лужам. Сергей особо не интересовался кораблями, но конечно видел фотографии и бывал в кино, а от чертежей он никогда не отворачивался."
    "Поэтому то, что вышло из под его ножа больше всего походило на «Эспаньолу» из «Острова сокровищ», с намеченными досками обшивки, окнами на корме и трюмным люком, с палубой, опущенной ниже уровня бортов."
    "Электроник повертел бывший кусок коры в руках, отложил его в сторону и отправился к ближайшим кустам, чтобы срезать подходящие сучки - заготовки для будущих мачт и бушприта."
    th "Что вот с ней делать, когда будет готова? В кружке оставить - это только позориться - я могу лучше сделать; подарить кому из малышей - нет у меня друзей среди них. Сейчас доделаю и оставлю на пристани. Кто найдет - того и будет."
    stop music fadeout 2.5
    play ambience ambience_boat_station_day fadein 1.5 
    # show d_oz normal pioneer at fleft with dissolve:
        # zoom 0.75 yalign 0.1                            # Позиция высоты Оксаны для длальности far
    show d_oz normal pioneer with dissolve:
        xcenter 0.31 yalign 0.28                            # Убрал зум, и чуток подвинул Оксану. Типа сидит на бревне
        
    "Электроник оглянулся на берег, на его месте сидела девочка и вертела в руках недоделанную «Эспаньолу»." 
    th "Оксана, кажется. Из младшего отряда. Вчера, когда я был на поле, они с физруком что-то обсуждали. Вот и подарю ей."
    show d_oz d pioneer with dissolve
    el "Нравится?"
    d_oz "Да, очень все здорово. Скажи, а ты научишь меня такие делать? Это же парусник?"
    el "А зачем тебе учиться? Этот забирай, только подожди, я его доделаю."
    d_oz "Нет, я хочу обязательно сама сделать."
    "Они еще долго сидели рядышком. Сергей выстругивал мачты и пристраивал их к корпусу, объясняя, что и зачем он делает. Потом: \"Ты же девочка, у тебя должны быть с собой иголка и нитки\", - натягивал ванты. А девочка сидела рядом и внимательно следила за руками кибернетика."
    el "Ну вот, осталось паруса привязать, и все будет готово. По воде его не пустить - опрокинется, но на полку можно поставить. Может, все-таки заберешь его себе?"
    d_oz "А можно? Но я все равно хочу сама сделать."
    "Электроник задумался. Как-то так выходило, что свободного времени, даже с учетом занятий в кружке, получилось с избытком."
    el "Тогда приходи завтра в кружок. Или нет, там мы Шурику будем мешать. Приходи сюда. И, если не тайна, скажи, зачем тебе это?"
    d_oz "Понимаешь..."
    show d_oz n pioneer with dissolve
    "Девочка испытующе посмотрела на кибернетика, потом, решив, что он заслуживает доверия, кивнула сама себе."
    d_oz "Есть один мальчик, который бредит парусами. А после этой смены он всё... в общем, мы можем больше не встретиться. Я все думала, что ему подарить, чтобы он... ну, на память."
    d_oz "И вот тебя увидела, и придумала. Только надо обязательно, чтобы я сама все сделала, своими руками, а то все пропадет."
    window auto
    "Ясности это не добавило, но стало понятно что Оксана настроена серьёзно."  # Пришлось воткнуть отсебятину по весьма интересной причине. Дальше нужен режим NVL, а чтобы интерфейс не пропадал при смене сцен, нужно задать window show. И в таком режиме интерфес NVL плавно проявится только если предыдущая реплика была от стандартного персонажа БЛ, или от автора. Если же реплика была от кастомного персонажа, то сначала на секунду врубится интерфес ADV, и только потом он переключится в NVL, а подобое безобразие рушит всю атмосферу.
    stop ambience fadeout 1
    scene black with dissolve_fast
    pause 0.5
    #window hide

    ##################################################################
    # show dct_int_coaching_room2 with dissolve #уточнить фон
    # $ set_mode_nvl()
    # "Алиса, как всегда в этот день, командовала средним отрядом. Сорок один бестолковый пионер должен был подмести дорожки, пристань и автобусную остановку, подкрасить гипсовых пионеров у ворот, а если нужно, то и сами ворота, ничего не поломать и не перемазаться в краске сами. Как правило, к четырем часам все заканчивали, но приходилось устраивать дополнительный банный день, особенно для мальчиков, возможностей рукомойника явно не хватало."
    # mt "А ты то почему устала? Не ты же с метлой танцевала? Назначь участки, обозначь фронт работ, и только ходи и контролируй." #кто это говорит? это Ольга, кто ж ещё)
    # "Вы не пробовали проконтролировать сорок одну Ульянку сразу? Нет, не сорок одну, конечно, Ульянка, она была уникум в этом плане, но у нее уже в том возрасте были мозги. А у этих...{w} Вот и приходится носиться по всей территории, пресекать, где надо, и заставлять тоже, где надо. Да иногда за кусты отзывать и физическим воздействием угрожать. Тогда понимают какое-то время.{w} И это еще они спят и алгоритмами управляются. А что будет, когда они проснутся?{w} А тут еще Катька откровенный саботаж начала и других к тому же подбивала."
    ##################################################################

    
    $ set_mode_nvl()
    window show
    play music music_list['everyday_theme'] fadein 0.5
    show dv angry pioneer:
        xcenter 0.5 alpha 0
        linear 1 alpha 1
    "Алиса, как всегда в этот день, командовала средним отрядом."
    show ext_houses_day:
        align(0.0, 1.0) alpha 0
        linear 0.8 alpha 0.5
        easein 0.8 zoom 0.5 alpha 1.0
    show ext_boathouse_day:
        align(1.0, 1.0) alpha 0
        pause 1
        linear 0.8 alpha 0.5
        easein 0.8 zoom 0.5 alpha 1.0
    show ext_no_bus:
        align(0.0, 0.0) alpha 0
        pause 2
        linear 0.8 alpha 0.5
        easein 0.8 zoom 0.5 alpha 1.0
    show ext_camp_entrance_day:
        align(1.0, 0.0) alpha 0
        pause 3
        linear 0.8 alpha 0.5
        easein 0.8 zoom 0.5 alpha 1.0
    window show
    "Сорок один бестолковый пионер должен был подмести дорожки, пристань и автобусную остановку, подкрасить гипсовых пионеров у ворот, а если нужно, то и сами ворота, ничего не поломать и не перемазаться в краске сами."
    show dct_ext_bathhouse_day with irisin
    "Как правило, к четырем часам все заканчивали, но приходилось устраивать дополнительный банный день, особенно для мальчиков, возможностей рукомойника явно не хватало."
    show mt smile panama pioneer far:
        xcenter 1.15 ycenter 0.55
        easein 1.5 xcenter 0.5
    mt "А ты то почему устала?"
    show mt grin panama pioneer far with dissolve_fast
    extend " Не ты же с метлой танцевала?"
    show mt laugh panama pioneer far with dspr
    show mt laugh panama pioneer far:
        pause 0.5
        easein 1.5 xcenter -0.15
    extend " Назначь участки, обозначь фронт работ, и только ходи и контролируй."
    
    nvl hide dissolve
    nvl clear
    nvl show dissolve
    
    scene black
    show dv sad pioneer
    with dissolve
    "Вы не пробовали проконтролировать сорок одну Ульянку сразу?"
    show dv guilty pioneer with dspr
    extend " Нет, не сорок одну, конечно, Ульянка, она была уникум в этом плане, но у нее уже в том возрасте были мозги. А у этих..."
    show dv angry pioneer with dissolve_fast
    "Вот и приходится носиться по всей территории, пресекать, где надо, и заставлять тоже, где надо."
    show dv rage pioneer as dv2 behind dv
    show d_ext_dv_hideout_day_7 behind dv2:
        alpha 0
        linear 1 alpha 1
    show dv angry pioneer:
        pause 1.2
        linear 0.2 alpha 0
    extend " Да иногда за кусты отзывать и физическим воздействием угрожать. Тогда понимают какое-то время."
    scene black
    show dv scared pioneer
    with dissolve_fast
    "И это еще они спят и алгоритмами управляются. А что будет, когда они проснутся?"
    show dv sad pioneer with dissolve_fast
    extend " А тут еще Катька откровенный саботаж начала и других к тому же подбивала."
    show dct_int_coaching_room3 behind dv:
        zoom 2.5 xcenter 0.5 ycenter 0.475 alpha 0
        linear 1 alpha 1
    stop music fadeout 1
    play ambience ambience_int_cabin_day fadein 1
    $ set_mode_adv()
    
    ##################################################################
    # stop music fadeout 1
    # play ambience ambience_int_cabin_day fadein 1
    # $ set_mode_adv()
    # scene dct_int_coaching_room2
    # show d_us sad sport:
        # zoom 0.825  xcenter 0.33 ycenter 0.76
    # show d_sf normal sport:
        # # zoom 0.825  xcenter 0.66 ycenter 0.76     # Мой косяк((, ycenter у Семёна должен быть 0.56 
        # zoom 0.825  xcenter 0.66 ycenter 0.56
    # with dissolve
    # "В общем, Алиса набегалась за сегодня на неделю вперед и сейчас сидела в тренерской у Семена и Ульяны, вытянув ноги и опиваясь чаем."
    # "У Алисы много чего крутилось на языке с прошлого цикла, но спрашивать и самой рассказывать она пока не была готова. Поэтому спросила о другом, о волейболе. И, когда все с энтузиазмом подхватили идею, а Ульяна умчалась с загоревшимися глазами:"
    # "У Алисы много чего крутилось на языке с прошлого цикла, но спрашивать и самой рассказывать она пока не была готова. Поэтому спросила о другом, о волейболе."
    # "Все с энтузиазмом подхватили идею, а у Ульяны даже глаза загорелись:"
    # us "А в самом деле, давно не играли, с прошлого цикла! Алиса, беги переодевайся, я за Леной, Мику и Сашей сбегаю."
    # "Только тогда Алиса решилась чуть приоткрыться."
    # dv "Сенька, я понимаю, почему ты ничего про Шлюз не рассказываешь. Но знаешь, как мне теперь перед тобой стыдно."
    # show d_sf normal sport with dissolve:
        # zoom 0.825  xcenter 0.66 ycenter 0.76
    ##################################################################
    
    "В общем, Алиса набегалась за сегодня на неделю вперед и сейчас сидела в тренерской у Семена и Ульяны, вытянув ноги и опиваясь чаем."
    scene dct_int_coaching_room2
    show d_us sad sport:
        zoom 0.825  xcenter 0.31 ycenter 0.78
    show d_sf normal sport:
        zoom 0.825  xcenter 0.69 ycenter 0.58
    with dissolve
    "У Алисы много чего крутилось на языке с прошлого цикла, но спрашивать и самой рассказывать она пока не была готова. Поэтому спросила о другом, о волейболе."
    show d_us smile sport
    show d_sf smile sport
    with dspr
    "Все с энтузиазмом подхватили идею, а у Ульяны даже глаза загорелись:"
    show d_us laugh sport with dspr
    us "А в самом деле, давно не играли, с прошлого цикла! Алиса, беги переодевайся, я за Леной, Мику и Сашей сбегаю."
    hide d_us with dissolve
    "И только тогда Алиса решилась чуть приоткрыться:"
    dv "Сенька, я понимаю, почему ты ничего про Шлюз не рассказываешь. Но знаешь, как мне теперь перед тобой стыдно."
    show d_sf normal sport with dissolve
    me "Алиса, я не..." #спрайт Семёна
    dv "Да знаю я, что ты \"не\". Мне не за то стыдно, что ты мог меня голой увидеть, хоть ты и говоришь что \"не\". Я тебе доверяю. А за то своё скотское состояние.{w} Ладно, побегу переодеваться. Через полчаса на площадке."
    stop ambience fadeout 1
    show black with dissolve
    window hide
    
    $ renpy.pause(1.0)
    play sound_loop ambience_camp_center_day fadein 1
    window show
    # show d_ext_dv_hideout_day_7 with dissolve #возможно другой фон
    show dct_ext_glade_behind_clubs_day with dissolve # Пусть будет такой.
    "В кружке кибернетики не было никого. Сыроежкин с Оксаной разговаривали на берегу о романтических отношениях между мальчиками и девочками, и женской и мужской психологиях."
    "Поскольку познания у них были исключительно сказочные, то разговор был интересен для обоих, несмотря на почти двукратную разницу в возрасте."
    
    # "А Шурик, обойдя здание клубов, сидел у его задней стены, спрятавшись ото всех между стеной и кустами барбариса, и пытался сосредоточиться и составить план работы кружка кибернетики. Получалось плохо." # По канону
    
    "А Шурик, обойдя здание клубов, сидел у его задней стены, спрятавшись ото всех за кустами барбариса, и пытался сосредоточиться и составить план работы кружка кибернетики.{w} Получалось плохо." # Просто "за кустами барбариса", т.к. они справа.
    stop sound_loop fadeout 1
    play ambience ambience_int_cabin_day fadein 1 #звук клубов + не нравится структура
    scene bg int_clubs_male_day with blinds
    "После того, как они с Сергеем утром протерли пыль со всех приборов и Сергей отпросился в библиотеку, Шурик, оставшись один, достал из ящика стола общую тетрадь, достал остатки второй версии кошкоробота, вытащил на верстак все имеющиеся в наличии радиодетали, болты, гайки и всякий механический хлам, и замер."
    "Нет, конечно, с самого начала было ясно, что из этого барахла ничего хорошего создать не удастся, а тем более, вторую Яну."
    "Они и первую непонятно как создали, выпаивая детали из списанных приборов."
    "Шурик посмотрел на туловище кошкоробота, на отдельно лежащую голову. Тут же, в тетрадке, набросал техзадание. Представил себе алгоритмы поведения робота. Потом взгляд его упал на кучку печатных плат - некондиционную продукцию предприятия-шефа."
    #"Подумал:" # Можно опустить
    "\~ Да, чтобы из этого сделать что-то достойное, нужно быть гением. \~{w}\nИ тут же получил ответ:\n«Не обязательно. Но из этого, действительно, ничего сложнее ультразвуковой пугалки - мышей гонять, не спаяешь.»"
    "И опять Шурик сначала мысленно согласился, а потом уже вздрогнул в испуге."
    th "Надо, надо к доктору" 
    "...Подумалось уже второй раз. И после этого не думалось ничего."

    window hide
    stop ambience fadeout 1
    scene bg ext_dining_hall_away_day with dissolve
    play sound_loop ambience_dining_hall_full
    scene bg int_dining_hall_people_day with dissolve
    $ renpy.pause(1.2, hard=True)
    stop sound_loop fadeout 1
    scene bg ext_clubs_day with dissolve
    play ambience ambience_int_cabin_day fadein 1
    scene bg int_clubs_male_day with dissolve
    "Шурик сходил на обед, удивился, не встретив там Сыроежкина и опять вернулся в кружок к прерванному занятию. Ничего не получалось, хоть плачь."
    "С такой ситуацией Шурик еще не сталкивался. Стоило занести ручку над тетрадным листом, как в голове, всплывала эта фраза:"
    th "Ничего сложнее пугалки не спаяешь."
    th "Звучит словно проклятие. Mожет, зря я Сергея отпустил? Может, с ним вдвоем что-нибудь и придумали бы?"
    stop ambience fadeout 1
    play sound dct_sfx_robot_run_club_attic fadein 1.5
    pause 0.8
    stop sound fadeout 1.5
    "Опять кто-то легкий пробежал по краю крыши, на Шурика сверху посыпался мусор, но задремавший Шурик не отреагировал."
    play music music_list['afterword'] fadein 1
    scene black with dissolve
    "Сон на этот раз был несвязный. Может быть там, во сне, внутренняя логика и присутствовала, но вот запомнился он разрозненными фрагментами."
    scene dct_dream_sh_first_disert
    show dct_dream_veil at shiver
    with dissolve
    "Вот полигон, где падают с неба горящие мишени и догорают уже на земле."
    "Вот тот же самый полигон, только по нему бегает маленький, с пятилетнего ребенка, алюминиевый человечек. Все военные недоуменно переглядываются, а Сыроежкин стоит тут же, у раскрытого ящика, и довольный улыбается."
    scene dct_ext_busstop_summer #нужны другие спрайты (переодеть), эти не подходят под описание
    show d_jnjn:
        zoom 0.3 xcenter 0.47 ycenter 0.66
    show dct_dream_veil at shiver
    with dissolve
    "А вот автобусная остановка, на которой стоят удивительно знакомые женщина, лет тридцати пяти и десяти-одиннадцатилетняя девочка. Женщина, по случаю прохладной погоды, в легком свитере, а девочку заставили надеть куртку, чем она крайне недовольна."
    "Они удивительно похожи друг на друга: легкие как пух волосы, которые шевелит слабый ветерок, у женщины пепельные, а у девочки еще природные - светло-русые; большие голубые глаза; маленькие, чуть вздернутые носики; рты чуть шире, чем нужно по идеальным пропорциям; острые подбородки."
    "Они обе заметили Шурика, но стоят и не шевелятся, девочка радостно улыбается, а женщина пытается спрятать улыбку под строгой маской."
    "Во сне Шурик знает, что их зовут Яна и Яна. Во сне Шурик знает, что сами они не заговорят - к ним надо подойти, знает и то, что не надо к ним подходить. Но сдерживаться нет сил и Шурик подходит."
    d_jn_old "Здравствуй, Саша."
    "Женщина смотрит в глаза Шурику и по ее щеке стекает слеза. Только что сдерживала улыбку и вот уже плачет. "
    d_jn_old "Ты опять нарушил своё же обещание."
    d_jn_young "Привет, Па! Как тебя долго не было! А мы сейчас к бабушке едем!"
    "Шурик не отвечает, только молча смотрит на них обеих, на то, чтобы молчать, у него ещё хватает сил. Тогда опять говорит женщина."
    d_jn_old "Пожалуйста, не приходи больше. И не пытайся нас вызывать. Все, что было в системе, ты уже вытащил. А сейчас... Даже то, что мы с тобой говорим - это только твоё воображение."
    "Из-за угла слышен шум мотора. Как обычно в этом сне. Пора расставаться. И Шурика, независимо от его желания, относит от обеих Ян."
    stop music fadeout 1 
    scene anim prolog_1 with dissolve
    play sound dct_sfx_horn_dinner_through_loudspeaker
    $ day_time()                        # Прописано, чтобы при обратной перемотке цвета не сбивались.
    $ persistent.sprite_time = "day"
    "Сквозь сон Шурик слышит горн - сигнал к ужину, и тут же Шурика будит пришедший за ним Сыроежкин."
      
    window hide
    
    
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    window auto
    
    play ambience ambience_camp_center_evening fadein 1
    scene dct_ext_glade_behind_clubs_sunset # Вечерняя версия
    show el laugh pioneer close
    with dissolve
    "Какое-то время сон еще сохраняется в памяти, но через несколько минут уже все забывается."
    th "Ультразвуковая пугалка?"
    "Aзартно подумал Шурик.{w} Оцепенение последних двух дней сняло как рукой. "
    show el smile pioneer close with dissolve_fast
    th "А если не пугалка?"
    "\"Ай!\" - Кто-то испуганно воскликнул в его голове." with hpunch #слегка дёрнуть экран
    sh "Сергей, a не замахнуться ли нам на тайны мозга? Что ты знаешь об ультразвуке?"
    show el surprise pioneer close with dissolve_fast
    stop ambience fadeout 1
    show black with dissolve
    window hide
    $ renpy.pause(1.0)
    
    
    window show
    play ambience ambience_dining_hall_full
    scene dct_int_dining_hall_table_sunset_back_people
    show dct_int_dining_hall_d_ka blur:
        xcenter 0.35 ycenter 0.35
    show dct_int_dining_hall_table_sunset_chairs
    show mt smile pioneer close:
        xcenter 0.74
    show dct_int_dining_hall_table_sunset_front
    with dissolve
    mt "Все готово к дискотеке?"
    "Hаклонившись, чтобы не повышая голоса пробиться сквозь шум столовой, спросила вожатая."
    "Алиса легкомысленно, соответственно настроению, пожала плечами."
    dv "Не знаю, Ольга Дмитриевна. А что к ней может быть не готово?"
    scene dct_int_dining_hall_table_sunset_back:
        xzoom -1
    show dct_int_dining_hall_table_sunset_chairs:
        xzoom -1
    show dv grin pioneer close:
        xcenter 0.58 ycenter 0.41
    show dct_int_dining_hall_table_sunset_front:
        xzoom -1
    with dissolve
    "Вообще, день сегодня оказался неплохим. Даже несмотря на необходимость пасти средний отряд на уборке и намеченный на \"когда будет время, но не позже воскресенья\" разговор с Леной и Персуновыми: всем четверым проснувшимся обитателям лагеря было что обсудить с прошлого цикла."
    "Главное - исчезла серость и скука, засасывавшая в себя Алису, когда хотелось забыть прошлый цикл и позапрошлый, и поза-позапрошлый...{w} Так и заканчивается активная фаза."
    mt "...Алиса, ты вообще меня слушаешь?"
    show dv surprise pioneer close with dissolve_fast
    "Ольга, оказывается, что-то говорила. "
    scene dct_int_dining_hall_table_sunset_back_people
    show dct_int_dining_hall_d_ka blur:
        xcenter 0.35 ycenter 0.35
    show dct_int_dining_hall_table_sunset_chairs
    show mt normal pioneer close:
        xcenter 0.74
    show dct_int_dining_hall_table_sunset_front
    with dissolve
    mt "Как тебе наш горнист? Готов он к самостоятельной жизни?"
    dv "Ольга Дмитриевна, мы сами себе до субботы сроку дали - присмотреться, а вы уже сегодня меня спрашиваете. Похоже, что готов."
    show mt normal pioneer close:
        linear 0.5 blur 10
    show dct_int_dining_hall_d_ka sharp with dissolve_fast
    "Алиса поймала неприязненный взгляд Кати и еще раз подтвердила."
    dv "Да, считаю что готов. "
    th "Пусть Катька позлится."
    show mt normal pioneer close:
        linear 0.5 blur 0
    show dct_int_dining_hall_d_ka blur with dissolve_fast
    mt "Тогда я не вмешиваюсь. Все на ваше усмотрение."
    show mt normal pioneer close:
        linear 0.5 ycenter 0.23
        pause 0.15
        linear 0.9 xcenter 1.25
    "Вожатая кивнула и, по привычке появившейся в этом цикле, взяв компот ушла за соседний столик, чтобы успеть обсудить там еще какую-нибудь вожатскую проблему."
    "А Алиса, не удержавшись и наплевав на манеры, нацепила на вилку кусок хлеба и принялась вычищать тарелку от остатков соуса. Ужин у поваров сегодня получился превосходным, что после смерти бабы Глаши было скорее исключением."
    th "Я с ней и не общалась почти. Но это был последний человек из пришедших снаружи. Они там, снаружи, ничем не лучше нас, я уверена. Но все равно, что-то мы потеряли." 
    show dct_int_dining_hall_d_ka sharp with dissolve_fast
    "Опять перехватила неприязненный взгляд Кати. Что такое? Нет, ревнует, это понятно, но вот конкретно сейчас?{w} А это, оказывается, Максим вопросительно смотрит на Алису из-за своего столика. Понятно, хочет что-то узнать, а спросить не решается. Странно, за сутки с небольшим близкого знакомства он не показался Алисе особо стеснительным."
    show mi normal pioneer behind dct_int_dining_hall_table_sunset_chairs with MoveTransition(1.5, enter=_moveleft, enter_time_warp=_ease_time_warp):
        xcenter 0.38 ycenter 0.29
    mi "Алисочка, можно к тебе подсесть?"
    scene dct_int_dining_hall_table_sunset_back:
        xzoom -1
    show dct_int_dining_hall_table_sunset_chairs:
        xzoom -1
    show dv grin pioneer close:
        xcenter 0.58 ycenter 0.41
    show dct_int_dining_hall_table_sunset_front:
        xzoom -1
    with dissolve
    "Подошла задержавшаяся у себя в кружке Мику и, не дожидаясь разрешения, поставила на столик поднос с ужином."
    "Присела, огляделась вокруг, разулыбалась и помахала рукой проходящему мимо Максиму."
    show dv sad pioneer close with dissolve_fast
    extend " А Алису это почему-то неприятно кольнуло."
    show dv shy pioneer close with dspr
    th "Ты что, Рыжая, ревнуешь что ли? Вы еще женскую драку с Катькой устройте. То есть, с Мику, а Катька будет драться с победительницей. Вот шоу-то будет!"
    scene dct_int_dining_hall_table_sunset_back_people
    show dct_int_dining_hall_d_ka blur:
        xcenter 0.35 ycenter 0.35
    show dct_int_dining_hall_table_sunset_chairs
    show mi smile pioneer close:
        xcenter 0.4 ycenter 0.42
    show dct_int_dining_hall_table_sunset_front
    with dissolve
    mi "Знаешь, Алисочка, я вчера пыталась заниматься с Максимом, думала, раз он умеет на горне играть, то может, он и на корнете сумеет. Но оказалось, что музыка у него не получается. За что ни возьмется, все время сигналы выходят какие-то."
    show mi laugh pioneer close with dissolve_fast:
        xcenter 0.425
    "Мику хихикнула."
    show mi smile pioneer close with dissolve_fast:
        xcenter 0.4
    mi "А вообще, мальчик очень хороший. Он похож на молодую породистую собачку, которая уже выросла из щенков, но все еще хочет поиграть. Алисочка, а правда, он будет жить у тебя в домике?"
    scene dct_int_dining_hall_table_sunset_back:
        xzoom -1
    show dct_int_dining_hall_table_sunset_chairs:
        xzoom -1
    show dv shocked pioneer close:
        xcenter 0.58 ycenter 0.41
    show dct_int_dining_hall_table_sunset_front:
        xzoom -1
    with dissolve
    "Алиса даже поперхнулась."
    dv "А? С чего ты взяла?"
    mi "Ну все так говорят. Нет, конечно, это неправильно, когда мальчика селят в одном домике с девочкой..."
    show dv shy pioneer close with dissolve_fast
    extend " Наверное?"
    "Алиса уже собралась сказать, что где Максим будет жить, пусть думает Ольга Дмитриевна, но, скорее всего, Алисе придется перебраться к вожатой в домик, а Максима поселят в ее домике, вместе с опоздавшим пионером, или же Алису оставят на старом месте, а Максим так и будет жить, вместе с Витькой."
    scene dct_int_dining_hall_table_sunset_back_people
    show dct_int_dining_hall_d_ka blur:
        xcenter 0.35 ycenter 0.35
    show dct_int_dining_hall_table_sunset_chairs
    show mi shy pioneer close:
        xcenter 0.425 ycenter 0.42
    show dct_int_dining_hall_table_sunset_front
    with dissolve
    "Но тут увидела еле сдерживаемую улыбку в глазах Мику."
    dv "Точно. Так и будет."
    "И вспомнив вчерашнюю реплику Семена, добавила."
    dv "Мы только что с вожатой составили список, чему именно из «плохого» я буду его учить."
    show mi laugh pioneer close with dspr
    "Вот теперь рассмеялись уже обе."
    mi "Алисочка..."
    scene dct_int_dining_hall_table_sunset_back:
        xzoom -1
    show dct_int_dining_hall_table_sunset_chairs:
        xzoom -1
    show dv laugh pioneer close:
        xcenter 0.58 ycenter 0.41
    show dct_int_dining_hall_table_sunset_front:
        xzoom -1
    with dissolve
    "Мику продолжала смеяться, но говорила серьезно."
    mi "Максим хочет тебя вечером пригласить на танец, но стесняется."
    show dv surprise pioneer close with dissolve_fast
    extend " Не отказывай ему, пожалуйста."
    show dv shy pioneer close with dissolve_fast
    show dv shy pioneer close:
        linear 0.4 ycenter 0.23
        pause 0.12
        easeout 1.2 xcenter -0.25
    pause 0.8
    scene dct_int_dining_hall_table_sunset_back_people
    show dct_int_dining_hall_d_ka blur:
        xcenter 0.35 ycenter 0.35
    show dct_int_dining_hall_table_sunset_chairs
    show mi shocked pioneer close:
        xcenter 0.425 ycenter 0.42
    show dct_int_dining_hall_table_sunset_front
    with dissolve_fast
    dv "Пока, Мику. На площади увидимся."
    show mi grin pioneer close with dissolve_fast:
        xcenter 0.4 ycenter 0.42
    $ sunset_time()                        # Прописано, чтобы при обратной перемотке цвета не сбивались.
    $ persistent.sprite_time = "sunset"
    "Покрасневшая Алиса предпочла закончив с ужином отправиться по своим делам, отмолчавшись на просьбу японки."
    stop ambience fadeout 1
    
    
    
    
    scene black with dspr
    window hide
    $ renpy.pause(1.0)
    
    $ night_time()
    $ persistent.sprite_time = "night"
    
    play ambience ambience_library_day fadein 1
    window show
    scene bg int_library_night with dissolve #приглушённый звук дискотеки
    # $ renpy.music.set_volume(0.05, delay=0)
    $ renpy.music.set_volume(0.08, delay=0)
    play music music_list["lightness_radio_bus"] fadein 3
    "На дискотеку Женя, конечно же, не пошла. Она никогда не ходила на первую дискотеку цикла, всегда оправдываясь тем, что не успела еще навести порядок в библиотеке, а на самом деле стесняясь своей внешности, неуклюжести, неумения танцевать и, главное, чувствуя себя крайне неуютно в такой толпе." 
    th "Зря я сюда приехала. Дома, по крайней мере, никто не требует \"обязательно участвовать в общелагерных мероприятиях\", и можно не чувствовать себя клоуном. Завтра начнут подкладывать кнопки на стул, начнут мазать край стола мелом. И всем будет смешно."
    th "Одни будут травить активно, другие следить с интересом за происходящим, третьи просто не будут вмешиваться, предоставляя возможность выпутываться самой. Это одно."
    th "А другое, как дать понять нравящемуся самой мальчику, что он зря теряет время? А из лагеря просто так не уедешь, эти две недели надо просто пережить."
    "Женя втянула воздух через сжатые зубы и щелкнула выключателем."
    play sound sfx_clench2  #play sound #выключатель fadein 1.5
    pause 0.2
    scene bg int_library_night2 with dspr
    pause 0.2
    un "Ой!"
    mz "Кто здесь?"
    show un surprise pioneer far with dissolve
    th "Лена. Как с ней обходиться?"
    un "Женя, это я. Я зашла, чтобы взять что-нибудь почитать. Дверь открыта была, а тебя не было, и я задремала в кресле."
    mz "Вообще-то библиотека уже закрыта. Но бери, раз уж пришла."
    un "Спасибо, я уже. Вот..."
    show un shy pioneer far with dissolve
    "Лена протянула книгу и покраснела."
    un "А тебя ждала, чтобы ты записала."
    hide un with dissolve
    "Женя, не садясь, записала в формуляр книгу, заперла за Леной дверь и только тогда позволила себе все проверить."
    "Нет, никаких «сюрпризов» в библиотеке после посещения Лены не оказалось, ни подпиленной ножки стула, ни воды, налитой в один из ящиков стола, ничего."
    "Женя от Лены гадостей и не ждала, но запустившаяся программа требовала всё проверить. Хотя нет, один сюрприз был."
    "В кресле, в котором дремала Лена, осталась после нее серая канцелярская папка-скоросшиватель, родная сестра тех, что были свалены кучкой на нижней полке одного из библиотечных стеллажей."
    "Женя переложила папку к себе на стол, а сама, походив между шкафов и выбрав книгу,уселась в то же кресло, закинув ноги на журнальный столик."
    "С площади доносилось только приглушенное «бум-бум-бум». Верхний свет Женя погасила, остался только тот, что проникал снаружи, от уличных фонарей, и настольная лампа, казалось бы - сиди и читай, тем более, что Женя любила это дело. Она даже про свои горести почти забыла. Но книжка не шла."
    "Во-первых, все время казалось, что Женя ее уже читала, и не один раз, всегда, с первых же слов, угадывая содержание очередного абзаца."
    "А, во-вторых, вспоминался сегодняшний день, начиная с букета. И как больно было прогонять Сыроежкина. Нет, книжкой не отвлечешься. На глаза попалась Ленина папка."
    "Было бы письмо, Женя бы не полезла, медицинские карточки выглядят совсем не так, надписи «Личное дело» на обложке тоже не было. А была надпись «Костер», красным фломастером. Женя откинула обложку."
    "Самым верхним был вложен рисунок цветными карандашами:"
    "Лесная поляна, окруженная кустами; посреди поляны догоревший, но еще дымящийся костер; вокруг костра лежат буквой П три больших бревна; на бревнах и рядом с бревнами всякие мелкие и не очень предметы вроде связки ключей, рогатки, вожатской панамки, гитары, книги, отвертки, еще чего-то..."
    "Розовеющее небо давало понять, что время там, на рисунке, - ранее утро."
    "Людей на рисунке не было. Только из кустов выглядывала чья-то, намеченная несколькими штрихами голова. Но только намеченная, так что не поймешь, молоденькая девушка это с забавной прической или крупная кошка. Женя перевернула страницу."
    "«Костер, пионерская сказка», - было отпечатано на машинке."
    "Следующая страница - рукописный текст, каллиграфически выведенный явно женской рукой: «Сенечке, которому, не смотря ни на что, так и осталось семнадцать. И Ульянке, которая доросла до девятнадцати!», - и несколько непонятных загогулин вместо подписи."
    "Следующая - опять рукопись, на этот раз, похоже, рукой физрука..."
    "Чем дальше открывались страницы, тем сильнее начинало биться сердце."
    # play sound sfx_knock_door7_polite
    play sound [ "<silence 2.0>", sfx_knock_door7_polite ]
    "Женя все никак не могла заставить себя разобраться в кривых невозможного почерка и прочитать, что же там понаписал физрук на целых пол страницы, когда в дверь библиотеки постучали. Женя вздрогнула, закрыла папку и пошла к двери."
    show un shy pioneer with dissolve
    un "Женя..."
    "Лена быстро взглянула и опять опустила глаза."
    un "Я забыла у тебя одну вещь."
    mz "Да, конечно. Забирай."
    "Очень хотелось спросить: «А что это? Можно почитать?» - Но Женя пока не на столько доверяла окружающим, чтобы вот так вот взять и показать свою слабость."
    stop music fadeout 1
    stop ambience fadeout 1
    scene black with dspr
    window hide
    
    $ renpy.pause(1.0)
    
    
    window show
    play ambience ambience_camp_center_night fadein 1
    scene bg ext_square_night_party2
    show d_ma grin pioneer:
        xcenter 0.4 yalign 0.02
    with dissolve
    d_ma "Тетя Алиса, а правда, что через полчаса дискотека?"
    "После ужина Максим решился и, пряча смущение за глупой клоунадой, обратился к помощнице вожатой."
    dv "Чего тебе надобно, дитятко?"
    show d_ma grin2 pioneer with dissolve_fast
    d_ma "Тётя Алиса... Алиса, а если я тебя приглашу на танец, ты меня не прогонишь?"
    "«А ты из молодых, да ранних», - хотела сказать «тётя Алиса», но не сказала. Только посмотрела чуть насмешливо."
    dv "Не прогоню. Приглашай... племянник."
    show d_ma smile pioneer with dissolve_fast
    extend " Но с условием. Жени и Ленки не будет, скорее всего, но, чтобы Мику и Сашу пригласил обязательно."
    scene bg ext_path_night with blinds
    "И вот сейчас Максим искал Алису, чтобы проводить ее до домика, под тем предлогом, что сам живет в соседнем. Пошел по тропинке на голос и, уже перед аллеей, нечаянно подслушал разговор."
    dv "Завтра уходите?"
    me "Да, в пять утра."
    dv "Смотрите, не пропадайте на неделю, как в прошлый раз. А то спасай вас потом."
    us "Рыжая! И вовсе не на неделю, а на пять дней! И вы спасали уже один раз, хватит. ОД очень мудрая женщина и не дала развалиться лагерю без ключевых фигур. Ну и мелкие ей помогли, конечно."
    "Говорили Алиса и физруки."
    th "Кажется, физруки собрались куда-то, а Алиса сейчас прощалась с ними."
    me "Сейчас налегке пойдем, а Рыжик подлечит, если что. Так что завтра туда, а послезавтра утром вернемся."
    dv "Да я понимаю, а все равно беспокоюсь за вас, Сенька. Должна же я хоть о ком-то беспокоиться?"
    me "А ты не беспокойся, а глянь лучше, что Лена нарисовала."
    $ renpy.music.set_volume(0.35, delay=0)
    play music music_list["lightness_radio_bus"] fadein 1
    show ext_square_night_party:
        alpha 0
        linear 0.5 alpha 1
    $ set_mode_nvl()
    "Максим вспомнил, что полчаса назад на площадь подошла Лена, отозвала Семена с Ульяной, подала из картонной папки какой-то листок и еще минут десять они о чем-то говорили."
    th "Значит, тоже прощалась, значит, правда, что завтра в пять утра они уходят."
    extend " Здесь сестренка Ульянина вчера мелькала, может, к ней?"
    "С танцами вообще получилось неплохо. Алиса, как обычно чуть насмешливая, Саша, благодарно улыбнувшаяся, танцевавшая на пионерской дистанции и, в конце сказавшая: «У тебя есть способности, если хочешь, приходи к Мику в кружок, подучим», Мику: «Максимочка, конечно же можно!»"
    "Всех этих девочек как будто знал, и со всеми как будто был в хороших отношениях уже давно-давно, с детского сада.{w} Еще раз Алиса. Катя фыркнула и отправилась танцевать с Витькой — соседом по домику."
    "А больше медленных танцев и не было.{w} То есть, был один - белый, в конце дискотеки. Катька мазнула глазами и опять пошла танцевать с Витькой, а Максим даже не обратил внимания. Он втайне надеялся, что его пригласит Алиса — не пригласила.{w} И вот это, а вовсе не постоянные насмешки, было обидно. На насмешки Максим отвечал той же монетой, но в нахальные янтарные глаза хотелось посмотреть еще раз, чтобы еще раз углядеть в их глубине искру улыбки."
    stop music fadeout 1
    pause 0.25
    show ext_square_night_party:
        alpha 1
        linear 0.5 alpha 0
    $ set_mode_adv()
    dv "Ну, тогда до послезавтра."
    us "Да, до послезавтра. К полудню будем. И не вздумайте нас спасать еще раз!"
    "Силуэты на аллее обнялись. Двое пошли в сторону спортзала, а третий повернул назад, к площади. Максим бегом обогнал Алису по тропинке, вывалился из кустов на площадь, и перехватил проходящую по аллее Двачевскую."
    scene bg ext_square_night
    show dv surprise pioneer2 far
    with irisout
    d_ma "Тётя Алиса, ты домой? Позволишь проводить?"
    show dv grin pioneer2 far with dissolve_fast
    dv "Провожай, провожатор."
    "Алиса, продолжала насмешничать."
    scene bg ext_square_night with fade
    "Через площадь шли, пиная по очереди бумажный стаканчик, неизвестно кем брошенный у питьевого фонтанчика."
    d_ma "Алис, а ты правда физруков спасала?"
    dv "Подслушивал.{w=0.5} Спасала, только они сами спаслись, без моей...{w=0.5} без нашей помощи."
    scene dct_ext_houses_night with dissolve
    d_ma "А куда они пошли?"
    show dv guilty pioneer2 with dissolve
    dv "Куда?"
    "Алиса замерла и ответила с услышанной Максимом ноткой неуверенности."
    dv "К сестренке Ульяниной в гости, наверное так."
    d_ma "В следующий раз я пойду с тобой."
    show dv surprise pioneer2 close with dissolve_fast
    "Максим тоже остановился и развернулся лицом к девушке. Лицо его потерялось в тени, только глаза блестели, отражая свет фонарей."
    show dv normal pioneer2 close with dissolve_fast
    "Мимо прошли, что-то увлеченно обсуждающие кибернетики. Речь шла о каком-то зондировании, о наложении сигналов и о пьезокерамике."
    dv "Ты еще..."
    show dv guilty pioneer2 close with dissolve_fast
    "Алиса представила себе растерянного и перепуганного пионера в поселке Шлюз, в окружении Семеновых двойников. Неизвестно вообще, как сознание Максима отреагирует на эту штуку."
    dv "Ты еще..."
    "Подобрать прилагательное не получалось."
    d_ma "Маленький? Глупый? Недостойный?"
    show dv grin pioneer2 close with dissolve_fast
    "И неожиданно для него прозвучал встречный вопрос Алисы."
    dv "Ну-ка, назови мне свой домашний адрес."
    hide dv with MoveTransition(1.2, leave=_moveleft, leave_time_warp=_ease_time_warp)
    "И Алиса пошла к себе, оставив на площади хватающего ртом воздух Максима. А на уже от домиков обернулась и грустно добавила."
    scene bg ext_house_of_dv_night
    show 3500_dv sadness pioneer2 at cright:
        zoom 0.75
    with dissolve
    dv "...просто ты еще не проснулся. Но спасибо, действительно спасибо."
    scene dct_ext_houses_night
    show d_ma normal pioneer:
        xcenter 0.5 yalign 0.02
    with fade
    "Алиса ушла и не услышала, как Максим ответил уже в пустоту."
    d_ma "В следующий раз я пойду с тобой."
    window auto
    show d_ma serious pioneer with dissolve_fast
    $ night_time()                        # Прописано, чтобы при обратной перемотке цвета не сбивались.
    $ persistent.sprite_time = "night"
    extend " Это не просьба, Алиса. Это просто информация."
    

    stop sound fadeout 2
    stop music fadeout 2
    stop ambience fadeout 2
    stop sound_loop fadeout 2
    scene black with dissolve2

    $ renpy.pause(3)
    
    $ renpy.sound.set_volume(1.0, delay=0)
    $ renpy.music.set_volume(1.0, delay=0)
    $ renpy.music.set_volume(1.0, delay=0, channel='ambience')
    $ renpy.music.set_volume(1.0, delay=0, channel='sound_loop')
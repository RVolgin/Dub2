label dct_simon:
    scene dct_int_coaching_room2 with blinds
    $ day_time ()
    $ persistent.sprite_time = "day"
    $ save_name = u"Дубликат.\nСёмка, Сёмка, Сёмочка -\nПредисловие"
    play ambience ambience_int_cabin_day
    play sound sfx_knock_door7_polite
    $ renpy.pause (2)
    me "Открыто!"
    show dv normal pioneer with MoveTransition(1.5, enter=_moveleft, enter_time_warp=_ease_time_warp)
    me "Алиса…"
    show un normal pioneer at cleft with MoveTransition(1.2, enter=_moveleft, enter_time_warp=_ease_time_warp)
    me "И Лена."
    th "Господи, только бы они не поссорились."
    me "Что-то случилось, девчонки?"
    dv "Доброе утро, Сень. Тут такое дело…"
    show dv guilty pioneer with dspr
    dv "В общем, мы с Ленкой ходили вас с Ульяной спасать."
    dv "И потерялись."
    me "Да наслышан уже. Спасибо, девочки."
    dv "Сенька, не мешай."
    dv "А пока вас искали, нашли папку с документами."
    show dv shy pioneer with dspr
    dv "Семён, мы{w=1}… Мы…"
    dv "Ленка, говори ты."
    show un serious pioneer with dspr
    un "Семён."
    un "Мы в неё заглянули. Чтобы понять, что там внутри. И почитали."
    dv "Совсем чуть-чуть."
    dv "Сенька, в общем, там про вас с Ульяной."
    hide dv with easeoutleft
    un "Семён, она ваша. Вот, возьми."
    hide un with moveoutleft
    
    scene dct_ext_switchman_house_day
    $ prolog_time ()
    $ persistent.sprite_time = "day"
    show dct_dream_veil:
        shiver
    with pixellate
    play music music_list["lightness_radio_bus"]
    th "Значит, этот сон я видел не просто так…"
    show d_us normal dress at center behind dct_dream_veil with moveinright:
        ypos -0.05
    d_u1 "Привет, ты Семён? Я Ульяна."
    $ renpy.pause (3)
    show blink
    stop music fadeout 1
    $ renpy.pause (2)
    
    scene black with dissolve
    play music tenderness
    show headline_text u"Сёмка, Сёмка, Сёмочка" at truecenter with dspr
    pause
    hide headline_text with dissolve
    
    show text(u"{size=48}{color=#ffdd7d}{i}Сочтены мои ночи…{/i}{/color}{/size}") at right with dspr
    pause
    hide text with dissolve
    stop music fadeout 1
    
            
################
#Глава 1. Аверс#    
################

label dct_avers:
    $ save_name = u"Дубликат.\nСёмка, Сёмка, Сёмочка -\nАверс"
    $ day_time()
    scene black
    show headline_text2 u"Глава 1. Аверс" at truecenter with dspr
    $ renpy.pause(3.0)
    hide headline_text2 with dissolve

    scene dct_int_switchman_house_day with blinds
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    d_dy "Доброе утро. Говорит радиоцентр посёлка Шлюз. Сегодня понедельник, первое июля одна тысяча девятьсот девяносто первого года. Первый день цикла."
    d_dy "Напоминаем, что утренний автобус на материк с сегодняшнего дня отправляется в десять часов утра."
    d_dy "Переходим к новостям…"
    play sound sfx_click_1
    th "Достал, бодрячок. Ненавижу."
    play sound dct_sfx_train_horn_far
    $ renpy.pause (3, hard = True)
    "Поезд кого-то привёз. Это тот сюрприз, о котором позавчера шеф говорил?"
    $ renpy.pause (3, hard = True)
    stop sound
    stop ambience
    
    scene dct_ext_switchman_house_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play music dreamers_of_the_day
    $ renpy.pause (3, hard = True)
    show d_us normal dress at center with moveinright:
        ypos -0.05
    d_u1 "Привет, ты Семён? Я Ульяна."
    th "{i}Чёрт! Какая красивая девушка. Нельзя! Фу, Семёныч! Не думать! Не подпускать и не приближаться!{/i}" 
    th "Добралась всё-таки. Ну да, эта доберётся, сразу видно."
    "Сейчас бы обматерить и послать обратно, посмотрев предварительно, как жизнерадостная улыбка сменяется гримаской незаслуженной обиды. Но нельзя — обещал шефу быть корректным."
    "Я не человек — я не лгу и обещания выполняю. Да и не умею я материть и посылать, в этом тоже моё отличие. Так и не смог научиться."
    "Может, сбежать ещё дальше? Но дальше — некуда. Дальше только внешний Периметр и автоматический синтезаторный узел, куда меня, живого, здешнее мироздание просто не пропустит."
    us "Ты что, глухой?"
    th "И зачем она здесь?"
    "Нет, я понимаю, что у студентки практика, студентка всем мешается, вот её и отправили ко мне на «дальние выселки»."
    "Но шеф-то знает, как я людей «люблю». Решил меня милой мордочкой перевоспитать? Так не поможет, да и какой в этом прок? Сколько там ещё Семёну Семёновичу жить? Вот то-то."
    "А в конце активной фазы сам же шеф и выжжет мне мозги. Научной ценности я не представляю, так — контрольный экземпляр."
    show d_us hurt dress with dspr
    us "Эй, ты слышишь меня?"
    me "Здравствуй."
    show d_us smile dress with dspr
    us "Ну ты и тормоз! И этот тормоз моим руководителем практики будет!?"
    th "Слава богу, нет."
    th "Никто не назначит руководителем практики копию человека. Пусть даже и находящуюся в активной фазе."
    me "У тебя записано в задании, кто твой руководитель. Вот и занимайся согласно заданию."
    th "Кажется, я достаточно корректен, даже голос не повысил."
    show d_us sad dress with dspr
    us "Но… Дурак!"
    hide d_us with moveoutright
    th "Ну вот, взял и обидел девочку."
    stop music fadeout 1
    stop ambience
    
    scene dct_ext_hangar_day with slideleft
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    show d_us sad dress at center with moveinleft:
        ypos -0.05
    th "{i}Не хочу я тебя обижать, прости. Но иначе никак.{/i}"
    us "Слушай, если не хочешь со мной разговаривать — дело твоё. Но помочь ты мне должен. Я же не просто так сюда прикатила, и ты здесь не просто так."
    th "{i}Я хочу тебе помочь. Но жизнь сложилась так, что мы с тобой по разные стороны забора.{/i}"
    "Вот как? Я кому-то что-то должен? Я здесь не просто так? Я не просил, чтобы меня в это втягивали, я…"
    "Но нет — просил. Шеф, хоть и человек, но заслуживает уважения. Он — единственный, кто не отмахнулся от меня и захотел помочь. Но рыжей Ульяне этого знать не обязательно."
    "Кстати, если она здесь и про меня знает, значит, сам шеф её и прислал. Значит, сам шеф и есть её руководитель практики. Однако, дедукция."
    "Понимаю, что ему некогда, но я то тут при чём? Или отослал подальше, чтобы она того, чего не нужно, не увидела?"
    "Моё существование — оно, в общем-то, всего лишь под грифом \"Для служебного пользования\" проходит, в отличие от тех же миксов."
    "А я — легально существующая в активном режиме копия. «Контрольный экземпляр НБО — нейтринно-белкового организма»."
    "«Нейтринно-белкового» — какой дурак такое название придумал?  Но очень высокому начальству понравилось, так теперь и живём."
    hide d_us with moveoutbottom
    "Всё, девочка на меня обиделась окончательно. Сидит на своей сумке и что-то пишет в тетради."
    "Дневник, что ли, ведёт? «Здравствуй, дорогой дневник. Сегодня я познакомилась с тормозом, придурком и грубияном в одном лице…»"
    show d_us hurt dress at center with moveinbottom:
        ypos -0.05
    us "Где у тебя оборудование?"
    me "У тебя за спиной."
    "Встала, подошла к воротам, заглянула внутрь. Решила-таки заняться практикой? Интересно, как она потащит хоть тот же рефрактометр?"
    "Никак не потащила. Сердито посмотрела на меня и опять уселась на сумку, опять что-то пишет."
    "А вот я сейчас глупость допустил. Из-за которой мне сегодня не придётся обедать."
    "Чёрт, ну не смогу я есть при Ульяне, а её не пригласить — воспитание, которого не было, не позволяет. А кормить её не хочу."
    play sound dct_sfx_train_horn_far
    "Совместная трапеза — она сближает, а я не хочу с Ульяной сближаться."
    hide d_us with moveoutright
    play sound sfx_slavya_gets_out
    "О, тепловоз загудел, сейчас это рыжее чудо уедет и больше не вернётся. Я пообедаю, а после обеда вытащу, кстати, рефрактометр и пойду на маршрут, займусь измерениями."
    stop ambience

    scene dct_int_switchman_house_night_bed with squares
    $ night_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_int_cabin_night
    th "А ведь красивая девушка. И характер весёлый — видно было, пока я не стал её доводить. И мозги на месте — бабуля тупых не терпит."
    play music higekitekina
    "А я… стыдно, конечно, стыдно. Но ведь это её сородичи выжигали нам мозги выключателями."
    "Чем она лучше? Кто-то из девочек даже успел понять, что происходит. А я вот тогда не понял."
    "Но этот крик: «Сёма, прощай!» — всё, что осталось в моей голове с прошлой активной фазы — я не забуду. И себе не прощу, что так и не смог решиться и отомстить." 
    "Ладно, хватит врать самому себе: две трети этой своей жизни я уже прожил. Так что ещё двухнедельных циклов двадцать-тридцать, или примерно год по Ульяниному счёту — и прощу, и забуду."
    "Опять Ульяна в голову лезет. Нет, не хочу я с ней общаться. Ни по-дружески не хочу, потому что очень боюсь влюбиться, ни так, как сегодня, потому что, кажется, уже."
    "Очень хочу, чтобы она больше не приезжала."
    stop music fadeout 1
    th "Интересно, она от природы рыжая? Наверное, да: веснушки и белая кожа."
    show blink
    stop ambience
    
    scene dct_ext_switchman_house_day
    $ prolog_time ()
    $ persistent.sprite_time = "day"
    show dct_dream_veil:
        shiver
    with pixellate
    show d_us normal dress at center behind dct_dream_veil with moveinright:
        ypos -0.05
    d_u1 "Привет, ты Семён? Я Ульяна."
    $ renpy.pause (2)
    
    scene dct_int_switchman_house_night_bed with vpunch
    $ night_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_int_cabin_night
    th "Хватит о ней думать. Спать, Семёныч, и выкинь эту рыжую Ульяну из головы."
    stop ambience
    
    scene dct_ext_switchman_house_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play sound dct_sfx_train_horn_far
    play ambience ambience_forest_day
    "Утро. День, от явления Рыжей, второй. Встаю — хочешь не хочешь, а программу надо выполнять. Поезд останавливается, у меня опять гости?" 
    th "{i}А ведь я рад её видеть. Только бы сохранить нейтральное выражение лица.{/i}"
    play music music_list["what_do_you_think_of_me"]
    show d_us hurt sport at center with moveinright:
        ypos -0.05
    $ renpy.pause (1)
    hide d_us with moveoutright
    "Ульяна кидает, не здороваясь, мне журнал, а сама разворачивается и идёт к сараю с приборами."
    "Чёрт, а я там их оставил вчера вечером неубранными. И рабочую тетрадь, и личную. Ладно, не бежать же наперегонки с Рыжей теперь."
    "Что там мне привезли? Ага, моя статья. За подписью Шефа, понятно, но тут уж ничего не поделаешь."
    "Зовут нас, правда, одинаково, так что можно сказать, что это моя подпись."
    "А из журнала выпадает записка от Шефа."
    "О! Меня ругают и от меня требуют. И ещё обозвали земляным червяком, то есть охамевшим малолеткой."
    "Ну да, с её точки зрения она уже большая, ей уже ну… лет восемнадцать, а я младше на пару лет и просто выпендривающийся школьник."
    th "Перегнул я вчера палку."
    "Нет, действительно перегнул. Ну что же, придётся терпеть компанию человека и вести себя равнодушно-корректно с Ульяной."
    stop music fadeout 1
    th "{i}Она невозможно красивая, хочется смотреть на неё и не отводить глаз.{/i}"
    stop ambience

    scene dct_ext_hangar_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    "И я очень медленно подхожу к сараю, оставив журнал на крыльце — никуда он не денется."
    "Извиняться не буду. Чёрт с ней, я готов извиниться перед Ульяной лично, но она ещё и человек. А я не хочу здесь видеть людей." 
    "Всё пройдёт, закончится у ней практика, и она уедет. НБО Семён успокоится и вернёт себе прежнее расположение духа, а через тридцать циклов вообще всё забудет, и всё вернётся на круги своя."
    "А рыжая Ульяна стоит и внимательно читает мою рабочую тетрадь. Тут же рядом лежат её дневник практики и задание."
    "Я и так знаю, что там написано, поэтому молча навьючиваюсь приборами, забираю свою тетрадь у Рыжей и иду в сторону Периметра выполнять программу."
    stop ambience

    scene dct_ext_railway
    show d_us hurt sport at fright:
        ypos -0.05
    with slideleft
    play ambience ambience_forest_day
    play sound_loop dct_sfx_steps2
    play music i_tried_to_bring_it_back
    "Рутинные действия: прийти на точку и измерить реальную и мнимую часть вероятностного потенциала в разных точках вакуоли. Сравнить рассчитанную и измеренную кривизну местного пространства в каждой точке."
    
    scene ext_path2_day
    show d_us hurt sport at fleft:
        ypos -0.05
    with slideleft
    "Ну а когда практика у рыжей закончится и она уедет, я буду сидеть у себя в будке, обрабатывать результаты и наносить изолинии на карту."
    
    scene ext_polyana_day
    show d_us hurt sport at fright:
        ypos -0.05
    with slideleft
    "Весной выйдет ещё одна статья, и всё."
    "Только иногда коллеги будут спрашивать у шефа: «Семён Семёнович, вы полевую работу совсем забросили? У вас были очень интересные результаты»."
    
    scene dct_ext_railway_without_rail
    show d_us hurt sport at fleft:
        ypos -0.05
    with slideleft
    "А Семён Семёнович будет врать что-нибудь. Не скажет же он, что это совсем другой Семён Семёнович таскал два года рефрактометр вдоль Периметра, обсчитывал результаты, писал статьи, а сейчас он стоит на линейке и поёт «Взвейтесь кострами»."
    
    scene dct_landscape_forest1
    show d_us hurt sport at fright:
        ypos -0.05
    with slideleft
    "А пока мы ходим по лесам и меряем. Рыжая Ульяна — оптическими методами, я — гравиметрическими."
    
    scene dct_ext_lake_day
    show d_us hurt sport at fleft:
        ypos -0.05
    with slideleft
    stop sound_loop
    me "Нет, к воде подойти не получится. Озеро уже за Периметром. Можешь камень кинуть, он долетит."
    "Кидаю камень в подтверждение своих слов, и мы долго стоим и смотрим, как круги бегут по абсолютно гладкой поверхности озера."
    stop music fadeout 1
    "Разговариваем коротко и только по делу."
    th "{i}Но почему от точки к точке я провожу её по красивейшим местам Периметра?{/i}"
    stop ambience
    
    scene dct_ext_switchman_house_day
    show d_us hurt sport at fright:
        ypos -0.05
    with slideleft
    play ambience ambience_forest_day
    "Обедаем молча, каждый своими продуктами и сам по себе."
    play sound sfx_slavya_run
    hide d_us with moveoutright
    $ renpy.pause (3, hard = True)
    "После обеда Ульяна уезжает, а я сажусь за обработку результатов."
    "В таком распорядке и проходит цикл."
    stop ambience
    
    scene dct_ext_switchman_house_day with slideleft
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play music music_list["eat_some_trouble"]
    "Неожиданно и непонятно активизировалась живность: несколько раз вылавливал членистоногих из тарелки или находил их в своей постели."
    "Иногда замечаю, как Ульяна порывается что-то сказать и тут же сама себя одёргивает."
    "И это правильно, потому что я не знаю, что и как я ей буду отвечать."
    stop music fadeout 1
    stop ambience

    scene dct_ext_switchman_house_day with slideleft
    play ambience ambience_forest_day
    play sound dct_sfx_train_horn_far
    $ renpy.pause (2)
    "{i}Вздрагивает сердце, когда я слышу гудок утреннего поезда.{/i}"
    show d_us normal sport at center with moveinright:
        ypos -0.05
    th "{i}Приехала.{/i}" 
    "На погоду, что ли, не посмотрела?"
    "Надо опустить глаза, сделать несколько вдохов, а потом уже объявить:"
    me "Сегодня не работаем. Будет дождь."
    "И ещё сегодня предпоследний день цикла, хотя тебе, Рыжая, этого знать не обязательно."
    hide d_us with moveoutright
    stop ambience
    
    scene dct_landscape_forest3_overcast_1 with dissolve
    play ambience dct_ambience_wind
    play music music_list["you_won_t_let_me_down"]
    "Вот казалось бы, всё сказал. Сиди в домике, жди дневной поезд и на нём укатывай. Но поезд уехал без неё, а где тогда это чудо?"
    $ persistent.sprite_time = "night"
    show d_us normal sport with dspr:
        zoom 0.13
        pos (0.5, 0.4)
    "Вон она — на дальнем краю поляны, малину дегустирует."
    #show dct_landscape_forest3_overcast_1 with dissolve2
    show dct_overcast_rain_1 with dissolve2:
        xzoom -1.0
    "А небо-то плохое, чёрт, и уже накрапывает."
    th "Плащ в зубы и бегом спасать Рыжую, Семёныч!"
    show d_us hurt sport with dspr:
        zoom 1.2
        pos (0.3, -0.05)
    us "Семён! Что ты…?"
    me "Идиотка! Садись на землю и не дёргайся!"
    th "И не задавай глупых вопросов!"
    "Накрываю её плащом и силой заставляю сесть."
    hide d_us with moveoutbottom
    "Она действительно идиотка. Или её никто не предупредил о здешних бурях."
    #scene dct_landscape_forest3_overcast_2
    show dct_overcast_rain_1:
        xzoom -1.0
    #show d_us normal sport with dspr:
        #zoom 1.2
        #pos (0.3, -0.05)
    show dct_overcast_rain_2 with dissolve:
        xzoom -1.0
    us "Всего-то мелкий дождик… Мама!"
    stop music fadeout 1
    stop ambience
    
    scene dct_landscape_forest3_overcast_2
    #show d_us normal sport:
        #zoom 1.2
        #pos (0.3, -0.05)
    show dct_overcast_rain_2:
        xzoom -1.0
    show dct_overcast_rain_3:
        xzoom -1.0
    with flash
    play sound dct_sfx_discharge
    play ambience dct_ambience_heavy_rain
    "«Бабах!» Без всякого предупреждения, кажется, прямо под ноги, бьёт молния."
    me "А ты думала, насиловать буду?"
    "Бесполезно шучу я, а мне тут же затыкают рот дождевые струи, и воздуха хватает только на то, чтобы не задохнуться."
    "Но, к счастью, Ульяна всё равно не слышит этой претупейшей шутки."
    
    scene dct_landscape_forest3_overcast_2
    #show d_us normal sport:
        #zoom 1.2
        #pos (0.3, -0.05)
    show dct_overcast_rain_2:
        xzoom -1.0
    show dct_overcast_rain_3:
        xzoom -1.0
    with flash
    play sound dct_sfx_discharge
    "Я сам-то себя не слышу, потому что первая молния — это было только начало."
    "«Всего-то мелкий дождик». Сами понимаете, я никогда не был в тропиках, но, наверное, именно так тропический ливень и выглядит."
    "Над головой пролетают ветки толщиной с мою ногу, с неба падает «вода с прослойкой воздуха», молнии бьют во всех направлениях сразу."
    
    scene dct_landscape_forest3_overcast_2
    #show d_us smile sport:
        #zoom 1.2
        #pos (0.3, -0.05)
    show dct_overcast_rain_2:
        xzoom -1.0
    show dct_overcast_rain_3:
        xzoom -1.0
    with flash
    play sound dct_sfx_discharge
    "Что-то связанное с выравниванием потенциалов, как сказал один из аспирантов в мой позапрошлый визит в Шлюз, когда я пристал к нему в курилке."
    "Да, тогда я ещё приставал с вопросами к людям, тогда мне ещё не попался на глаза тот злосчастный приказ о закрытии программы «Активация»."
    "Мы сидим, завернувшись в мой плащ, который хоть и промок насквозь, но не даёт струям воды литься за шиворот."
    
    #scene dct_landscape_forest3_overcast_2
    #show dct_landscape_forest3_semen_us:
    #    matrixcolor TintMatrix("#A1C9D0")  # Перекраска спрайта через преобразование matrixcolor. Свойство TintMatrix с цветом "#A1C9D0" даёт результат почти идентичный ночным спрайтам
    #show dct_overcast_rain_2:
    #    xzoom -1.0
    #show dct_overcast_rain_3:
    #    xzoom -1.0
    scene dct_semen_us_thunderstorm
    with flash
    play sound dct_sfx_discharge
    "Ульяна забыла все обиды и прижимается ко мне, ахая при каждом ударе молнии. Не от страха ахая, а от восхищения. Да ещё и хохочет!"
    "Чёрт, и я её понимаю! Я бы сейчас сам вскочил и захохотал, раскинув руки и подставив себя дождю и ветру, отбросив плащ."
    "Удерживают меня две вещи: присутствие свидетеля и то, что ветер такой силы просто смахнёт меня как пушинку."
    "Так что мы сидим, вцепившись в землю и друг в друга, чтобы не взлететь, а я ограничиваюсь тем, что позволяю струям воды хлестать мне по лицу."
    
    scene dct_landscape_forest3_overcast_2
    #show d_us laugh sport:
        #zoom 1.2
        #pos (0.3, -0.05)
    show dct_overcast_rain_2:
        xzoom -1.0
    show dct_overcast_rain_3:
        xzoom -1.0
    with flash
    play sound dct_sfx_discharge
    me "Сейчас улетишь в Изумрудный город!"
    "Опять бесполезно кричу — всё равно не услышит."
    "Я бы сам улетел отсюда куда-нибудь, но дальше Периметра не улетишь, а потом, в начале цикла, просто очнёшься перед воротами."
    "Сквозь струи воды я вижу медленно ползущее пятно света: это вечерний поезд, преодолевая стихию, упрямо ползёт в Шлюз, прокатившись через все узлы Сети."
    "Поезд там, а Ульянка здесь. И ночевать ей здесь, потому что без бани я её не отпущу, иначе простынет, а в эту ночь по всем признакам будет туман — тот самый."
    "А ей в туман никак нельзя, так что пусть думает что хочет, но я её не отпущу."
    stop ambience

    scene dct_landscape_forest3
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    show d_us smile sport:
        zoom 1.2
        pos (0.3, -0.05)
    with dspr
    play ambience ambience_forest_evening
    play music music_list["i_want_to_play"]
    "Дождь обрывается внезапно. Просто — вот он был, и вот его нет."
    "Ульяна оборачивается ко мне, не отстраняясь, и я понимаю, что она меня только что…"
    th "{i}Простила!{/i}"
    "По улыбке понимаю, по горящим глазам, по словам, которые так и рвутся из неё. Нет сил удержаться и не улыбнуться в ответ."
    us "Это, это… Это прекрасно. Такая силища! И… спасибо тебе, что прибежал за мной."
    "А сейчас начнёт стремительно холодать."
    "Выпавшая дождём вода испаряется и отбирает тепло от земли и воздуха, а поскольку объём вакуоли ограничен, температура в ней падает очень быстро."
    me "Бегом в домик!"
    "И мы наперегонки несёмся ко мне в логово."
    stop music fadeout 1

    scene dct_ext_switchman_house_sunset with dissolve
    "На крыльце, перед дверями, Ульяна тормозит, как будто ей неловко, но я легко подталкиваю её в спину и заставляю шагнуть внутрь."
    stop ambience

    scene dct_int_switchman_house_sunset
    $ sunset_time ()
    $ persistent.sprite_time = "sunset"
    show d_us normal sport at cright
    with dissolve
    play ambience ambience_int_cabin_evening
    "Чистые полотенце и футболку достаю из шкафа и кидаю на диван. Больше ничего нет — не трусы же семейные ей предлагать."
    me "Оботрись и переоденься в сухое. Я буду через полчаса. Если умеешь, растопи плиту."
    "А сам отправляюсь топить баню, запрещая себе с переменным успехом эротические фантазии."
    stop ambience
    
    scene dct_int_switchman_house_night_candle
    $ night_time()
    $ persistent.sprite_time = "night"
    show d_us smile tshirt at cright:
        zoom 1.2
        ypos -0.05
    with pixellate
    play ambience ambience_int_cabin_night
    play music music_list["raindrops"]
    th "И что мне с Ульяной делать?"
    "Сидит напротив, варёную картошку за обе щёки наворачивает."
    "Электричества после бури нет, так что мы сидим и ужинаем при свечах — романтика."
    "Сейчас, по идее, мы должны друг другу о себе что-нибудь рассказывать. Просто так, для завязки более близких отношений."
    us "Нет, ты, конечно, грубиян, но я тебя простила."
    "Ульяна машет рукой, чтобы я не извинялся."
    us "Я за две недели поняла, что ты хороший человек. Не знаю, может, у тебя зуб болит или ещё чего. Я когда болею, тоже злая становлюсь."
    "А я сейчас расскажу о себе, для развязки отношений."
    "Я нарушу тем самым закон о государственной тайне, но я не уверен, что он на меня распространяется."
    "Это всё равно что бумагу, на которой шпионское донесение написано, обвинить в государственной измене."
    "Не хочу потом плеваться перед зеркалом, обвиняя себя в двуличии. И обманывать её не хочу. Особенно её."
    me "Я не хороший человек и не плохой человек. Видишь ли, я сомневаюсь, что НБО можно считать человеком."
    show d_us normal tshirt
    me "Когда люди надолго задерживаются в вакуоли, у них появляются копии — их тени на границе соприкосновения двух вселенных…"
    me "Одна из этих теней сейчас тебя кормит картошкой, которую сама и вырастила."
    "Всей правды не говорю, но не лгу ни слова и основную картину обрисовываю как есть. И про фазы моей жизни, и про тему «Активация», и про закрытие этой темы."
    show d_us sad tshirt
    me "… А где-то через год по вашему календарю меня просто не станет. Тушка останется. Будет ходить, есть, пить, разговаривать. Но в конце цикла будет забывать всё, чему научилась."
    me "Так только, иногда будут ей сны сниться, от которых к завтраку уже и не останется в голове ничего."
    me "И лучше не бросайся к начальству в поисках правды. Ты только подставишь моего шефа — единственного человека, которого я уважаю."
    me "Только он один мне сказал, что считает, что я должен знать всё, и дал мне много-много документов о таких как я."
    me "И рассказал, как четыре года назад тут устроили охоту на таких как я — нефиг вам, пионеры, на людей быть похожими и людей смущать. Специальный прибор придумали."
    me "Я один только и существую. Контрольный экземпляр. Когда меня не станет — нового найдут."
    me "И как ты думаешь, насколько я после этого люблю людей? Насколько хочу с вами общаться? Даже с тобой."
    me "Ладно, что-то я тебя загрузил. Забудь и живи счастливо."
    me "Во всём этом плохо только то, что ты мне нравишься."
    "Я и последнюю фразу вслух произнёс, хотя и покраснел. Но при свете свечи этого не видно."
    "Ульяна молчит. Не думаю, что она сильно меня испугалась, но картину мира я ей подпортил основательно."
    show d_us normal tshirt
    us "Сём. А у меня практика закончилась. Завтра в два автобус."
    "Всё верно: цикл заканчивается, и все люди должны быть либо в Шлюзе, либо на материке. Тем более всякие посторонние практикантки."
    me "Не бойся. В восемь утра здесь поезд в Шлюз пойдёт, не опоздаешь."
    us "Сём…"
    me "Что?"
    us "Дурак ты.{w} И тормоз."
    "Некоторое время жду разъяснений, но, кажется, не дождусь."
    me "Ночевать будешь здесь, на диване. Ведро в сенях. На улицу ночью не суйся — будет туман, и тебе в него попадать никак нельзя."
    "Заглушаю грусть от завтрашнего расставания бытовыми мелочами."
    th "Ладно, уедет — мне спокойнее будет. А грусть пройдёт. А скоро уже и грустить некому будет."
    stop music fadeout 1
    stop ambience
    
    scene dct_int_switchman_house_night_candle_bed
    $ night_time()
    $ persistent.sprite_time = "night"
    show d_us normal tshirt at cright:
        ypos -0.05
    with dissolve
    play ambience ambience_int_cabin_night
    me "Спокойной ночи."
    us "Спокойной ночи… Сём?"
    th "Моя футболка на Ульяне — как короткое платье. Или как ночная рубашка."
    me "Что тебе?"
    us "А почему ты сказал, что мне в туман нельзя?"
    me "Знаешь, есть такие страшные сказки, в которых кукла, или тень, или портрет на стене постепенно подменяют собой человека, вытягивают из него душу?"
    me "Все думают, что это прежний человек, а это тень. А сам человек исчезает. Вот, в тумане всякие тени встречаются. Войдёт Ульяна, а выйдет её тень."
    us "А ты? Ты можешь в туман зайти?"
    me "Я могу. Потому что я и есть такая тень. Спи."
    show blink
    stop ambience
    $ renpy.pause (2)
    scene dct_ext_switchman_house_day with squares
    $ sunset_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play sound dct_sfx_train_horn_far
    th "Девять циклов от отъезда Ульяны, или четыре месяца по её счёту."
    play music music_list["farewell_to_the_past_edit"]
    "Шеф приехал."
    me "Привет, Семён Семёнович."
    show d_ss normal shirt with moveinright:
        xpos 0.4
    d_ss "Подставляй нос, родственник."
    "Шеф на полном серьёзе замахивается конвертом, чтобы шлёпнуть меня по носу."
    me "Эй, я тут при чём?!"
    show d_ss smile shirt
    d_ss "Я почтальоном работать не нанимался."
    "Но всё обходится без телесных повреждений."
    show d_ss normal shirt
    d_ss "Кстати, будешь отвечать — предупреди свою Рыжую, что служебная переписка просматривается."
    d_ss "Не каждое письмо, понятно, но Толяныч бдит. Так что пусть лишнего не пишет."
    stop music fadeout 1
    stop ambience

    scene dct_int_switchman_house_night_candle with dissolve
    play ambience ambience_int_cabin_night
    "Опять нету электричества, опять свечи, опять на ужин картошка с рыбными консервами."
    "Только сейчас без романтики. Какая может быть романтика с собственным оригиналом?"
    "Я разворачиваю письмо к пламени свечи."
    stop ambience
    
    scene black
    show d_us smile dress at center:
        ypos -0.05
    with pixellate
    us "Привет, Сёмка…"
    play music forgotten_summer
    $ set_mode_nvl()
    "{i}Привет, Сёмка.{/i}"
    "{i}Знаешь, с каким удовольствием я вывожу это: «Привет, Сёмка»? Представляю твою удивлённую физиономию и давлюсь от сдерживаемого смеха — весело мне.\nА бабуля косит в мою сторону строгим глазом и, наверное, думает: «Что такого смешного я сейчас сказала?».{/i}"
    "{i}Сто раз начинала это письмо и сто раз бросала. Каждый раз боялась, {s}что у тебя уже закончилась{/s} {s}что ты стал таким, как{/s} что ты уже всё забыл.\nСовсем всё — понимаешь меня? И сейчас этого боюсь, но очень хочу думать, что ты меня помнишь, и просто не могу уже себя сдерживать.{/i}"
    "{i}Как ты? Беспокоюсь о тебе, хоть ты и пытался меня прогнать и сделать так, чтобы я забыла тебя как страшный сон.\nТак вот, знай, что ничего у тебя не вышло! И мой тебе совет: никогда-никогда не улыбайся девушкам.{/i}" 
    "{i}Завидую тебе и твоему бесконечному лету. У нас уже поздняя осень: холодно, ветрено, дождливо, грязно. Иногда ещё и снег пытается идти. Все ходят с сопливыми носами и чихают.{/i}"
    nvl clear
    "{i}Я теперь на третьем курсе. Гоняют ужасно. Говорят, самый трудный курс и самые звери преподы. А ещё нашей группе «повезло»: нам читает лекции и ведёт у нас практику лично бабуля.\nЭто кошмарная женщина. Все плачут, без различения на двоечников и отличников, и то, что я у неё в любимчиках, означает, что я плачу в два раза громче и в два раза чаще.\n«У меня есть только две оценки: отлично и неуд, — её слова, — двоечников я заставляю выучить на пять, а отличникам доказываю, что они ничего не знают».{/i}"
    "{i}Очень хочу приехать к вам, очень хочу к тебе туда, в лето. А ещё, ты не смейся надо мной, но очень хочу, чтобы ты опять со мной не разговаривал и ходил с мрачной физиономией.\nА я бы не могла понять, за что, и злилась бы на тебя. И подбрасывала бы тебе кузнечиков в тарелку. Сём, прости, но это была я. Подбросила бы ещё какую-нибудь живность, но там были только стрекозы, жуки и кузнечики.\nИ плащ твой безразмерный хочу померить, и фуражку железнодорожную. Сём, ты и правда стрелочник?"
    nvl clear
    "{i}Сёмка, я решила специализироваться на вашей теме.\nПускай мне на кафедре и сказали, что вы — бесперспективный материал, побочный эффект и зря потраченные на вас государством ресурсы.\nПускай. Сём, я обязательно что-нибудь для вас придумаю, потому что не должны люди пропадать вот так.{/i}"
    "{i}Ещё раз — как ты там?\nЯ хочу, чтобы ты знал, что здесь есть люди, которым не всё равно. Как минимум одному человеку не всё равно, и ты его знаешь!{/i}"
    $ set_mode_adv ()
    stop music fadeout 1
    us "Пока!\nТвоя рыжая Ульяна."

    scene dct_int_switchman_house_night_candle
    show d_ss normal shirt:
        xpos 0.2
    with pixellate
    play ambience ambience_int_cabin_night
    d_ss "Ответ будешь писать?"
    "А я забыл, что шеф здесь и ждёт."
    me "Скажи, «оригинал», зачем ты со мной возишься? Я ведь никакой ценности не представляю, а ты, укрывая меня, фактически подставляешься сам."
    me "Необходимости в «контрольном экземпляре» нет никакой, и все это понимают."
    "Шеф пожимает плечами и отвечает сразу же. Как будто у него давно готов ответ на мой вопрос."
    d_ss "«И искупи убиваемых, не щади»."
    "Здесь с подачи бабы Глаши в ходу цитаты на славянском языке, вот и шеф цитирует."
    d_ss "Нельзя было давать появляться вам на свет — тут я согласен с Денисовной. Правда, это от нас не зависело."
    d_ss "Но раз уж вы появились, то хотя бы одному из вас я могу дать возможность прожить его жизнь так, как он сам того хочет — вспомни, как ты подошел ко мне больше ста циклов назад, с какой просьбой."
    d_ss "А то, что ты моя копия, ничего не меняет, но помогло мне пробить это исключение."
    d_ss "Люди думали, что понимают мои мотивы, и охотнее шли мне навстречу. Думали, что проявляют снисхождение к моим слабостям, а может, и собирали на меня компромат."
    d_ss "Только ты про ответ так ничего и не сказал. Будешь его писать?"
    th "Двадцать циклов, не более. Сорок недель."
    me "Мне осталось что-то около двадцати циклов. Меньше вашего года. Я хочу уйти со спокойной душой, никого не оставив позади себя."
    d_ss "Ну и дурак. Смотри, до утреннего поезда у тебя время ещё есть."
    th "Ага. А ещё тормоз и зануда. Правда, шефу это знать не обязательно."
    stop ambience
    
    scene dct_int_switchman_house_night_bed with dissolve
    play ambience ambience_int_cabin_night
    d_ss "Спокойной ночи, Семён Семёнович."
    me "Спокойной ночи, Семён Семёнович."
    show blink
    stop ambience
    
#################
#Глава 2. Реверс#    
#################

label dct_revers:
    $ save_name = u"Дубликат.\nСёмка, Сёмка, Сёмочка -\nРеверс"
    $ day_time()
    scene black
    show headline_text2 u"Глава 2. Реверс" at truecenter with dspr
    $ renpy.pause(3.0)
    hide headline_text2 with dissolve
    
    scene dct_int_switchman_house_day_bed with blinds
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    play music music_list["raindrops"]
    th "Пятнадцать циклов от отъезда Ульяны, или семь месяцев по её счёту."
    "Как всегда, я просыпаюсь за четверть часа до поезда, и как всегда в начале нового цикла, настраиваю свой внутренний календарь."
    th "{i}Почему я не могу забыть?{/i}"
    "Шестнадцать циклов назад считал, сколько мне осталось, а сейчас утро начинаю с подсчёта, сколько времени прошло."
    "Может, стоило на письмо ответить? Была бы ни к чему не обязывающая переписка, у девушки был бы экзотический друг, о котором она бы когда-нибудь потом рассказала внучке."
    "Глупо. Глупо, бессмысленно, бесполезно."
    stop music fadeout 1
    stop ambience
    
    scene dct_int_switchman_house_day_bed with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    "Ну что, пора вставать? Тем более шеф со статьёй торопит. С утра поработаю, а потом свалю куда-нибудь на реку, к старому корпусу или на озеро."
    "Или не ходить к старому корпусу? Там Виолетта последнее время поселилась. Не хочу с ней пересекаться, но интересно, что она там делает?"
    "«Оборотня» ловит? Слухи об оборотне даже до меня дошли."
    "Оборотень — это, конечно, бред. А может, это мой товарищ по несчастью?"
    "Проснулся, осознал себя, каким-то чудом избежал выключателя, добрался до Шлюза, а что делать дальше — не знает?"
    "Нет, бывший пионер так себя бы не повёл. Не стал бы он прятаться, он с радостной улыбкой на лице промаршировал бы прямо сквозь западные ворота."
    "Тут бы его и сцапали. Так что слухи, слухи, слухи… Развлечений тут особых нет: бильярдная да кино раз в неделю. Вот жители посёлка себя страшилками и развлекают."
    stop ambience
    
    scene dct_int_switchman_house_day with slideleft
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound dct_sfx_train_arrival_with_horn_far
    play ambience ambience_int_cabin_day
    $ renpy.pause (5, hard = True)
    th "Вот уже и шум поезда слышен."
    th "Гудок?"
    th "Кто-то приехал?"
    th "В такую рань?"
    th "Кто? Шеф в лазарете, зам его куда-то в узлы собирался, так что сейчас командировку оформляет."
    $ renpy.pause (5, hard = True)
    th "Может, почту или продукты привезли? Ну тогда пакет скинут на платформе, а я потом подберу."
    $ renpy.pause (5, hard = True)
    play sound dct_sfx_steps2
    $ renpy.pause (3, hard = True)
    "Кто-то идёт — лихорадочно натягиваю шорты."
    play sound sfx_knock_door6_closed
    "Стук в дверь незнакомый."
    me "Иду!"
    play music tenderness
    th "Господи!!!"
    show d_us smile dress:
        zoom 1.3
        pos (0.2, -0.2)
    with dspr
    us "Сёмка! Задушишь же! И поставь меня на место!"
    hide d_us
    show d_us smile dress at cright:
        ypos -0.05
    with dspr
    "Рыжик, с большой сумкой — кажется, той самой, с какой она приезжала на практику."
    us "Погодите обниматься, гражданин! Рёбра мне ещё пригодятся."
    show d_us laugh dress with dspr
    "Ульяна демонстративно ощупывает свои бока."
    me "Улька, ты откуда взялась?"
    show d_us normal dress with dspr
    stop music fadeout 3
    us "На твою голову? Каникулы у меня. А я устроилась на четверть ставки лаборантом в ваш институт, спасибо бабуле."
    us "Так что теперь мне ещё и деньги платить будут за то, что мы видимся. Целую тридцатку. В компенсацию за потраченные на тебя нервы."
    play music music_list["timid_girl"]
    show d_us smile dress with dspr
    us "А сейчас — одевайся."
    "Из сумки летят джинсы и клетчатая рубаха, чёрные носки и кроссовки."
    show d_us normal dress with dspr
    us "Вроде твой размер. Попробуй только скажи, что не подошло. Я по всей общаге ходила и побиралась. И давай быстрее, а то опоздаем!"
    "А я смотрю и не верю своим глазам. Может, у меня внезапно активная фаза закончилась, а это просто бред отключающегося мозга?"
    th "Бедный шеф, не видать ему статьи."
    me "Рыжая, ты что творишь?"
    us "Молчи и делай, что говорят. И не тормози, а то сама тебя одевать стану. Маленького Сёму. Сюрприз я тебе приготовила."
    hide d_us with moveoutright
    th "Послушаться?"
    "Послушаюсь, хотя вообще-то мне гражданскую одежду носить нельзя."
    "Глупый запрет, всё равно у меня кроме пионерской формы нет ничего, но он существует."
    "А пока я переодеваюсь в комнате, Ульяна из кухни ругает меня." 
    us "Гад ты, Сёмка. Мог бы и ответить на письмо. Знаешь, как я переживала?!"
    us "Хорошо, твой шеф, когда был в университете, нашёл меня. И рассказал про завихи в твоей голове."
    stop music fadeout 1
    me "Уля, это не завихи. Я и сейчас так считаю. А скоро счёт оставшихся циклов уже по пальцам пойдёт."
    show d_us normal dress at cright with moveinright:
        ypos -0.05
    "Ульяна не отвечает, а вместо этого заходит в комнату и, не обращая внимания на мои чёрные семейники, встаёт напротив меня, глядя глаза в глаза."
    hide d_us
    show d_us sad dress:
        zoom 1.3
        pos (0.2, -0.2)
    with dspr
    "При её росточке это трудно, но у неё получается."
    us "Сёмк, вот только что ты меня очень серьёзно обидел. Впервые за всё время нашего знакомства, кстати."
    us "Неужели, если бы я медленно тонула в болоте, ты бы стоял на твёрдой земле и ничего не пытался бы сделать?"
    us "Вот и молчи тогда! Я найду способ тебе помочь! А сейчас — одевайся, а то и вправду опоздаем."
    stop ambience
    
    scene ext_path2_day with dissolve:
        running
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play sound_loop dct_sfx_running
    "Мы бежим к путям. Чужие кроссовки слегка болтаются на ногах, в остальном Ульяна более-менее угадала с размером."
    stop sound_loop
    stop ambience

    scene dct_ext_railway with slideleft
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    show d_us normal dress at right with dspr:
        ypos -0.05
    us "Сёмк, а те пути, на которые стрелка — они куда?"
    me "Не знаю, Уля. По ним не ездит никто и никогда. Я и стрелку-то на них перевести не могу — заблокирована."
    me "Говорят, они в шахту ведут, где термоядерный заряд взорвали, когда дорогу сюда открыли."
    me "Но это ещё во времена первой экспедиции было; ни Шлюза, ни узлов ещё не было."
    me "Уже и участников первой экспедиции не осталось. Даже бабуля сюда только с третьей экспедицией спустилась."
    stop ambience
        
    scene dct_ext_sim_ul_train with slideleft
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play sound dct_sfx_train_arrival_with_horn
    play music music_list["what_do_you_think_of_me"]
    "Тепловоз сбрасывает скорость далеко перед платформой и сейчас подползает со скоростью пешехода."
    me "Рассказать тебе про здешние парадоксы?"
    me "Ехать полчаса — что от меня в Шлюз, что из Шлюза ко мне."
    me "Пешком в Шлюз — два часа с половиной с любой точки Периметра, а обратно — как повезёт: можно за те же два с половиной часа пробежать, но обычно от шести часов до двух суток."
    me "И то если дорогу знаешь, а если не знаешь, то так и будешь крутиться вокруг забора, огораживающего посёлок."
    "Собственно, я сейчас карту этих искривлений и составляю. Ну и заодно пытаюсь теорию уточнить. Потому что в теории всё параллельно и перпендикулярно и никаких тайных троп не предусмотрено."
    us "Сём, это даже в самом посёлке заметно."
    us "Если не идти по аллее, а свернуть на определённую тропинку, то от восточных ворот до медпункта можно добежать за три минуты, хотя тропинка и извивается как змеиный хвост и делает крюк аж к казарме."
    us "А по асфальту — в полтора раза дольше. А вот обратная дорога одинаковое время занимает."
    me "Ты тоже это заметила? Значит, так и назовём это явление: Ульянкины тропы."
    stop music fadeout 1
    stop sound fadeout 1
    stop ambience
    
    scene dct_ext_warehouse2_day_7 with squares
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "Как давно я в Шлюз не выбирался. Поезд заехал в ворота, прополз мимо пакгаузов и встал у дальнего конца погрузочной рампы."
    "Через два часа ему обратно: повезёт научные группы в узлы. Выйдя, оглядываюсь на кабину машиниста и, как обычно, не вижу его там."
    "Ещё одно местное чудо: машинисты-невидимки."
    show d_us smile dress at right with moveinright:
        ypos -0.05
    us "Сёмка, потом будешь рот разевать!"
    "Но Ульяна не даёт мне попроникать в тайны местного мироздания, а схватив за руку, тащит куда-то своей именной тропой: между спорткомплексом и казармой через небольшой лесок к восточным воротам, а оттуда уже по главной аллее в столовую."
    stop ambience
    
    scene ext_dining_hall_away_day with dissolve
    show d_us normal dress with dspr:
        zoom 1.2
        pos (0.5, -0.05)
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "Начало рабочего дня, поэтому народу на улице почти нет. Но всё равно мне не по себе."
    us "Всё в порядке, Сём?"
    me "Да, Уля. Просто отвык от людей у себя в лесу."
    "Хорошо ещё, что встреченные люди не знают, кто я. А то выпрут сейчас НБО с запретной для него территории, а то и чего похуже сделают."
    "А я буду доказывать, что я легальный." 
    th "Тьфу! Не буду я унижаться, не доставлю я им такого удовольствия."
    "Хорошо, что Ульяна заставила переодеться. А то пионерская форма — она как лагерная роба, сразу в глаза бросается; вот, кстати, зачем она и нужна."
    "Но сейчас я не в форме, поэтому люди равнодушно проходят мимо, задерживая взгляд на идущей со мной под руку Ульяне."
    th "Гм, а я ревную, оказывается."
    stop ambience
    
    scene ext_dining_hall_near_day
    show d_us smile dress at center:
        zoom 1.1
        ypos -0.05
    with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    us "Ну чего встал, Сёмка? Иди, не бойся. Всё начальство в Москве на совещании, здесь остались только твой шеф и Виолетта за главного."
    us "Шеф в лазарете, а Виолетта… Виолетты можешь не опасаться."
    us "Нам перекусить надо перед автобусом. А то дальше еда только за деньги, а у бедной студентки денег нет тебя кормить, амбала здоровенного."
    stop ambience
    
    scene int_dining_hall_day
    show d_us smile dress:
        zoom 1.2
        pos (0.25, -0.05)
    with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_dining_hall_empty
    us "Ну как тебе, Сём?"
    th "Девушка меня в ресторан пригласила, да?"
    me "Надо сказать, что совсем неплохо."
    "Правда, сравнить здешнюю еду с ресторанной кухней не могу — никогда не был…"
    show d_us hurt dress with dspr
    us "Мама."
    "Ульяна неожиданно прерывает мои размышления."
    "И ещё добавляет, для непонятливых:"
    us "Капец!"
    "Ульяна, кажется, даже вжалась в стул, чтобы сделаться ещё меньше ростом, а я хочу обернуться, чтобы понять, кто её так напугал?"
    us "Сёмка, сиди тихо и не отсвечивай — может быть, пронесёт."
    show d_to normal casual behind d_us with moveinleft:
        zoom 0.8
        pos (0.15, 0.1)
    $ renpy.pause (0.5)
    hide d_to with moveoutright
    "Мимо нас к раздаче проходит Толяныч, то есть Анатолий Васильевич. Заместитель руководителя филиала по кадрам и какому-то там режиму."
    "Вот только командир армейцев, охраняющих посёлок, всё норовит Толянычу честь отдать."
    show d_to normal casual behind d_us with moveinright:
        zoom 0.8
        pos (0.15, 0.1)
    $ renpy.pause (0.5)
    hide d_to with moveoutleft
    "Как бы то ни было, но припозднившийся к завтраку Толяныч берёт на раздаче поднос и, равнодушно посмотрев сквозь нас, уходит в дальний конец зала."
    us "Пронесло, не узнал. Сейчас не торопясь доедаем, ты уносишь всю посуду, а я тебя жду у входа, и выходим вместе на улицу."
    us "А потом бегом ко мне в комнату, там берём зимнюю одежду и на остановку. Как раз автобус подойдёт. И сразу садимся в автобус, чтобы не отсвечивать."
    me "Уля, меня же без пропуска всё равно не выпустят."
    show d_us smile dress with dspr:
        zoom 1.2
    us "Всё продумано, Сём. Не трепыхайся."
    stop ambience
    
    scene ext_dining_hall_away_day with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "Так и делаем. А пока мы не вышли из столовой, я всё время чувствовал спиной взгляд Толяныча. Нервы."
    stop ambience

    scene dct_int_residential_block_day with squares
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    "Ульянина комната здесь совершенно необжитая: голый матрас, пустая тумбочка, ни занавески на окнах."
    "Рыжая достает из шкафа ещё одну сумку — близнеца утренней, и вытряхивает из неё аляску, вязаную шапочку, шерстяные носки и перчатки."
    me "Уля, про джинсы и рубашку из общаги я ещё поверю, но аляску зимой кто же отдаст? Сознайся, откуда она."
    show d_us sad dress at center with dspr:
        ypos -0.05
    us "Это брата моего аляска."
    us "Он… Она ему не нужна."
    th "Ладно, поверю."
    play sound sfx_bus_honk
    "Тут слышим, как сигналит автобус."
    show d_us normal dress with dspr
    us "Побежали. Да не надевай её сейчас, ты что, жара такая! В автобусе оденешься."
    "Тот самый загадочный автобус, который ходит на не менее загадочный «материк»."
    stop ambience
    
    scene black with dissolve
    play music music_list["door_to_nightmare"]
    "Почти не помню свою пионерскую жизнь в узле. Даже номер узла не помню. Пятнадцатый, кажется, или шестнадцатый."
    "Помню только, что в последний день цикла вожатая объявляла, что смена закончилась и завтра утром все едем по домам."
    stop music fadeout 5

    scene ext_square_day with pixellate
    $ prolog_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    show mt normal panama pioneer with moveinright
    mt "Ребята, сегодня последний день смены."
    mt "Поэтому никаких мероприятий сегодня не будет. Вы должны навести порядок в домиках и перед домиками. Кто всю смену не мыл полы — помыть. Мальчики, я к вам обращаюсь."
    mt "Кто был записан в кружки, в библиотеку, в секции — должен сегодня сдать инвентарь, книги, инструменты."
    mt "А завтра утром придут автобусы и отвезут вас в райцентр."
    stop ambience
    
    scene ext_house_of_mt_sunset with dissolve
    $ prolog_time ()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_camp_center_evening
    play music music_list["i_dont_blame_you"]
    show sl smile pioneer with moveinleft
    sl "Семён, может, погуляем по лагерю на прощание?"
    me "Славя, может, лучше посидим рядышком на прощание? Ты и так по лагерю всю смену носилась."
    show sl normal pioneer with dspr
    sl "Всю смену не считается. Всю смену я обязанности выполняла, а сегодня я отдыхаю."
    show sl shy pioneer with dspr
    sl "И так хочется погулять, попрощаться, чтобы память осталась. Понимаешь?"
    show blink
    $ renpy.pause (1)
    stop music fadeout 1
    stop ambience
    
    scene ext_houses_sunset with dissolve
    $ prolog_time ()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_camp_center_day
    play sound sfx_slavya_run
    th "Ну вот и утро. Сейчас на автобус и домой."
    show sl smile sport with moveinright
    stop sound
    th "Славя. Даже в день отъезда не изменяет привычкам. А вдруг автобус придёт, пока она бегает?"
    me "Привет, Славя! Не боишься от автобуса отстать?"
    sl "Привет. Какой автобус? Мы же вчера только приехали. А ты откуда знаешь, что меня Славя зовут?"
    th "Простите, это как? Вчера же погуляли по лагерю и по домикам разошлись."
    th "Меня ещё в щёчку поцеловали на крыльце."
    sl "Ой, прости, мне надо бежать — к линейке переодеться. А после линейки поговорим, хорошо?"
    hide sl with moveoutleft
    play sound sfx_slavya_gets_out
    th "Ну хорошо. Но странно. Какая ещё линейка?"
    stop sound
    stop ambience
    
    scene ext_square_day with dissolve
    $ persistent.sprite_time = "day"
    $ prolog_time ()
    play ambience ambience_camp_center_day
    show mt normal panama pioneer with dissolve
    me "Ольга Дмитриевна, а когда отъезд?"
    mt "Э-э-э… Семён, правильно? Семён, отъезд через две недели. Смена же только началась."
    me "То есть как? Вчера же вы нам объявляли, что автобус завтра утром."
    mt "Семён, ты ничего не путаешь? Не могла я вам этого объявить, вы вчера поздно вечером приехали, и мне хватило работы вас сонных по домикам расселять."
    me "Да что вы все из меня дурака делаете! Мы уезжать сейчас должны! Уже обед скоро, а вы все ведёте себя так, будто мы только вчера приехали!"
    play music music_list["doomed_to_be_defeated"]
    show mt angry panama pioneer with dspr
    mt "Пионер, как ты разговариваешь с вожатой?"
    me "Нормальные вожатые не морочат голову пионерам!"
    show mt rage panama pioneer with dspr
    mt "Персунов! Если тебе напекло голову, то марш в медпункт! Если ты здоров, то после линейки марш в столовую картошку чистить!"
    stop music fadeout 1
    stop ambience
    
    scene black with pixellate
    $ day_time ()
    "Далее следовало моё недоумение, неудобные вопросы, непонятные ответы, спор, скандал. Пытался сбежать — конечно, безуспешно."
    "Я уже решил, что с ума сошёл, но примчались на третий или на четвертый день шеф с Толянычем, взяли меня за руку, буквально, и отвели на станцию хитрой тропинкой, где посадили на поезд и увезли сюда, в посёлок."
    
    scene ext_square_day with dissolve
    $ persistent.sprite_time = "day"
    $ prolog_time ()
    play ambience ambience_camp_center_day
    show mt rage panama pioneer at right with dspr
    show d_to smile casual at left with moveinleft
    d_to "Ольга Дмитриевна. По нашей вине произошла чудовищная ошибка. Семён не должен был попасть в ваш лагерь. Вы понимаете? Давайте присядем, разберёмся с его документами и посмотрим, что можно сделать."
    hide mt
    hide d_to
    with moveoutright
    show d_ss normal shirt behind dct_dream_veil with moveinleft:
        xpos 0.2
    d_ss "Семён, я думаю, что нечего тебе делать в этом \"санатории\". Как ты полагаешь?"
    stop ambience
    
    scene black with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play music music_list["door_to_nightmare"]
    "Я, наивный, думал что всё — что сейчас окажусь дома, как я этот дом помнил, но…"
    "В общем, в итоге я оказался обитателем будки стрелочника в пятнадцати километрах к северу от посёлка. Откуда мне уже нет выхода."
    "Ну а когда у меня закончится активная фаза, это тело аккуратно вернут в пятнадцатый же, допустим, узел."
    "А моё Я — всё, чем я жил, от чего я плакал, чему радовался, от чего я кричал по ночам в домике, и от чего хохотал, когда попадал под здешнюю бурю, даже Ульяна, какой я её сейчас запоминаю — почти всё сгинет, в лучшем случае осев где-то в блоках памяти системы."
    stop music fadeout 1
    show d_us smile dress at center with pixellate:
        ypos -0.05
    us "Сём, ты чего?"
    me "Нет, ничего. Задумался просто."
    hide d_us with pixellate
    
    scene int_bus with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    voice "Никто ничего не забыл? Можно ехать?!"
    "Водитель спрашивает, обернувшись в салон."
    #Вот у автобуса водитель вполне видимый и осязаемый: пожилой лысый дядька в меховой безрукавке поверх футболки, трениках, шлепанцах и кожаной кепке с пуговкой на макушке.
    voices "Поехали!"
    "Отвечают ему с заднего сиденья."
    play sound sfx_intro_bus_engine_start
    "И автобус начинает неспешное путешествие на материк."
    
    scene int_bus with vpunch
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience sfx_intro_bus_engine_loop
    play music dreamers_of_the_day
    "Нет, не начинает. Только подтягивается поближе к воротам, и в салон заходят двое."
    "Прапорщик с повязкой дежурного и… опять Толяныч."
    "Эти двое берут у водителя список пассажиров и неспешно идут по проходу, проверяя документы."
    "Прапорщик по нашему борту, Толяныч по противоположному."
    "Доходит очередь и до нас. Ульяна не глядя суёт прапорщику свой студенческий, пропуск и ещё какую-то корочку — видимо, мой аусвайс."
    "Прапорщик смотрит студенческий, смотрит пропуск, смотрит на Ульяну и отдает ей её документы."
    "Потом раскрывает «мои», внимательно смотрит на фотографию, на меня."
    "Сейчас обман вскроется, Ульяну выпрут с работы и с учёбы, а меня вернут в мою будку. На цепь."
    voice "Семён Семёнович, как ваша фамилия?"
    me "Персунов."
    "Не знаю, что там написано в удостоверении, поэтому отвечаю как есть."
    "Рука с удостоверением зависает в воздухе, а на физиономии прапорщика явно читается сомнение. Я чувствую, как Ульянка вцепилась в подлокотник." 
    "Положение спасает, как ни странно, Толяныч. Проверив документы у сидящих по своему борту автобуса, он подходит к прапорщику и бесцеремонно тычет его пальцем в бок."
    d_to "Ну ты закончил? Булки остывают, кефир выдыхается. Пошли уже."
    "Удостоверение возвращается ко мне, а прапорщик и Толяныч покидают автобус, завизировав на выходе список пассажиров."
    stop music fadeout 1
    play sound sfx_intro_bus_door_open
    queue sound sfx_intro_bus_transition
    "Дверь с шипением встаёт на место, проверяющие удаляются в будку дежурного, дневальный закрывает половинки ворот, а автобус наконец покидает пределы посёлка."
    "Я раскрываю удостоверение: с фотографии на меня смотрит лицо шефа."
    "Тут он значительно моложе, чем в жизни, и сходство со мной несомненное, но я понимаю колебания прапорщика."
    me "Авантюристка."
    show d_us laugh dress at center with moveinbottom:
        zoom 1.3
        ypos -0.05
    us "Не будь занудой, Сёмк. Я знала, что мы прорвёмся."
    stop ambience
    
    scene int_bus_night with dissolve
    $ night_time ()
    $ persistent.sprite_time = "night"
    play ambience sfx_intro_bus_engine_loop
    play music music_list["lightness_radio_bus"]
    "А автобус катит по бесконечной степи."
    "Или небо затягивает тучей, или совершенно внепланово наступает ночь, но в салоне ощутимо темнеет."
    "Зажигаются фары, зажигается освещение в салоне, гаснет освещение в салоне."
    "Вдруг начинает работать печка. Какая-то женщина — из тех, которым вечно дует — требует закрыть люк. А и правда, из люка ощутимо тянет стылым воздухом."
    "Ульяна неожиданно спрашивает меня:"
    show d_us normal dress at cright with moveinbottom:
        zoom 1.3
        ypos -0.05
    us "Сколько тебе лет?"
    "Я прикидываю в уме прожитые циклы и фазы и перевожу их в года."
    me "Где-то около двенадцати, если я правильно подсчитал."
    show d_us laugh dress with dspr:
        zoom 1.3
    us "Сёмк, ты тормоз. Про ваши циклы я теперь знаю. На сколько лет ты себя чувствуешь?"
    show d_us normal dress with dspr:
        zoom 1.3
    "Вопрос Ульяны опять ставит меня в тупик, и я начинаю копаться в себе."
    $ set_mode_nvl ()
    "Проблема в том, что в таких как я напихано множество обрывков памяти чужих людей, прошедших через вакуоль. И все эти обрывки неуклюже склеены в более-менее связанную ленту."
    "Поэтому иногда встречаются забавные анахронизмы. Например, я помню, что к поезду, который увёз меня в пионерский лагерь, мы с родителями подъехали на такси."
    "Всё хорошо, но это была «Победа». В восемьдесят девятом году."
    "Так что память отпадает, остаются только ощущения."
    $ set_mode_adv ()
    me "По разному, Уля. От семнадцати до двадцати семи. От настроения и занятости зависит."
    "Ульяна кивает головой, соглашаясь."
    us "Да, я тебя так же воспринимаю. Хотя твоя мысль про двенадцать мне понравилась."
    show d_us smile dress with dspr:
        zoom 1.3
    us "А мне просто девятнадцать. Без всяких \"плюс-минус\"."
    stop music fadeout 1
    stop ambience
    
    scene int_bus_night with slideleft
    $ night_time ()
    $ persistent.sprite_time = "night"
    play ambience sfx_intro_bus_engine_loop
    "За бортом становится всё темнее, всё холоднее, в салоне начали запотевать окна."
    "Пора одеваться. Мне — легко, а вот Ульяне… Она выгоняет меня в проход и командует:"
    show d_us normal dress at cleft with moveinbottom:
        zoom 1.3
        ypos -0.05
    us "Снимай куртку, завешивай ей промежуток между сиденьями, а сам отвернись и не подглядывай. И куртке упасть не давай!"
    hide d_us with moveoutleft
    "Десять минут пыхтения и чертыханий за моей спиной, десять минут невидимой мне акробатики."
    show d_us normal winter at cleft with moveinleft:
        zoom 1.3
        ypos -0.05
    us "Сём, можешь сесть на место. Спасибо тебе."
    hide d_us with moveoutbottom
    "А автобус всё едет и едет по бесконечной дороге, монотонный шум, полумрак, покачивание."
    "Скоро весь салон, все пассажиры дружно, как по команде, начинают засыпать."
    "Засыпает и Ульяна, положив голову мне на плечо; засыпаю и я, чувствуя щекой её макушку."
    show blink
    $ renpy.pause (1)
    stop ambience
    
    scene intro_xx with squares
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience sfx_intro_bus_engine_loop
    play sound_loop dct_sfx_alarm
    "Я просыпаюсь от звука электрического звонка. Открываю глаза, осторожно выпрямляюсь, стараюсь не разбудить Ульянку."
    us "Как спалось, Сём? Я уже давно не сплю."
    me "А где…"
    us "На переезде стоим. Вагоны туда-сюда таскают, нам ещё долго стоять, а вообще…"
    th "Ну долго так долго."
    stop sound_loop fadeout 2
    "Ульяна что-то говорит. А я прислушиваюсь к своим ощущениям и ничего необычного не чувствую. Ну подумаешь, НБО выбрался на материк, всего-то навсего."
    "Смотрю на свою руку — нет, не просвечивает. Скребу ногтями лёд на стекле \(когда успел намерзнуть?\) — рука сквозь стекло не проваливается."
    us "Ты чего, Сём?"
    me "Меня пугали, что нейтринные системы неустойчивы и без подпитки энергией распадаются. Вот я и проверяю, не распался ли я."
    us "Ой…"
    "Я слышу страх в голосе Ульяны."
    us "Может, нам сразу назад?"
    me "Да шучу я. Просто проверял, не сон ли это."
    us "Гад!"
    "Ульяна пребольно щипается. Прямо сквозь аляску."
    play sound_loop dct_sfx_alarm fadein 2
    "Мы сидим в тёплом и уютном автобусе, сквозь лобовое стекло видно, как катаются по рельсам вагоны."
    "Народ — кто дремлет, кто тихонько переговаривается. А Ульяне сидеть надоело."
    us "Я балда! Пошли на выход! Я же живу по эту сторону линии, две остановки и мы у меня."
    "И она подскакивает с кресла, подхватывает сумку и тащит меня за собой по проходу."
    stop sound_loop
    stop ambience

    scene bus_stop with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience sfx_street_traffic_outside
    "Минута на объяснения с водителем, и вот мы уже стоим на тротуаре, а я, зачерпнув в пригоршню снег, плавлю его в руке. Вот он какой, оказывается…"
    "Вдыхаю полной грудью воздух и чудом не закашливаюсь от дурного запаха, от обилия выхлопных газов, ещё от чего-то."
    "Преимущества у жизни в лесу есть. Я так подозреваю, что и воду из реки здесь пить нельзя."
    show d_us normal winter at center with moveinright:
        ypos -0.05
    us "Сём, это снег. И застегни молнию на куртке, а то простудишься."
    "Ага, простужусь. НБО не болеют, и переломы у них срастаются в течение суток. Правда, это там, а здесь?"
    me "Уля, ты меня совсем за Маугли держишь. Ещё покажи мне трамвай и троллейбус и держи меня за руку, чтобы я не убежал."
    "Замечаю, что Ульяна волнуется, и обнимаю её, чтобы успокоить."
    me "Я физически нигде дальше Шлюза не был, но памяти других людей в меня забито больше, чем нужно. Так что просто веди меня."
    stop ambience
    
    scene dct_intro_11 with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_street2
    #play sound_loop sfx_cooling_engine_loop    
    "А ведь прав был я, когда говорил Ульяне, что могу испугаться и убежать." 
    "Вот к остановке подходит автобус, полный по случаю утреннего часа пик. Столько народу, ужас!"
    "Я перестаю думать и просто замираю перед раскрывшимися дверями."
    us "Вперёд, Сёмка!"
    "Ульяна чувствительным толчком придаёт мне нужный импульс."
    #stop sound_loop
    stop ambience

    scene dct_landscape_city1
    show d_us normal winter at cright:
        ypos -0.05
    with squares
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_street1
    "Десять минут адских мучений и ещё десять минут пешего путешествия по кварталу панельных домов."
    us "Смотри, Сём, во-о-он там мой детский сад, а в эту школу я ходила."
    "Ещё пять минут Ульяна тратит на беседу то ли с учительницей, то ли с соседкой."
    "«Здрасьте, Надежда Павловна. {w}Нет, каникулы у меня. {w}На третьем. {w}Зайду обязательно. {w}Это он в горах так загорел. {w}До свидания»."
    us "Уф. Еле отделалась. Она вообще-то тётка неплохая, но прилипчивая."
    us "Вот, это мой дом. Заходи и не стесняйся."
    stop ambience
    
    scene dct_int_access_7 with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play sound_loop dct_sfx_lift
    "А я, конечно, стесняюсь, но преодолеваю себя." 
    "Нам на третий. Ульяна открывает дверь своим ключом и запускает меня. Обычная четвёрка-хрущевка."
    stop sound_loop
    
    scene dct_int_room_of_us_day
    show d_us normal winter at center:
        ypos -0.05
    with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_living_room
    play music rustic_ballad
    "Почему-то ожидал, что нас встретит сибирский кот, но кота нет. Зато есть клетка с щеглом в большой комнате, и мы проходим мимо щегла в комнату к Ульяне."
    us "Располагайся, я сейчас."
    hide d_us with moveoutleft
    "Ульяна выбегает из комнаты, а я оглядываюсь."
    "Кукла и футбольный мяч, медведь — явно любимый, в отличие от той же куклы. Аккуратнейше застеленная кровать и Весёлый Роджер вместо ковра над ней."
    "Хвост велосипедной цепи, выглядывающий из-под шкафа. А на трюмо, около сундучка для рукоделия, примостился большой глобус из школьного кабинета географии, весь исчирканный фломастерами."
    show d_us smile dress at center with moveinleft:
        ypos -0.05
    "Прибегает Ульянка, приносит сковородку и две вилки, видит направление моего взгляда и поясняет:"
    us "Это у нас отработка летом в школе была, вот я старый глобус и позаимствовала. А потом мы с мальчишками по нему путешествовали."
    "Я сажусь на кровать, Ульянка напротив меня на стул. А между нами на табуретке сковородка с яичницей."
    me "А это что?"
    "Я показываю вилкой на подоконник, на котором лежит недоделанная модель парусника."
    hide d_us with dspr
    "Ульянка вдруг делается грустной, встаёт со стула и, повернувшись ко мне спиной, начинает что-то искать в ящике стола."
    stop music fadeout 3
    show d_us sad dress with dspr:
        zoom 1.1
        ypos -0.05
    us "Это?"
    us "Это мы с братом клеили, но не успели доделать. А потом брата в армию забрали. Он обещал, что обязательно доклеим, как вернётся."
    "На Ульянкиной ладони совсем маленькая фотография. Парень приблизительно наших сегодняшних лет. На нём военная форма, какую носят на юге — с панамой, а снят он где-то на фоне гор."
    me "И что, обманул и не стал доклеивать?"
    "Я идиот, и понимаю это, только озвучив вопрос."
    me "Ох, прости меня, Уля! Давно?"
    #hide d_us
    show d_us hurt dress at center:
        zoom 1.3
        ypos -0.05
    with dspr
    "Подскакиваю с места и обнимаю Ульянку."
    us "Семь лет назад. Мне тоже есть за что ненавидеть людей."
    hide d_us with dspr
    "Ульяна утыкается лицом мне в грудь."
    us "Но я этого не делаю."
    th "Но я же и не человек вовсе."
    us "Вы бы долго присматривались друг к другу, но в конце концов подружились."
    us "Вы чем-то похожи. Он так же прятал свои чувства, но жёг ими себя изнутри. И не подпускал к себе никого, кроме самых близких."
    "Настроение скомкано у обоих."
    show d_us sad dress at center with dspr:
        zoom 1.1
        ypos -0.05
    "Чтобы перевести тему, спрашиваю про фотографию, стоящую на столе."
    me "Подруга?"
    stop ambience
    
    scene dct_int_room_of_us_day_2 with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_living_room
    "Там Ульяна, чуть моложе сегодняшней, рядом с какой-то девочкой."
    stop ambience
    
    scene dct_int_room_of_us_day_2 with dissolve:
        zoom 1.5
        anchor (0, 540)
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_living_room
    "Ульяна хохочет во всё горло, а девочка сверлит фотографа строгим взглядом."
    us "Ага. Танька. Очень хорошая девочка. Ты серьёзный и положительный, ты бы Хмурке понравился."
    stop ambience
    
    scene dct_int_room_of_us_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_living_room
    play music tenderness fadein 2
    show d_us normal dress with dspr:
        ypos -0.05
    th "Не знаю, лучше бы я тебе понравился."
    "Боль постепенно уходит куда-то в глубину этих голубых глаз."
    "Но теперь я знаю, как она выглядит, и вижу, что никуда она не делась. Просто затаилась там до следующего удобного момента."
    th "Что же ты, Рыжик, за меня уцепилась? Ведь к осени и меня не станет. Если бы знал, что всё так повернётся, действительно прогнал бы тебя в день знакомства. А сейчас уже не хватит сил."
    me "Не знаю никакой Хмурки."
    me "Мне вот ты полгода назад понравилась. Но об этом я тебе уже говорил."
    us "Это полгода назад. А сейчас?"
    th "А как ты думаешь, Рыжик?"
    "Одно из преимуществ моего положения состоит в том, что я, зная оставшийся мне срок, могу позволить себе говорить чистую правду."
    me "И сейчас, и всегда."
    us "Ты мне тоже, Сёмк. Сейчас и всегда."
    "Вот такое у нас выходит неловкое объяснение."
    th "Только вот сроку этому «всегда» до конца лета."
    stop ambience

    scene black with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    $ set_mode_nvl ()
    "А дальнейший день совершенно не откладывается в памяти."
    "Вот Ульяна по моей просьбе тащит меня куда-то на самую высокую точку города, чтобы я мог увидеть панораму."
    me "Нет, я надеялся что-нибудь вспомнить. Но нет."
    "\nВот мы катаемся с деревянной горки, и я сажаю занозу глубоко под кожу ладони."
    us "Сёмк, её надо вытащить и продезинфицировать!"
    me "Обязательно, Рыжик, как только приедем, сразу же к доктору пойду."
    "\nВот мы на катке, где выясняется, что я умею кататься на коньках."
    "\nВот мы бежим опять домой к Ульяне, где натыкаемся на её родителей."
    us "Папа, мама, это Семён. Мой товарищ с работы и очень-очень хороший человек! А сейчас нам надо бежать — автобус ждать не будет!"
    "\nВот мы бежим на автобус — тот, на котором приехали сюда, а водитель неодобрительно смотрит на нас, опаздывающих."
    "\nИ так до самого вечера, когда мы, сбросив зимние вещи в комнате Ульяны, с хохотом втискиваемся в поезд, идущий в синтезаторный узел, чтобы доехать до моего домика."
    $ set_mode_adv ()
    stop music fadeout 1
    
    scene dct_int_switchman_house_day_bed with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    me "Как спалось, Рыжик?"
    show d_us smile tshirt at center with moveinbottom:
        zoom 1.3
        ypos -0.05
    us "Очень плохо, Сёмк. Кто-то всё время меня будил."
    me "Знаешь, та же самая картина. Кто-то всё время меня будил. Вставай, завтракать будем."
    hide d_us with moveoutright
    stop ambience
    
    scene dct_int_switchman_house_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    play music music_list["dance_of_fireflies"]
    show d_us normal dress at cright with dissolve:
        zoom 1.2
        ypos -0.05
    us "А ещё у меня для тебя сюрприз! Каникулы кончатся, а я останусь! Буду приезжать в среду вечером, а уезжать в воскресенье."
    show d_us smile dress with dspr:
        zoom 1.2
    us "По четвергам у мальчиков военка, а я не учусь, а по пятницам у нас «Научно-практическая работа» в расписании."
    us "Так что я совершенно законно буду к тебе приезжать на три дня каждую неделю."
    show d_us shy dress with dspr:
        zoom 1.1
    us "Только ты не думай, что мы с тобой будем заниматься… тем, чем… занимались. Я с тобой работать буду."
    show d_us sad dress with dspr:
        zoom 1.2
    us "Опыты на живом тебе ставить. Потому что ты мне на всю жизнь нужен, а времени мало. Сёмк, как времени мало, и нужно успеть…"
    us "Сегодня утром проснулась, а перед глазами календарь, и в нём ещё один день перечёркнут. И страшно стало: вдруг не успею."
    show d_us normal dress with dspr:
        zoom 1.2
    us "Сёмка, я не справлюсь, если ты мне не поможешь! Только имей в виду: если я не успею, я дождусь твоего следующего пробуждения, и тогда ты у меня побегаешь."
    show d_us smile dress with dspr:
        zoom 1.3
    us "Кузнечик в супе и гусеница под одеялом тебе конфетами покажутся!"
    show d_us normal dress with dspr:
        zoom 1.3
    us "Сёмка, я обязательно придумаю, как тебе помочь. Ты, главное, не усни в это время. А потом уже ты никогда не уснёшь. И всё у нас будет хорошо и даже замечательно!"
    th "Рыжик, Рыжик. Думаешь, что мы первые на этой дороге?"
    stop music fadeout 1
    stop ambience
    
    scene dct_ext_cliff_day with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_lake_shore_day
    "Но работать мы начнём завтра, а сегодня можно просто поваляться на берегу."
    "Никто сюда не придёт, люди стараются без нужды не покидать границы посёлка, так что главное — сделать так, чтобы костёр не дымил."
    show d_us smile swim at center with moveinright:
        ypos -0.05
    me "А теперь рассказывай, что вчера было?"
    us "Ну Сём. Я решила, что надо тебя в гости пригласить. А то я у тебя была, а ты у меня нет. Вот я и провернула авантюру."
    me "Да уж, действительно авантюру. А если бы попались?"
    us "Ой, Сёмка, ну не попались же. Не будь занудой."
    show blink
    $ renpy.pause (1)
    stop ambience

    scene ext_aidpost_day with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "Заноза всё-таки воспалилась, и Ульяна погнала меня к доктору."
    "Вот так, мы доехали до посёлка, она украдкой чмокнула меня и побежала на автобус."
    show d_us smile dress at center with moveinleft:
        ypos -0.05
    us "Всё, пока, Сёмка. Не грусти так, через три дня увидимся."
    hide d_us with moveoutleft
    "А я побрёл в медпукт, поминутно оглядываясь, словно ожидая, что Ульяна вот-вот выскочит из кустов."
    stop ambience
    
    scene int_aidpost_day
    show cs normal glasses
    with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_medstation_inside_day
    play music fm_freemusic_chillout_music
    cs "Ну показывай, пионер."
    me "Здравствуйте, Виолетта Церновна. А где доктор?"
    cs "Я за него. Сокращение штатов и экономия бюджета — слыхал про таких зверей? И отмена надбавок за удалённость."
    cs "Вот и пришлось вспомнить про свой диплом, пока дурачка на это место не найдут. Вот только его и не ищут."
    show cs smile glasses with dspr
    cs "Но ты раздевайся, нам это не помешает."
    me "Так я уже, Виолетта…"
    "Показываю воспалившуюся занозу."
    show cs normal glasses
    cs "Как интересно… {w=1}И давно это у тебя?"
    me "Заноза? Недели две. Сразу вытащить не получилось, а потом я забыл про неё. Думал, что сама выйдет. И вот."
    cs "Две недели, говоришь."
    "Виола что-то прикидывает у себя в голове, потом достаёт фотоаппарат и делает снимок. Вытаскивает занозу, рассматривает ладонь, делает ещё один снимок."
    cs "Жалобы ещё есть, пионер?"
    me "Есть. Надоело лето, хочу оставшиеся три времени года."
    show cs shy glasses
    cs "Сам понимаешь, это не ко мне вопрос."
    hide cs with MoveTransition(1.3, leave=_moveright, leave_time_warp=_ease_time_warp)
    "А потом Виола запирает медпункт изнутри и подвергает меня медицинскому осмотру с пристрастием."
    "Дополнительно берутся анализы: все, какие возможно. И достаётся пачка тестов, которые я вот прямо сейчас должен пройти."
    "Чем-то это напомнило тот день, когда шеф привёз меня, ещё ничего не понимающего, в посёлок."
    "И представил пред ясны очи бабы Глаши, Толяныча, той же Виолетты, самого себя, Трофимова и ещё пары мэтров, которые уже не работают."
    th "Снимает мои параметры в конце активной фазы? Возможно."
    me "Ну и как? Представляю я ценность для науки?"
    show cs normal with MoveTransition(1.3, enter=_moveright, enter_time_warp=_ease_time_warp)
    cs "Для науки вряд ли. Но не все ценности стоит доверять науке — это я тебе как имеющая отношение к этой самой науке говорю."
    cs "Если больше нет вопросов, то свободен. Можешь пообедать в столовой, я сейчас распоряжусь, чтобы тебя покормили."
    #cs "Если больше нет вопросов, то свободен, пионер. Можешь пообедать в столовой, я сейчас распоряжусь, чтобы тебя покормили."
    me "Спасибо. Только, Виолетта Церновна... Какой я пионер? Называйте уж меня организмом, честнее будет."
    cs "Какая интересная просьба. {w}Можно, я её проигнорирую, {w}пионер?"
    stop music fadeout 1
    stop ambience
    
    scene black with dissolve
    $ prolog_time()
    $ persistent.sprite_time = "day"
    me "Доктор, я буду жить?"
    show cs normal stethoscope with dspr
    cs "А смысл?"

    scene ext_dining_hall_near_day with pixellate
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "Сейчас пообедаю и домой. А через три дня Рыжик приедет."
    $ renpy.pause (2)
    stop ambience

###############
#Глава 3. Гурт#    
###############

label dct_edge:
    $ save_name = u"Дубликат.\nСёмка, Сёмка, Сёмочка -\nГурт"
    $ day_time()
    scene black
    show headline_text2 u"Глава 3. Гурт" at truecenter with dspr
    $ renpy.pause(3.0)
    hide headline_text2 with dissolve

    scene dct_int_switchman_house_day_bed with blinds
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    th "{i}Двадцать два цикла от знакомства с Ульяной.{/i}"
    show d_us normal tshirt at cright with moveinbottom:
        zoom 1.3
        ypos -0.05
    us "Доброе утро, Сём."
    hide d_us with moveoutright
    stop ambience
    
    scene dct_int_switchman_house_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    show d_us normal sport at cright with moveinright:
        zoom 1.1
        ypos -0.05
    us "Сём, мне на поезд пора."
    $ set_mode_nvl ()
    "Ульяна переживает больше меня. Приезжает каждую неделю на три дня, обклеивает датчиками, бывает, бьёт током, заставляет решать тесты."
    "Всё это записывает, а потом всю ночь не спит на кухне — обрабатывает результаты."
    "И с каждым циклом становится всё мрачнее и мрачнее. Скрывает это, но я-то вижу."
    "Я поддерживаю её как могу, делаю вид, что верю, что всё хорошо. Но вот, кажется, этой ночью наступил кризис."
    "Я же тоже не сплю вместе с Ульяной. И вот, вместо привычных шелеста бумаги, щёлканья клавиш, бормотанья вполголоса —  тишина."
    "А потом — сдерживаемый всхлип."
    $ set_mode_adv ()
    stop ambience
    
    scene dct_int_switchman_house_night_candle_bed with pixellate
    $ prolog_time ()
    $ persistent.sprite_time = "night"
    play sound dct_sfx_scare
    play ambience ambience_int_cabin_night
    me "Что случилось, Рыжик?"
    play sound sfx_chair_fall
    play music brush_marks
    show d_us sad sport at cright with moveinright:
        zoom 1.1
        ypos -0.05
    us "Сёмка, у меня сессия начинается, и я завтра на целых два цикла уеду."
    "Пытаюсь перевести всё в шутку."
    me "Обещаю тебя дождаться, Уля."
    us "Сём, не в этом дело. Я тупая, бездарная и бесполезная дура! Которая ничего не успевает!"
    us "Я изучила всё, что есть на свете про таких как ты, я терроризировала Виолу, бабулю, твоего шефа, я запустила учёбу…"
    us "Лучше бы я в футбольную команду год назад записалась, чем на эту практику проклятую."
    us "А сейчас получается, что я тебя обманула. Я пообещала и не сделала…"
    "Первый раз вижу Ульяну такой подавленной. Она тихо жалуется."
    show d_us sad sport with dspr:
        zoom 1.2
    us "Я не могу открыть эту дверь. И никто не может."
    us "Полгода назад я думала, что всё так просто, а никто не занимается вами, потому что вы никому не нужны. А чем дальше я зарывалась, тем всё оказывалось сложнее и запутаннее."
    us "А тут ещё эта сессия. Может быть, этих двух циклов нам и не хватит!"
    us "Сегодня я ехала с материка в посёлок с твоим шефом, и он сказал мне, что вы отражения людей только с одной стороны. А с другой — отражения тех существ из другой вселенной, которые тоже лезут в вакуоли, только из своего мира."
    show d_us sad sport with dspr:
        zoom 1.3
    us "Сёмка, скажи мне, что ты не инопланетянин."
    th "Устала."
    me "Рыжик, всё нормально. Я тебя никогда и ни в чём не обвиню. В неудаче? Глупо."
    me "Знаешь, Рыжик. Если даже всё так печально, то всё равно ты была и пока есть в моей жизни. Спасибо тебе."
    me "И ещё за то, что ты что-то для меня пыталась сделать и тебе было не всё равно."
    "Мне приходится впрыскивать в Ульяну дозу оптимизма, хотя обычно происходит наоборот."
    me "Но мы же не сдадимся?"
    show d_us normal sport with dspr:
        zoom 1.2
    us "Нет, конечно, Сёмк."
    me "В самом плохом для нас случае ты просто повторишь попытку."
    stop music fadeout 2
    show d_us smile sport with dspr:
        zoom 1.1
    us "Сёмк?"
    me "Что?"
    us "Это для тебя будет плохой случай. А я уж оторвусь на тебе за все потраченные нервы. Тебе, кстати, привет от моих родителей."
    stop ambience
    
    scene dct_int_switchman_house_day with pixellate
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    show d_us normal sport at cright with dspr:
        zoom 1.1
        ypos -0.05
    us "Пока, Сём. Не скучай тут без меня. Я тебе свою рабочую тетрадь оставляю. Думай, что ещё можно попробовать."
    us "Я постараюсь пораньше обернуться. А ты лучше представляй, как я приеду и никуда не уеду!"
    hide d_us with moveoutleft
    stop ambience
    
    scene dct_ext_switchman_house_day with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    "Ульяна уехала, а обратным поездом приехал шеф."
    show d_ss normal shirt with dspr:
        xpos 0.4
    d_ss "Родственник, будешь мне помогать."
    stop ambience
    
    scene black with dissolve
    $ prolog_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "Вчера Рыжая уехала. Повисла на мне, сказала, чтоб я её не провожал, и убежала на поезд."
    "А появится теперь она только через полтора цикла. Или даже через два — сессия."
    "Зато потом обещала поселиться тут на постоянное место жительства."
    show d_us normal dress at center with pixellate:
        ypos -0.05
    us "Нельзя сдаваться! Но времени совсем не осталось, Сёмк!"
    stop ambience
    
    scene dct_ext_switchman_house_day with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play music music_list["my_daily_life"]
    show d_ss normal shirt with dspr:
        xpos 0.4
    me "Яволь, шеф. А что делать надо?"
    d_ss "Узлы мы объедем, с пионерами пообщаемся, данные соберём. Не стоило бы тебя к этим делам привлекать, но больше некого."
    d_ss "Сам видишь, сокращения у нас. В лабораториях одни только заведующие остались да те, у кого темы отработаны больше чем на семьдесят пять процентов."
    d_ss "Остальных на материк отправляют. Охрану вон сократили, остались полтора армейца и Толяныч."
    me "А что за тип в галстуке по посёлку шатается?"
    d_ss "Это начальство новое, из бывших комсомольских вождей. Филиал-то здешний в самостоятельное учреждение выделили, а его руководить прислали."
    d_ss "Ну а мы все в старом институте числимся, вот нас на материк и вывозят. Скоро и мы с тобой расстанемся."
    th "Даже жалко."
    me "А как же?…"
    d_ss "Рыжая твоя любовь? Не знаю. Может и всё хорошо ещё будет…"
    d_ss "Сегодня заночую у тебя, а завтра отсюда на поезде поедем."
    d_ss "Кстати, это последние циклы, когда он ходит. Тоже сокращают. Остаётся только кольцевой поезд и составы снабжения. Ну и автобусы ещё."
    stop music fadeout 1
    stop ambience

    scene dct_int_house_of_kids with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    th "{i}День второй от отъезда Ульяны.{/i}"
    "Мы с шефом устраиваемся в домике, уже в четвёртом по счёту узле. Два узла в день: один до обеда, один после." 
    "Поезд высаживает нас на платформе возле моста, а сам уезжает в неведомые дали."
    "Мы идём к лагерю, ориентируясь на цветные пятнышки флагов и отблеск солнца на макушке Генды."
    "В лагере сразу находим вожатую, а та уже по указаниям Шефа собирает пионеров в столовой. Я раздаю анкеты, пионеры их заполняют, я анкеты собираю."
    "Мы обедаем или остаёмся ночевать, смотря по времени суток; а переночевав или пообедав, хитрой тропинкой идём к мосту, где нас подбирает поезд."
    show d_ss normal shirt with dspr:
        xpos 0.4
    d_ss "Ну и как тебе Узлы, родственник?"
    me "Лагеря? Древнегреческий Элизиум."
    d_ss "Острова блаженных? Ну да. Блаженны нищие духом…"
    "Мы с шефом достаточно легко понимаем друг друга. Всё-таки сказывается моё происхождение."
    me "Шеф, а расскажи, откуда я взялся?"
    show d_ss smile shirt with dspr
    d_ss "Да нечего тут рассказывать. По глупости — да, по глупости, можно сказать, залетел."
    show d_ss normal shirt with dspr
    d_ss "Молодой был, вроде Рыжей твоей, полез датчики ставить в одиночку. Ну и в язык тумана вляпался. Чуть в нём и не остался."
    d_ss "А когда к Старому корпусу выбрался, тут ты за мной из тумана и вышел. Хорошо, что я тогда испугался, не стал к тебе подходить, а спрятался в здании."
    d_ss "А потом смотрю: человек лежит, голый, и вроде живой ещё. Подбежал, стал в чувство приводить. Тут меня и нашли. Тебя в Узел, мне строгий выговор."
    stop ambience

    scene ext_road_day with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_ext_road_day
    play sound_loop dct_sfx_steps2
    th "{i}День четвёртый от отъезда Ульяны.{/i}"
    play music music_list["i_want_to_play"]
    "Анкеты розданы, анкеты собраны, обед поглощён. Идём к железной дороге."
    "Обратная тропка, кстати, не такая и хитрая. Ещё в первый раз возникло подозрение, а сейчас мой намётанный глаз уже сразу видит, где и куда нужно свернуть с шоссе, чтобы не попасть в заковыристый выгиб местного пространства и не оказаться развёрнутым лицом к лагерю."
    "Часто даже раньше шефа, который всё время сверяется с каким-то прибором вроде компаса."
    stop sound_loop
    show d_ss normal shirt at left with dspr
    d_ss "С пионерами не общался?"
    me "…"
    "Отрицательно мотаю головой."
    hide d_ss with dspr
    "Какое там. Я только пригляделся к ним за эти семь узлов. Я, оказывается, за время активной фазы забыл, как девочки выглядят. Алиса, Лена, Славя..."
    "Миксов — тех помню: Мику, Женю и Сыроежкина, но их двойников я и в посёлке вижу. А вот Ольгу Дмитриевну — ту сразу узнал."
    "Алиса, Лена, Славя, Сыроежкин, Женя и Мику — вот и весь старший отряд. А остальные, средний отряд и мелюзга — те вообще не заслуживают внимания."
    "Ну так мы их и не анкетируем."
    th "Интересно, кем нас шеф представляет вожатой?"
    stop music fadeout 1
    stop ambience

    scene dct_int_train_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_vent
    me "Шеф, а почему пионеры?"
    "Так, разговаривая, мы с шефом добрались до железки, а сейчас обходим вагон и опускаем чёрную светомаскировку на окнах."
    "«А то никуда, кроме Шлюза, из узла не попадём» — объяснил мне шеф в первый же день."
    show d_ss normal shirt at center with dspr
    d_ss "А кто его знает? Может, детство своё вспоминал кто-то из первой экспедиции. Хотя нет, там копии вроде бы взрослые были."
    d_ss "Ну значит из второй или третьей. ИМ всё равно."
    me "А кому — ИМ?"
    d_ss "Есть такая гипотеза. Из тех, что озвучиваются молодняком за рюмкой чая."
    d_ss "Что мы столкнулись с разумными обитателями другой вселенной, когда начали изучать вакуоли. Что мы этих обитателей не заметили, а они нас принялись изучать. Причём изучать, строя модели, то есть вас."
    d_ss "А потом им это наскучило, и они ушли, бросив и модели, и машину по их созданию, и машину по обустройству вакуолей."
    d_ss "Самое интересное, что мы нашли входы в эти машины и даже как-то ими пользуемся."
    stop ambience
    
    scene dct_int_train_day_dark with vpunch
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    "Состав наконец трогается. Сейчас надо поспать, а то вообще из Узла не выберемся. Если не поспать, то хотя бы прикрыть глаза и замолчать минут на десять."
    show blink
    stop ambience
    $ renpy.pause(1.5,hard=True)
    

    scene ext_dining_hall_away_sunset with squares
    $ sunset_time ()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_camp_center_evening
    th "{i}День десятый от отъезда Ульяны.{/i}"
    "Очередной узел. Вечер. Сегодня в лагере дискотека! Надо же."
    show d_ss normal shirt at center with dspr
    d_ss "Родственник, сходи протрясись."
    me "Ты представляешь, как это будет выглядеть, шеф?"
    me "Прихожу я такой нарядный на дискотеку, а меня обступает толпа и молча смотрит. Самые смелые — трогают. На мои вопросы — глупо хихикают и краснеют. Если пригласить потанцевать, то убегут или упадут в обморок."
    "Почему-то все мои встречи с пионерами в отсутствие шефа проходят по такому сценарию."
    "А ещё на них таблички можно повесить: «Грустная скромница», «Активистка», «Лагерная хулиганка», ну и так далее. Это уже для совсем тупых посетителей."
    th "Господи, Ульянка, чудо моё рыжее, не дай мне превратиться в подобное, вытащи меня отсюда, пожалуйста!"
    show d_ss smile shirt with dspr
    d_ss "Ничего, просто дай им привыкнуть к тебе."
    d_ss "В общем, как хочешь. Дело твоё. Вот с твоим двойником я тебе общаться запрещаю. Если он захочет с тобой поговорить, лучше убегай. Главное, чтобы он сам вашего сходства не успел заметить."
    hide d_ss with dspr
    th "Надоело мне их жалеть — пойду и потанцую."
    "Алиса, Лена, Славя, Мику, Женя, да хоть вожатая. С Сыроежкиным я танцевать отказываюсь — бедный Сыроежкин."
    stop ambience
    
    scene ext_square_night_party with squares
    $ night_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_camp_center_night
    play music music_list["lightness"]
    "На первый взгляд — обычная дискотека в маленьком пионерском лагере. В одном углу октябрята под руководством Слави танцуют что-то своё, посередине площади топчутся старшие — все, кроме Жени."
    "По периметру — половина среднего отряда. А вторая половина сидит на скамейках, потому что стесняется. Ну и Женя там же."
    "Двойника не видать. Двойник, кажется, вечно где-то пропадает."
    "Веселье? Нету там никакого веселья. Мелкие ещё как-то развлекаются, а остальные как будто ответственную работу выполняют, а потом вспоминают, что улыбка входит в условия контракта, и приклеивают на себя улыбку."
    show un cry_smile dress far with dissolve2
    "Хотел развернуться и уйти, но перехватил взгляд Лены. Столько тоски и безнадёги в глазах я даже у себя в зеркале не видел."
    "Ну, значит, с Леной."
    show un scared dress far with dspr
    show blinking
    "Я моргнул, и впечатление исчезло, но ведь было же оно… Ульяна всё твердит, что я добрый." 
    th "Я ни разу не танцевал с Ульяной. И получается, первый свой осознанный танец я буду танцевать с Леной."
    show un shocked dress with dspr
    me "Лена, потанцуешь со мной?"
    show un surprise dress with dspr
    un "Я?… … …"
    show un shy dress with dspr
    un "Да."
    show un cry dress close with dspr
    "И полные глаза слёз, непонятно почему."
    hide un with dissolve
    "Потом пришлось танцевать ещё и с Мику, Алисой и Славей."
    "Кто, если не я?"
    th "Бедные. Скоро и я таким же стану."
    stop music fadeout 1
    stop ambience

    scene ext_clubs_day with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    th "{i}День одиннадцатый от отъезда Ульяны.{/i}"
    d_ss "Это ещё что за делегация? Ты не знаешь, родственник?"
    me "Кто бы знал, шеф."
    show un shy pioneer far at fleft with moveinbottom
    $ renpy.pause (0.5)
    show sl shy pioneer far at cleft
    show dv shy pioneer far at cright
    with moveinbottom
    $ renpy.pause (0.5)
    show mi shy pioneer far at fright with moveinbottom
    "М-дя. Мы на поезд спешим, а у выхода из лагеря нас ждут девочки."
    "Сидели на крылечке у… в общем, в этом здании в посёлке — механические мастерские, а в лагерях в нём постоянно Сыроежкины отираются."
    "А как увидели нас — встали, дружно, как по команде. Стоят и смущённо так под ноги смотрят. Как будто у Лены заразились."
    me "До свидания, девочки."
    sl "До свидания, Семён, приезжай ещё."
    stop ambience
    
    scene ext_road_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_ext_road_day
    play sound_loop dct_sfx_steps2
    "Идём по шоссе. Шеф молчит. Думает. А когда мы сворачиваем на тропинку к железной дороге, шеф выдаёт:"
    d_ss "Семён Семёныч! Импринтинг! Ну конечно, как я сразу не догадался! Зря ты меня послушался вчера… Танцевал с ними?"
    me "Ну да, а что?"
    d_ss "И?"
    me "Что? Я им посочувствовал."
    d_ss "Просто, кажется, ты фактически признался каждой из них в симпатии."
    d_ss "И это может скорректировать их алгоритмы."
    "И снова шеф молчит до самого поезда."
    stop sound_loop
    stop ambience

    scene dct_int_train_day with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    "А уже в вагоне со вздохом говорит, глядя прямо перед собой..."
    show d_ss normal shirt at center with dspr
    d_ss "Можно этого не осознавать, но пустота внутри — она ноет и просит её заполнить. Особенно когда от тебя одна дырявая оболочка."
    d_ss "Меня это тоже касается."
    hide d_ss with moveoutleft
    th "А ведь я, в сущности, о шефе ничего не знаю."
    "Изначально я не принимал людей, потом, влюбившись в Ульяну, был вынужден как-то упорядочить своё отношение к человечеству."
    "Но дружить с ними — увольте."
    stop ambience

    scene dct_int_room_of_us_day with pixellate
    $ prolog_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_living_room
    show d_us sad dress with dspr:
        zoom 1.1
        ypos -0.05 
    us "Вы бы долго присматривались друг к другу, но подружились бы…"
    stop ambience
    
    scene dct_int_train_day with pixellate
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    th "Да, разбираться нужно с конкретными людьми, а не с человечеством в целом."
    me "Шеф. А зачем и почему вы стирали самосознание у активированных?"
    show d_ss normal shirt at center with moveinleft
    d_ss "От страха. Не моя идея, но если хочешь наказать инициаторов, то можешь начать с меня."
    stop ambience
    
    scene ext_houses_sunset with squares
    $ sunset_time ()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_camp_center_evening
    show d_ss normal shirt at center with dspr
    d_ss "Я и сейчас боюсь."
    "Шеф сегодня молчал до конца дня. Я решил уже, что разговор закончен, но вечером, в очередном лагере, шеф возвращается к разговору."
    d_ss "Я и сейчас боюсь. И остальные боятся. Нужно рассказать тебе всё по чести и совести, а мы боимся."
    d_ss "Не тебя, конечно. А той штуки, что держит тебя на привязи. Вдруг услышит."
    stop ambience

    scene dct_ext_switchman_house_sunset with squares
    $ sunset_time ()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_forest_evening
    th "{i}День двадцатый от отъезда Ульяны.{/i}"
    show d_ss smile shirt at center with dspr
    d_ss "Ну всё, пока, родственник. Спасибо за помощь."
    hide d_ss with moveoutright
    "Всё кончается. И наша поездка по лагерям тоже. Шеф уезжает в посёлок, а я остаюсь у себя в будке."
    "Не скажу, что я простил шефа, но мне кажется, он искренне верит в то, о чём мне говорил."
    "Где-то, вот-вот, в начале следующего цикла должна вернуться Ульяна. А я переночую и поеду в посёлок помогать шефу."
    stop ambience
    
    scene dct_ext_bulletin_board with squares
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    th "{i}День двадцать девятый от отъезда Ульяны.{/i}"
    play music music_list["door_to_nightmare"]
    "Стою перед доской объявлений в посёлке, читаю \"Приказ №1 по Научно-производственному центру «Вакуоль»\"."
    th "Какое оригинальное название."
    $ set_mode_nvl ()
    "Утром едва дождался поезда и поехал в посёлок, чтобы встретить Ульяну. А приехал к руинам."
    "Посёлок сейчас напоминает контору, работники которой готовятся к переезду, упаковывают вещи, причём машина уже сделала одну ходку и вот-вот вернётся."
    "Ещё кто-то работает, но над посёлком уже витает дух брошенности."
    "Ульяна не приехала, поговорить с шефом не получилось, я бестолково шатался по посёлку, пока не оказался около доски объявлений. На меня никто не обращал внимания, так что я остановился почитать."
    nvl clear
    "Да, филиал института реорганизуется в \"Научно-производственный центр.\"" 
    "Директором назначен… Замом назначен…"
    "Персоналу института приказано убыть по месту основной площадки института."
    "Отъезд персонала организуется в три приёма, и дальше списки. Первая партия уезжает сегодня, Ульяна и шеф во второй очереди — это через три дня."
    "Прикидываю: сегодня Ульяны не было, следующий автобус — завтра. Завтра приедет, послезавтра уедет, забрав медведя."
    th "Господи, плохо-то как!"
    "И что с этим делать — непонятно. Не держать же её здесь, в лесу? Пойти к новому начальству? Не знаю, не верю я в успех. Дождаться Ульяны и вместе с ней решать? Не-зна-ю!"
    $ set_mode_adv ()
    "А пока я так стою и тихо паникую, меня окликают."
    stop music fadeout 1
    voice "Это вы — дубликат Персунова?"
    stop ambience
    
    scene ext_dining_hall_away_day with slideleft
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    show d_tie normal at center with moveinright
    "Оглядываюсь. Ко мне подходит тот самый тип, о котором я спрашивал у шефа. Кожаный пиджак, галстук — по здешней жаре-то. Пижонская папка под мышкой."
    "Лет ему около тридцати, лицо приветливое и располагающее. Как сказал шеф: «Из бывших комсомольцев»."
    "Почему, кстати, «бывших»? Жду продолжения."
    d_tie "Я директор НПЦ. И у меня для вас поручение. Подождите меня у здания дирекции."
    "Ни \"Здравствуйте\", ни назвать себя по имени. Типичный человек."
    stop ambience
    
    scene dct_ext_module_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "Поручение так поручение. Отправляюсь следом за Галстуком к административному модулю. Может, если я хорошо себя зарекомендую, удастся выторговать отсрочку для Ульяны?"
    "Начальство скрывается внутри модуля."
    "На крыльце остаётся лежать выпавший из папки машинописный листок. Поднимаю его: отдам хозяину, когда тот вернётся. И отправляюсь на площадь, с таким расчётом, чтобы меня было видно с крыльца модуля."
    "Буду наблюдать за суетой отъезда."
    th "Не хочу, чтобы Ульяна уехала, вот!"
    th "Господи, в которого я не верю и которому нет до таких как я никакого дела — сделай так, чтобы Ульяна осталась!"
    stop ambience

    scene dct_ext_module_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    play music music_list["heather"]
    show d_tie normal at center with dspr
    d_tie "Заберёте из посёлка местных{w=1} миксов и отвезёте на поезде в Узел номер один. Там передадите их{w=1} вожатой. Вот их путёвки и копии приказов о возвращении в лагеря."
    d_tie "А сами возвращайтесь в посёлок, в моё распоряжение."
    me "Отвезти и вернуться. Понятно."
    d_tie "Это хорошо, что вам понятно. Если будут какие-то препятствия со стороны бывшего персонала института, сразу же ставьте меня в известность."
    me "Понятно. Тогда я пошёл?"
    d_tie "Одну минуту. Скажите, вы точно единственный из НБО, кто сейчас в активной фазе?"
    me "Да, а что такое?"
    d_tie "Нет, ничего. Я вас больше не задерживаю."
    stop music fadeout 3
    stop ambience
   
    scene dct_ext_module_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    th "Как-то он странно смотрел на меня. Как собака на кости на мясном прилавке."
    "С местными миксами я в этом цикле ещё не общался, так что придётся сначала знакомиться."
    "Кто там у нас ближайший? Женя? Вот с неё и начнём."
    stop ambience
    
    scene dct_int_cabinet_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    play music music_list["i_want_to_play"]
    "Женя работает под началом Толяныча в отделе кадров: картотеки, приказы, учёт и прочее. Вал бумаг — вот, наверное, Толяныч и обзавёлся помощником из миксов."
    me "Здравствуйте. Есть кто?"
    show mz normal glasses pioneer far with dspr
    mz "Здрав{w=1}…"
    show mz angry glasses pioneer far with dspr
    mz "А ты что здесь делаешь? Это отдел кадров, а не детский сад!"
    th "Злюка. Ничуть не изменилась."
    show mz rage glasses pioneer far with dspr
    mz "Ты что, в цирке? Прекращай улыбаться и говори, зачем пришёл, или убирайся!"
    #hide mz
    show d_to normal casual at center
    with dspr
    d_to "Женя, не шуми. Здравствуйте, Семён, странно вас видеть в отделе кадров."
    me "Здравствуйте, Анатолий Васильевич. Я вообще-то за Женей."
    "Протягиваю Анатолию Женину путёвку в лагерь и приказ нового начальства."
    "Путёвку тот отдаёт Жене, а приказ оставляет себе."
    d_to "И правда — давай, Евгения, съезди на полторы недели. А то не до тебя сейчас."
    show d_to normal casual at cright with move
    hide mz
    show mz bukal glasses pioneer at cleft
    with dissolve
    mz "Дядя Толя, а может, я останусь? Вы же не справитесь."
    d_to "Поезжай, доча. Справлюсь."
    stop music fadeout 1
    stop ambience
    
    scene dct_int_chief_corridor_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    th "О как."
    "\"Доча\" и \"дядя Толя\". Сколько тут живу, а услышал впервые. Тоже кусок чьей-то жизни."
    "Ставлю в воображаемом списке из трёх имён первую галочку и отправляюсь на поиски Сыроежкина."
    stop ambience
    
    scene ext_clubs_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    play music music_list["timid_girl"]
    me "Сыроежкин, стой не шевелись!"
    show el scared pioneer far with easeinright
    el "А? Что?"
    show el surprise pioneer with dspr
    el "А кто ты? Я тебя раньше в посёлке не видел."
    th "А какая разница? Видел, не видел…"
    me "Это не важно, кто я. А вот тебе нужно в лагерь ехать. Вот путёвка, вот приказ. Всех стажёров в лагерь возвращают."
    show el normal pioneer with dspr
    "Сыроежкин берёт у меня путёвку, берёт копию приказа, сосредоточенно читает её."
    show el smile pioneer with dspr
    el "О! Женя едет! Это хорошо!"
    th "Влюблённый…"
    "Даже не обратил внимания на то, что все имена в приказе взяты в кавычки: НБО настоящие, личные имена не положены."
    show el normal pioneer with dspr
    el "Ну хорошо. Если надо, значит надо."
    show el upset pioneer with dspr
    el "А всё-таки жаль. Я ведь здесь работаю с самим Трофимовым!"
    th "Так, кажется, пора убегать."
    show el serious pioneer far with dspr
    el "… Такие проекты!"
    stop music fadeout 1
    stop ambience
    
    scene dct_ext_residential_block_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    el "Это будет прорыв! Это…"
    th "Еле сбежал."
    stop ambience
    
    scene dct_ext_residential_block_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "Мику — она, как и я, числится в лаборатории шефа, так что — к шефу. Потому что саму Мику я немного побаиваюсь."
    "Интересно, где его сейчас искать?"
    stop ambience
    
    scene dct_ext_module_smoking_place with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    play music music_list["two_glasses_of_melancholy"]
    "Шеф находится в последнем месте, где его можно найти. В курилке."
    show d_ss normal shirt at center with dspr:
        zoom 0.8
    th "Наконец-то нашёлся."
    me "Шеф, подожди, не убегай!"
    show d_ss normal shirt with dspr:
        zoom 0.9
    d_ss "А, родственник. Мику нужна? Наслышан уже. Давай путёвку, я распоряжусь."
    d_ss "Жалко отпускать её, очень жалко. Но так будет правильно."
    th "И не болит же у них голова от её трескотни?"
    "Правда, местные миксы выгодно отличаются от тех миксов и копий, что живут по лагерям. Люди и люди, только что биография с несуразностями и память с пробелами. Или это общение с хомо сапиенс на них так благотворно сказывается?"
    show d_ss normal shirt with dspr:
        zoom 1.0
    d_ss "Будешь?"
    "Некурящий шеф неожиданно предлагает мне сигарету. Я отказываюсь. Тогда шеф закуривает сам."
    d_ss "Новое начальство не поощряет контакты между вами и нами. А так мы вроде как просто в курилке сидим."
    d_ss "Я тут кое-что оставил для тебя — на случай, если не увидимся. В лаборатории, на доске объявлений. И вот, послушай."
    hide d_ss with moveoutleft
    "Недокуренная сигарета летит в консервную банку, приспособленную под урну. Шеф встаёт и уходит. А на скамье остаётся лежать магнитофонная кассета."
    th "Где и на чём я могу её послушать?"
    "Это такая шутка со стороны шефа? Оставить кассету, но не оставить магнитофон."
    d_tie "… и не задерживайте отъезд, никаких отсрочек! Все бывшие сотрудники должны покинуть территорию. Не поймут по-хорошему — значит, будем по-плохому…"
    "За углом ругается новое начальство. Наверное, не стоит показывать начальству, как я исполняю его поручение, сидя в курилке. Мне ещё за Ульяну просить."
    "И светить кассетой, выпирающей из тесного кармана пионерских шорт, тоже не стоит."
    "Поэтому я прячу кассету под металлический отлив ближайшего к курилке окна, а сам ныряю в кусты, чтобы Ульянкиной тропой выйти к складам, к железнодорожному тупику, где и буду дожидаться своих миксов."
    stop music fadeout 1
    stop ambience
    
    scene dct_ext_warehouse2_day_7 with squares
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "К трём часам дня появляются пассажиры."
    "Женя в сопровождении Толяныча, Сыроежкин с туристическим рюкзаком и Мику с пижонским чемоданом — явно не казённым, явно кто-то из сотрудников подарил. Любят её всё же? Возможно."
    "Идём к вагону, а по дороге Толяныч, взяв меня под локоть, отводит в сторону."
    d_to "Семён, на секунду."
    show d_to normal casual at center with dspr
    me "Я слушаю вас, Анатолий Васильевич."
    d_to "Я здесь уже практически никто. Так что могу только советовать. Так вот, Семён. Не возвращайтесь в посёлок."
    d_to "Потеряйтесь — или в лагере, или в вашем бунгало, пока не уснёте. И не давайте себя найти. Ни в коем случае."
    d_to "У вас это получится, всё равно лучше чем вы Периметр никто не знает."
    "Я ценю деликатность Толяныча, назвавшего пассивную фазу сном, но…"
    me "Не совсем понимаю, о чём вы. И я не хочу ссориться с новым начальством."
    show d_to smile casual with dspr
    d_to "А оно не будет с тобой ссориться."
    "Толяныч непринуждённо переходит на ты."
    d_to "Ты же не ссоришься со своими приборами."
    d_to "А когда прибор сломается, из него ещё можно извлечь много полезного. Вон, Сыроежкин знает."
    show d_to normal casual with dspr
    d_to "Ну я вам совет дал, а дальше вы сами."
    th "И опять мы на вы."
    hide d_to with dspr
    th "Всё равно я не понимаю его логики…"
    "Но Толяныч уже утратил ко мне интерес."
    stop ambience

    scene dct_int_train_day with squares
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    d_to "Семён — на всякий случай, прощай. Сыроежкин — как тронетесь, светомаскировки на окнах опусти!"
    "Толяныч уже подал в вагон Женины вещи, поцеловал Женю в темя, подсадил на высокие ступени вагона и сейчас даёт с платформы последние указания."
    "Вспархивает в салон Мику. Я, как галантный кавалер, уже подал снизу её чемодан. Всё, можно ехать."
    stop ambience
    
    scene dct_int_train_day with vpunch
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    play sound_loop dct_ambience_train_slow
    "Вагон трогается, Толяныч машет персонально Жене. Я закрываю двери, поезд выезжает за территорию посёлка и начинает плавно ускоряться. Сыроежкин бегает от окна к окну и опускает шторы."
    stop sound_loop
    stop ambience
    
    scene dct_int_train_day_dark with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    "Родственники мои, нейтринно-белковые брат и обе сестры, уже распределились по вагону: Мику — в хвосте, Женя — максимально далеко от Мику, Сыроежкин — неожиданно рядом с Женей, что она, впрочем, терпит."
    "А мне остаётся только компания девочки, считающей себя наполовину японкой."
    play music music_list["so_good_to_be_careless"]
    show mi smile pioneer with dspr
    "Та с такой надеждой смотрит на меня и так радостно-приветливо улыбается, что я, несмотря на кислое настроение и мысли об Ульяне, улыбаюсь в ответ."
    mi "Привет. А ты такой же стажёр, как и все мы? Тогда можно я тебя буду Сенечка звать? А я — Мику. Это японское имя, потому что моя мама настоящая японка…"
    "И так далее…"
    "Хорошо ещё, что голос у Мику не очень громкий и очень приятный."
    "Можно представить себе, если закрыть глаза, что ты сидишь около фонтана перед библиотекой. Или в пятом секторе Периметра на берегу говорливого ручья."
    "Темы монологов примерно известны, так что можно почти не следить за ходом «беседы», только поддакивай изредка."
    stop music fadeout 5
    stop ambience
    
    scene black with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    play sound_loop higekitekina fadein 3
    $ set_mode_nvl ()
    "Ульяна… Да, шеф прав насчёт пустоты. Ноет и просит заполнить. И я заполняю пустоту Рыжей."
    "Хочется волчьим воем повыть на Луну от тоски… А у тех девочек в лагерях своя пустота. Только в силу краткости своей жизни они это осознать не успевают."
    "Тени… Оболочки… Отражения… Люди. И они, бедные, пытались заполнить пустоту мной."
    nvl clear
    th "Завтра моя Рыжая приезжает. Наплюю на совет Толяныча и поеду в посёлок."
    "Я размышляю о наших с Рыжиком перспективах. Год назад я не то чтобы смирился, но принял свою судьбу, и основной моей задачей было избежать Выключателя."
    "Не верю и не верил, что шеф пошёл бы на такой шаг, но и кроме шефа здесь хватает народу."
    "А потом пришла Ульяна и нахально влезла в мой мирок. Я не надеялся, что Рыжик изобретёт свою чудо-таблетку. Но так захотелось поверить в сказку, и я поверил."
    "Под показным скептицизмом, который так обижал Ульяну, я поверил."
    nvl clear
    "И вот, кажется, Рыжик упёрлась в тот же тупик, что и здешние учёные до неё. Сто-двести циклов, и такой как я отключается, вернувшись в «пионерское» состояние."
    "Как будто кто-то перекидывает тумблер, и несчастный НБО перестаёт быть человеком разумным. Или как будто несчастный НБО слишком далеко отходит от своего природного состояния, и его, дёрнув за подтяжки, возвращают назад."
    $ set_mode_adv ()
    stop ambience
    
    scene black with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    show mi serious pioneer with dspr
    mi "Сенечка, тебя что-то беспокоит?"
    "Мику вдруг вклинивается в мои мысли."
    "Я поворачиваю голову и вижу пару обеспокоенных глаз."
    "Она славная девушка. Если бы она была человеком, обязательно нашёлся бы кто-то, кто не стал бы прятаться в свои мысли от потока её слов, кто увидел бы в ней живую, ищущую душу."
    "Кто-то стал бы ей близким настолько, что Мику сбросила бы этот щит из слов, за которым она прячется от окружающих."
    stop sound_loop fadeout 1
    stop ambience
    
    scene dct_int_train_day_dark
    show mi serious pioneer
    with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    me "Скажи, Мику. Вот в будущем наука обязательно создаст роботов, во всём неотличимых от человека. Сергей не даст соврать."
    me "И вот, предположим, ты встречаешь такого робота. Как ты определишь, когда робот перестал быть роботом, который живёт по программе, и стал человеком?"
    show mi surprise pioneer with dspr
    mi "Ух!{w} Обожаю такие разговоры."
    mi "Ну во-первых, он должен быть умным."
    show mi dontlike pioneer with dspr
    mi "Нет, полным-полно глупых людей."
    show mi normal pioneer with dspr
    mi "Во-вторых, он должен чувствовать: грустить, смеяться, злиться."
    show mi sad pioneer with dspr
    mi "Но ведь можно всё это запрограммировать. Записать, когда нужно смеяться, а когда плакать. И в современных комедиях часто записан смех за кадром. Не для роботов же его записывают."
    show mi serious pioneer with dspr
    mi "Ну и вопрос ты задал, Сенечка. Хорошо, тогда в-третьих…"
    stop ambience
    
    scene dct_int_train_day_dark
    show mi serious pioneer
    with hpunch
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    play music music_list["a_promise_from_distant_days_v2"]
    "Вагон встряхивает на стрелке — мы проезжаем мимо моего дома."
    "Пора на боковую. Миксы мои прекрасно чувствуют этот момент, потому что отключаются один за одним."
    show mi grin pioneer with dspr
    "Мику поднимает на меня глаза."
    mi "Ну и хитрый ты, Сенечка. Ты специально задал мне вопрос, на который нет точного ответа. Но я отвечу. Вот сейчас подремлю полчаса и отвечу."
    hide mi with moveoutbottom
    "Мику потягивается и тут же засыпает, доверчиво положив голову мне на плечо."
    "Не знаю почему, но я чувствую симпатию ко всем этим миксам и копиям."
    th "Пока я не сплю, я не дам вас в обиду."
    "А сейчас и мне надо поспать, в прямом смысле слова."
    show blink
    $ renpy.pause (1)
    stop music fadeout 1
    stop ambience
    
#################
#Глава 4. Жребий#    
#################

label dct_lot:
    $ save_name = u"Дубликат.\nСёмка, Сёмка, Сёмочка -\nЖребий"
    $ day_time()
    scene black
    show headline_text2 u"Глава 4. Жребий" at truecenter with dspr
    $ renpy.pause(3.0)
    hide headline_text2 with dissolve   
    
    scene ext_camp_entrance_day with blinds
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    play music music_list["what_do_you_think_of_me"]
    show mt normal panama pioneer with dspr
    mt "Семён, а может, ты тоже останешься?"
    mt "А то у меня в старшем отряде и так одни девочки, и ты ещё двоих привёз."
    th "На ловца и зверь бежит."
    "Что там Толяныч вчера мне советовал? Раствориться в лагере? Так пожалуйста, даже просить не надо."
    "И судя по отношению Толяныча к Жене, он добра мне хотел. Правда, человек он, конечно, сложный. Правда, он человек."
    me "Ольга Дмитриевна, я же вам и мальчика тоже привёз. Да у вас и свой один был."
    show mt smile panama pioneer with dspr
    mt "Вот именно, что всего один, и тот…"
    show mt normal panama pioneer with dspr
    mt "А твой Серёжа — он сейчас организует кружок «Умелые руки» в составе самого себя. И будет там до самого конца смены прятаться от общественной жизни. Уж поверь моему опыту, я это ещё вчера поняла."
    mt "Ну как, остаёшься?"
    th "Рыжая-рыжая моя Ульяна приезжает сегодня и…"
    th "И? Для начала просто — Ульяна приезжает сегодня!"
    me "Нет, Ольга Дмитриевна. От меня больше пользы дома будет."
    show mt sad panama pioneer with dspr
    mt "Ну как скажешь. Тогда не задерживаю. До свидания."
    "Почему у меня сейчас такое впечатление, что вожатая — не просто микс, живущий по цикловой программе, но нечто большее?"
    "Потому что она лет на восемь старше обычного возраста миксов и копий? Из-за её взгляда, совершенно отличного от взглядов здешних пионеров? Осмысленного какого-то. И остаться она меня пригласила сама, по личной инициативе. Ладно, не буду уточнять. Меня Ульяна ждёт."
    "Смотрю в зелёные глаза вожатой, улыбаюсь ей."
    me "До свидания! И спасибо, что приютили и накормили."
    show mt smile panama pioneer with dspr
    mt "Семён, не говори ерунды. Я ещё никого голодным не оставляла и за ворота не выгоняла. Всё, беги, а то опоздаешь."
    hide mt with dspr
    stop music fadeout 1
    stop ambience
    
    scene ext_no_bus with dspr
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    "Точно, надо бежать. Ну, на самом деле не бежать, а идти, но надо. Со своими бывшими подопечными я ещё за завтраком попрощался, так что — вперёд!"
    stop ambience
    
    scene ext_road_day with dspr
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_ext_road_day
    play music music_list["raindrops"]
    "Вперёд по извилистой тропинке от лагеря до платформы, где временами приходится идти спиной вперёд или делать несколько шагов, закрыв глаза."
    "Сначала по дороге до первой опоры ЛЭП, а там свернуть чуть правее, держась на ладонь правее приметного дерева на вершине дальнего холма."
    "Далее — смотря по времени суток и порядковому номеру дня цикла."
    "Я научился находить тропы в складках пространства без всяких приборов."
    th "К чему только мне эти знания и умения? Всё равно всё забуду."
    stop music fadeout 1
    stop ambience
    
    scene dct_int_train_day with squares
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    "Грустный тепловоз, делающий свой последний рейс, подобрал меня."
    "Как он умудряется попадать из посёлка в лагеря, хотя железная дорога делает кольцо вдоль Периметра, нигде его не пересекая — загадка."
    "Шеф в самом начале моей активной стадии пытался мне объяснить что-то про параллельные объёмы, но я ничего не запомнил."
    "Мне тогда было не до того, я пребывал в эйфории от внезапных перспектив. Всего-то сотня с небольшим циклов назад, или четыре года по людскому счёту."
    "Внешне я с тех пор не изменился совершенно, а вот внутренне у меня, кажется, год за пять пошёл."
    stop ambience
    
    scene dct_int_train_day
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train
    "Поезд выползает на мост — это значит, что мне пора поспать, а то так и буду кататься вокруг этого лагеря."
    "Некоторое время смотрю в окно, стараясь разглядеть золотую искру солнца на бронзовой макушке памятника, потом укладываюсь на сиденье, высунув ноги в проход, и задрёмываю, чтобы распахнуть глаза через десять минут."
    stop ambience
    
    scene dct_int_train_day with vpunch
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_train_slow
    th "Перескочили." 
    "Момент перехода не чувствуется, но всегда знаешь, совершился он или нет. И ещё я не помню случая, чтобы переход произошёл не туда, куда тебе надо."
    "Скоро стрелка, скоро мой домик. Интересно, в нём и вправду когда-то стрелочник жил? А ещё интереснее задача: выйти около дома или ехать в посёлок?" 
    th "Где Рыжая сейчас?"
    "Нет, выйду здесь. Если Ульяна приехала в посёлок утренним автобусом, то она уже ждёт меня. А если приедет вечером, то до вечернего автобуса ещё семь часов, я пешком три раза успею до посёлка дойти."
    stop ambience
    
    scene dct_ext_railway with vpunch
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    "Выскакиваю, едва поезд останавливается, и бегу к дому."
    stop ambience
    
    scene dct_ext_switchman_house_day with dspr
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play music music_list["door_to_nightmare"]
    "Калитка не заперта — значит, Ульяна здесь."
    stop ambience
    
    scene dct_int_switchman_house_day with dspr
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_int_cabin_day
    play sound sfx_open_door_1
    me "Угадай, кто пришёл!?"
    ".{w}.{w}."
    me "Уля!"
    ".{w}.{w}."
    me "Рыжик, ты здесь?"
    "Ули нет. Есть только записка на столе."
    show effector_letter_2_1 with dspr:
        zoom 2.0
        pos (200, 400)
    us "{i}Привет, Сёмка. Жаль, что мы разминулись, но так, наверное, к лучшему…{/i}"
    show effector_letter_2_1 with dspr:
        zoom 2.0
        pos (200, -100)
    us "{i}…Сразу меня не хватятся, пока не начнут последний персонал вывозить, а там мне уже будет всё равно…{/i}"
    show effector_letter_2_1 with dspr:
        zoom 2.0
        pos (-1400, -1100)
    us "{i}…Вероятность нашей встречи целых 0,3\% — это я тебе как любимая студентка бабули говорю. А это значит, что никуда мы друг от друга не денемся!\nСёмка, Сёмка, Сёмочка…{/i}"
    stop ambience
    
    scene black with flash_red
    $ day_time()
    $ persistent.sprite_time = "day"
    $ renpy.sound.set_volume (0.5)
    play sound sfx_nightmare_explosion
    "И тут я выключаюсь."
    stop music
    
    scene dct_ext_railway with dissolve2:
        zoom 1.5
        anchor (200, 400)
    $ day_time()
    $ persistent.sprite_time = "day"
    $ renpy.sound.set_volume (1.0)
    play ambience ambience_forest_day
    th "Это что — прощай, активная фаза? Как я здесь?.."
    "Лес. Насыпь. До посёлка чуть больше километра. У меня в руках медведь. Медведь. Ульяна..."
    th "Ульяна!"
    "Ульянка приехала. Приехала ещё вчера, прочитала тот злосчастный приказ, не стала ночевать у себя в комнате, а сразу кинулась ко мне."
    "Увидела, что меня нет, запаниковала…  И сейчас от неё осталась только записка: «Сёмка, Сёмка, Сёмочка…»."
    stop ambience

    scene black with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play music the_day_when_i_die
    "Ну и я, прочитав записку, тоже запаниковал."
    "Кажется, я метался вокруг домика, проверил сарай и баню, влез на крышу, заглянул в погреб. Даже в колодец."
    "Добежал до малинника. Всё время звал. Разбил в кровь руки о стену сарая."
    "Разнёс в сарае несколько приборов, как будто они виноваты — кажется, звон бьющегося стекла чуть успокоил меня."
    "Почему я не выпал из активной фазы, чтобы последовать за Рыжиком, я не знаю — наверное, что-то держало меня."
    "После зачем-то схватил с дивана медведя и побежал в посёлок."
    show d_us normal dress at center with dissolve:
        ypos -0.05
        alpha 0.5
    us "Сёмка, Сёмка, Сёмочка…"
    hide d_us with dissolve2
    th "«Сёмка, Сёмка, Сёмочка…» — всё, что от неё осталось."
    "Единственный мой друг. Первая и единственная моя любовь. Человек."

    scene dct_ext_railway with dissolve:
        zoom 1.5
        anchor (200, 400)
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play sound_loop dct_sfx_pulse
    "Сердце колотится как бешеное, я сижу на земле, сплёвываю густую слюну и стараюсь восстановить дыхание. Похоже, я бежал сюда от самой своей будки."
    "Медведь лежит тут же у меня на коленях, глядя своими пуговичными глазами в голубое, с редкими кучевыми облаками, небо. Зачем я его схватил? Не помню."
    stop sound_loop fadeout 3
    "Который час? Не знаю. Часы где-то потерялись, но, кажется, автобус с сотрудниками ещё не ушёл."
    "Вот что я сейчас сделаю: пойду и отдам медведя шефу — пусть передаст Ульянкиным родителям. А сам буду думать, как жить дальше."
    "Наверное, по совету Толяныча скроюсь в лесу."
    "Потому что общаться с новым начальством у меня теперь нет необходимости; опять же, лишний раз общаться с людьми нет и желания."
    "А единственный человек, с которым есть такое желание — ушла."
    stop ambience

    scene dct_ext_railway with dspr:
        subpixel True
        zoom 1.5 anchor (200, 400)
        ease 5.0 zoom 1.0 anchor (0, 0)
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    "Встаю, как встают старики, медленно подобрав ноги под себя и медленно распрямляя их. Пошатываюсь. Только что не покряхтываю."
    dreamgirl "А за оставшиеся циклы я ускоренно постарею и, достигнув внутреннего возраста девяносто два года, отправлюсь в пассивную фазу, что символически будет означать смерть старого меня и рождение нового."
    th "Что за дурь в голову лезет, честное слово. Разум старается защититься от трагедии? Разум потерпит."
    th "Рыжик, Рыжик. Лучше бы ты уехала на материк. Я бы знал, что ты где-то есть и, может быть, ещё будешь счастлива."
    stop music fadeout 1
    stop ambience
    
    scene dct_ext_railway
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    "Насыпь — вот она, до посёлка —  пятнадцать минут быстрым шагом. Делаю пару шагов в сторону насыпи, и тут что-то случается."
    stop ambience
    
    scene dct_ext_railway_gr_bl with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play music music_list["afterword"]
    "Сначала в мире пропадают все цвета, остаётся почему-то только зелёный."
    "И вот в этом чёрно-зелёном, как экран осциллографа, мире у меня выдёргивают из-под ног землю."
    
    scene dct_ext_railway_gr_bl with vpunch:
        zoom 1.5
        anchor (900,400)
    $ day_time()
    $ persistent.sprite_time = "day"
    "Я падаю и, уже упав, чувствую, как продолжает вздрагивать почва подо мной, а со стороны посёлка проносится волна холода."
    "Я не знаю, что это, но нервная система трактует это как холод. Немеют руки и ноги."
    "Слух тоже пропадает, а может, не слух, а сами звуки. Не слышно ничего и никого: ни птиц, ни листвы, ни ветра, ни насекомых."
    "Время тоже теряется: неизвестное число секунд я просто лежу, пока постепенно не восстанавливается зрение и не возвращается слух и способность мыслить."
    
    scene dct_ext_railway with dissolve2:
        zoom 1.5
        anchor (900,400)
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    th "Что-то случилось, и случилось это в посёлке."
    stop ambience
    
    scene dct_ext_railway with dspr:
        running
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    play sound_loop dct_sfx_running
    "Вскакиваю, уже не как старик, а как семнадцатилетний, и бегу туда."
    stop sound_loop
    stop ambience
    
    scene dct_ext_railway with dspr
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    th "Забыл медведя!"
    "Вспоминаю, что забыл медведя, и поворачиваю назад. Мечусь совершенно бестолково."
    "Возвращаюсь к дереву, под которым пришёл в себя, и вижу, что зелёное дерево больше не зелёное."
    "Осина пылает красной сентябрьской листвой, и на фоне остальной летней зелени это выглядит пугающе."
    th "Внезапный локальный сентябрь в вечном лете…"
    th "Не до того, и нет никакой надежды и желания когда-нибудь разобраться."
    "Поднимаю с травы медведя и уже шагом отправляюсь в Шлюз."
    stop music
    stop ambience
    
    scene dct_ext_warehouse2_day_7 with squares
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_center_day
    "И первое, что я вижу, едва попав на открытое место — это столб дыма, поднимающегося откуда-то с площади."
    "Железнодорожные ворота распахнуты, двери казармы распахнуты, дневальный у пакгаузов отсутствует — наверное, у обоих ворот тоже."
    stop ambience
    
    scene dct_ext_module_day
    show dct_ext_module_smoke
    with dissolve
    $ day_time()
    $ persistent.sprite_time = "day"
    play ambience dct_ambience_fire
    play sound_loop ambience_medium_crowd_outdoors
    play music homeworldcollapse
    "В посёлке пожар. Горит административный модуль: третий его этаж, где размещался узел управления маяком и часть теоретических лабораторий."
    "Сотрудники института — кто стоит разинув рот, кто бестолково носится, кто пытается тушить пожар, схватив со щита ведро или огнетушитель."
    "Армейцы уже развернули пожарный рукав и сейчас собираются войти в главные двери, а двое из них как раз и отгоняют от этих дверей гражданских доброхотов."
    show cs normal far at left with dspr
    "Виолетта тут же, вооружённая фанерным ящиком с красным крестом, готовится принять раненых."
    hide cs with dspr
    show d_bg angry suit bag with dspr:
        zoom 0.8
        xalign .9 yalign .1
    "И чуть поодаль с непроницаемым видом наблюдает за этим безобразием здешняя императрица — баба Глаша."
    "Или, наверное. уже императрица в изгнании. Филиал-то закрылся."
    hide d_bg with dspr
    show d_tie normal with easeinright:
        zoom 0.9
        ypos 0.1
    hide d_tie with easeoutright
    "Попадается на глаза Галстук. Галстук с совершенно растерянным видом бегает от одного человека к другому, а от него только отмахиваются."
    "А вот автобуса на площади я не вижу. Значит, и с автобусом я опоздал. Хочу отойти подальше, но тут двери модуля распахиваются изнутри, и четверо армейцев в противогазах выводят пострадавших."
    "Толяныч вообще-то выходит сам, но тут же опускается на траву, а вот двоих оставшихся выносят практически на руках."
    stop music fadeout 1
    stop sound_loop
    stop ambience
    $ renpy.pause (0.5)

    scene dct_ext_switchman_house_sunset with squares
    $ prolog_time ()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_forest_evening
    "После пожара я пешком вернулся к себе. Но оставаться в домике не смог: слишком много Ульяны было вокруг."
    stop ambience
    
    scene dct_ext_hangar_sunset with dspr
    $ prolog_time ()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_forest_evening
    play music forgotten_summer
    "Обошёл двор."
    stop ambience
    
    scene dct_landscape_forest3 with dspr
    $ prolog_time ()
    $ persistent.sprite_time = "sunset"
    play ambience ambience_forest_evening
    "Опять прогулялся к малиннику, где я пытался спрятать Ульяну от ливня, укрыв плащом."
    stop ambience
    
    scene dct_ext_hangar_night with dspr
    $ prolog_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_forest_night
    "Пока бегал, окончательно стемнело."
    "Я вернулся во двор. Заглянул в сарай и представил себе, как Ульяна там ждала прихода тумана."
    "Так заболело в груди, что чуть опять не отключился."
    stop ambience
    
    scene dct_int_switchman_house_night_candle with dspr
    $ prolog_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_int_cabin_night
    "Вернулся в домик, а там каждый гвоздь напоминает о ней."
    "И были-то вместе, если дни складывать, то цикла три, не больше."
    "Я всё ждал, что сейчас выскочит из-за угла — что-то в кулаке зажато, глаза горят от восторга: «Сёмк, смотри что я нашла!»"
    "Мне казалось, что это такая затянувшаяся шутка и Рыжик вот-вот появится откуда-то, чтобы меня напугать. Не появлялась."
    stop music fadeout 1
    stop ambience
    
    scene dct_ext_railway_night with dissolve
    $ prolog_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_forest_night
    "Я взял спальник, остатки хлеба, консервов и пошёл в обход Периметра, не планируя больше возвращаться ни в домик, ни в посёлок."
    show d_us smile sport at cright with dspr:
        ypos -0.05
        alpha (0.5)
    $ renpy.pause (0.5)
    hide d_us with dspr
    stop ambience
    
    scene ext_path2_night with dissolve
    $ prolog_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_forest_night
    show d_us smile sport at cleft with dspr:
        ypos -0.05
        alpha (0.5)
    $ renpy.pause (0.5)
    hide d_us with dspr
    "Невозможно, обходя Периметр, не вспоминать наше знакомство с Ульяной; но на Периметре, по крайней мере, голова была занята тем, что нужно было следить, куда делать следующий шаг, чтобы не запутаться в складках пространства; и Ульяна там, на Периметре, была в прошлом, в днях нашего знакомства."
    "Или в будущем — я планировал в свой самый последний цикл, перед тем как нынешний Я уснёт навсегда, провести её по всему Большому кругу: Зеркальное озеро, Ржавые скалы, Говорящий ручей, Ковыльная степь, Бескрайнее болото, Корабельная роща и так далее."
    stop ambience
    
    scene ext_polyana_night with dissolve
    play ambience ambience_forest_night
    $ prolog_time ()
    $ persistent.sprite_time = "night"
    show d_us smile sport at cright with dspr:
        ypos -0.05
        alpha (0.5)
    $ renpy.pause (0.5)
    hide d_us with dspr
    "А вышло так, что я шёл один. Мысленно, а чаще вслух разговаривая с Рыжиком. Легче мне не стало, но я начал привыкать к боли."
    stop ambience
    
    scene dct_bonfire with dissolve
    $ prolog_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_forest_night
    play sound_loop dct_ambience_bonfire
    "А на третью ночь, когда я сидел у костра на каменном берегу того самого Зеркального озера, до которого можно докинуть камнем, но к которому невозможно подойти ногами, пришёл тот самый туман."
    "Для меня это был просто туман — чуть более плотный, чем туман обычный, но и только. А вот человека такой туман растворяет полностью, возвращая потом в виде НБО-копий."
    "Вот и тогда туман проникал под одежду, лип к коже, кажется, даже просачивался сквозь поры, а потом разочарованно отступал, чтобы через полчаса повторить попытку."
    whisper "Сёмка, Сёмка, Сёмочка…"
    "И не знаю, галлюцинация это или я что-то на самом деле слышал, но, казалось, туман постоянно шептал Ульянкину присказку." 
    "Да, тогда я и понял, что Ульяна здесь, осталось её только найти и отобрать у тумана. Я встал на ноги и, не дожидаясь рассвета, пошёл в посёлок."
    stop sound_loop
    stop ambience

    scene black with dissolve
    $ prolog_time ()
    $ persistent.sprite_time = "day"
    $ set_mode_nvl ()
    play music osc_two
    "Можно многое рассказать."
    "Как постепенно впадало в отчаяние и дичало оставшееся население посёлка, когда осознало, что мы оказались отрезанными от материка."
    "Как старшина роты охраны с удовольствием бил морды двум пьяным «научным сотрудникам», а армейцы взяли на себя обеспечение порядка в посёлке в обмен на обещание бабули вернуть всех домой в течение шести циклов."
    "Как я и командир армейцев выдёргивали за трос Виолетту из тумана у восточных ворот, а потом неслись втроём на Волге к воротам западным, чтобы Виолетта-оригинал смогла встретить и инициировать вышедшего из тумана своего будущего подлинника."
    "Как я первый раз увидел и услышал сдублированную Виолетту: это было жутковато — видеть, как она двигалась и разговаривала абсолютно синхронно с Виолеттой-оригиналом."
    "Как толпа «научных сотрудников» чуть не линчевала меня, а спасли меня те же армейцы.  Знаний биохимика хватило на то, чтобы поставить брагу, а готовый дистиллятор всегда можно взять в лаборатории."
    "Как в два приёма эвакуировали людей на материк, так что осталось в опустевшем посёлке пятеро: бабуля, сдублированные Виола, Толяныч и Трофимов, и я. Ну и ещё двое научных сотрудников и один солдат, потерявшиеся в тумане."
    "Жаль, что умер третий пострадавший в пожаре — дядя Валера, инженер местной коммунальной службы. Умер, не вынеся дупликации."
    "Как мы переводили сеть лагерей на автономный режим работы, как программировали течение циклов, как я, никого не поставив в известность, сочинял программу функционирования для самого себя, потерявшего личность."
    $ set_mode_adv ()
    stop music fadeout 1
    
    scene dct_ext_gate_entrance_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    show d_to normal pioneer at center with dissolve
    "Всё закончилось."
    "Сейчас я стою перед автобусом, а Толяныч набирает код на Выключателе, готовясь стереть меня."
    hide d_to with dspr
    "У каждого из оставшихся разные цели."
    "Бабуля как будто что-то доказывает — самой себе, нам и, кажется, ещё и пионерам."
    "Толяныч и Виола, похоже, как-то смогли встроиться в здешнюю систему. Не знаю я, почему они не эвакуировались — может, опасались того, что их не признают за людей."
    "Трофимов — тот для меня загадка, но гению и положено быть загадкой. А сейчас он унёс эту загадку с собой."
    stop ambience
    
    scene ext_bus with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    show sh upset pioneer far with dspr
    show d_bg angry suit bag with dspr:
        zoom 0.8
        xalign .7 yalign .1
    "Потому что Толяныч уже стёр старого Трофимова, а новый Шурик садится сейчас в свой Икарус, помогая, как порядочный пионер, подняться в салон бабе Глаше."
    hide d_bg with dspr
    hide sh with dspr
    stop ambience
    
    scene dct_ext_gate_entrance_day with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    "Я? Я же пошёл искать свою Ульяну."
    "И очень важно, чтобы нашел её именно тот я, который сейчас прощается со всем окружающим. Пускай я изменюсь и потеряю память, но это буду я."
    "Пускай Ульяна потеряла память и изменилась, но это она. А вместе мы — мы. Я надеюсь на это — больше ничего, кроме надежды, у меня не осталось."
    "Сейчас подумал, что Ульяна давно уже готовилась к этому. Как иначе объяснить эту её фразу, сказанную при расставании?"
    stop ambience
    
    scene dct_ext_switchman_house_day with pixellate
    $ prolog_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_forest_day
    show d_us normal dress at center with dspr:
        ypos -0.05
    us "Сёмка, ты больше не будешь один!"
    stop ambience
    
    scene dct_ext_gate_entrance_day with pixellate
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    "То есть она приехала уже подготовленная и сделала то, что сделала, по твёрдому своему решению."
    "Вспоминаю её записку: «… вероятность целых 0,3\%…». Вот, даже вероятность подсчитала — правда, два нолика убрала. Чтобы подбодрить меня, не иначе."
    "Вспоминаю слова милой девушки Мику — там, в лагере, когда я собирался на поезд, ещё не зная, что меня ждёт."
    stop ambience
    
    scene ext_square_day with pixellate
    $ prolog_time ()
    $ persistent.sprite_time = "day"
    show mi serious pioneer with dspr
    play ambience ambience_camp_center_day
    mi "Сенечка, я тут думала над твоим вопросом про людей и роботов. Хотела рассказать про творчество, любовь, эмоции, но — в общем, Сенечка, знаешь, что я подумала?"
    mi "Что только человек способен всё бросить, отказаться от гарантированного выигрыша и поставить на самый невероятный вариант. Загадать, что случится чудо, и монетка, которую он сейчас подбросит, останется стоять на ребре."
    show mi cry_smile pioneer with dspr
    mi "Самое интересное, что иногда это срабатывает."
    $ renpy.pause (0.5)
    hide mi with dspr
    "Она много ещё чего говорила, но суть я запомнил."
    stop ambience

    scene dct_ext_gate_entrance_day with pixellate
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    show d_to normal pioneer at center with dissolve
    d_to "Семён, ты уверен? Ты можешь остаться, как мы. Хоть физруком в первом узле. И нам за Глафиру Денисовну будет спокойнее."
    th "Могу, наверное."
    "Вы поделитесь опытом, или здешнее мироздание само вступит со мной в контакт."
    "Но встретит Ульяну тогда совсем другой человек, а не я — это я знаю точно. Хотя тот человек и будет считать себя мной нынешним — это я тоже знаю точно."
    "Ловлю глазами стрекозу, зависшую над головой у Толяныча, и улыбаюсь ей. Последнее, что я видел — это была стрекоза-богатырь. Так и запишем."
    me "Толяныч, не тяни кота за хвост. Пионером был, пионером и останусь."
    show cs normal at right with moveinright
    cs "Мы приглядим за тобой, пионер. Обещаю."
    stop ambience
    
    scene dct_ext_gate_entrance_sim with hpunch
    play sound sfx_click_1
    queue sound sfx_inhale
    $ renpy.pause (0.5)
    
    scene dct_ext_gate_entrance_day with dissolve2
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    "Щёлкает кнопка Выключателя. И моё Я (хочу сказать — моя душа, но души у таких как я, кажется, нет) отделяется от тела. Я как будто вижу себя со стороны, с высоты верхушек деревьев."
    "Но я, оказывается, ещё могу управлять своим телом: неуклюже, как будто управляю марионеткой за ниточки."
    stop ambience
    
    scene ext_bus with dissolve
    $ day_time ()
    $ persistent.sprite_time = "day"
    play ambience ambience_camp_entrance_day
    "Последний раз оглядываюсь и отправляю бывшее своё тело к дверям терпеливо ждущего автобуса."
    play music music_list["everlasting_summer"]
    show d_us normal dress at center with dspr:
        ypos -0.05
        alpha (0.3)
    th "Вот и я сейчас поставил на то, что монетка упадёт на ребро, Рыжик. Слышишь? Я иду за тобой."
    stop music fadeout 3
    stop ambience
    
    $ renpy.pause(2.0, hard=True)
    $ persistent.d_sim = d_sim + 1
    
    scene black with blinds
    
menu:
    "Конец книги «Сёмка, Сёмка, Сёмочка»"
    "Продолжить":
        jump dct_cat_house
    "В меню":    
        jump dct_mnu2

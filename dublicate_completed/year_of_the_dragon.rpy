label dct_dragon:
    scene black
    play music music_list["raindrops"]
    show headline_text u"Год дракона" at truecenter with dspr
    $ renpy.pause(5.0)
    hide headline_text with dissolve
    show text(u"{size=48}{color=#ffdd7d}{i}Дождь. Опять. Всю ночь.\nДо самого утра.{/i}{/color}{/size}") at right with dspr
    $ renpy.pause(3.0)
    hide text with dissolve
    stop music fadeout 1
    $ save_name = u"Дубликат. Год дракона"
    
    scene int_bus_people_day with hpunch
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    $ set_mode_adv ()
    "Просыпаюсь как от толчка."
    "И сразу начинаю крутить головой."
    th "Доброе утро, Алиса. Где-это я?"
    th "А, это же автобус, я же в лагерь еду!"
    play music i_tried_to_bring_it_back
    "Как-то я с приключениями сюда добиралась, но не вспоминается так, спросонья. Или это все сон мне снился? Интересно, что за компания у меня на эти две недели будет?"
    "Рядом мелкая спит, года на два-три меня по младше. Тоже рыжая, как и я." 
    "Поселить её с собой в одном домике? Домик Рыжих будет. Что-то родственное в ней чувствую, надо будет, как проснется, познакомиться с ней поближе."
    "Поднимаюсь на ноги и выглядываю в проход. Люди как люди." 
    "Вон девочка спит, на Ленку похожа."
    th "А хорошо бы... Нет, это невероятно, чтобы Ленка из Ленинграда сюда приехала."
    "Парней всего двое и оба явные ботаники."
    "Так, еще одна гитаристка, кроме меня. Интересно, как она с такими длинными волосами живет? Да еще и в такой цвет выкрасила. Ох, наживет она тут проблем от администрации." 
    "Кто еще есть интересный?" 
    "И тут меня толкают локтем в бок. Не сильно, но бесцеремонно."
    stop music fadeout 1
    show us grin sport close at left with moveinleft
    us "Привет, Рыжая!"
    play music music_list["eat_some_trouble"]
    th "На себя посмотри."
    th "Я, конечно, рыжая, но нельзя же так сразу."
    dv "От рыжей слышу, а меня, вообще-то, Алиса зовут."
    stop music fadeout 3
    "Холодно смотрю на соседку."
    show us sad sport close with dissolve
    us "Ты что, Алиса, это же я, Ульяна…" 
    show us angry sport close with dissolve
    us "Ты что, всё забыла? Семена помнишь? Бомбоубежище помнишь? Вечер в столовой помнишь?" 
    "Я только пожимаю плечами. О чем речь, вообще?"
    show us fear sport close with dissolve
    us "Ну ничего, Алиса, я тебя в покое не оставлю, я заставлю тебя все вспомнить!" 
    th "А вот сейчас соседка точно или заплачет, или поколотит меня. Что с ней такое?"
    "Я машинально сую руку в карман куртки. «Чья куртка? Откуда она на мне?» И нащупываю там свернутую бумажку." 
    "Записка, почему-то чертежным шрифтом, очень уверенным почерком, как-будто человеку привычно так писать: {i}«Алиса, а сильная отдача у арбалета?»{/i}."
    "И вторая строчка: {i}«Надо тебе дать пендель, чтоб проснулась. Если ты и так все вспомнила, то поймешь меня. Прощения не прошу.»{/i}." 
    "Соседка что-то продолжает говорить, а я замираю и не слушаю ее. Я вспоминаю!"
    th "Что-то с памятью твоей стало, Алиса. Даже дом родной не могу вспомнить. Ну давай, хотя бы день отъезда вспомни!."
    stop sound_loop
    
    scene black with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "day"
    $ set_mode_adv ()
    play sound sfx_door_bell
    $ renpy.pause(1)
    stop sound
    "Вот я стучусь к соседке."
    play music music_list["trapped_in_dreams"]
    play sound sfx_open_door_1
    dv "Здрасьте, Марьпетровна. Что-ж вы не спрашиваете, кто пришел?" 
    d_mp "А зачем, Алисочка? Только ты одна так и звонишь. Как-будто точку в конце предложения ставишь. Переночевать пришла? Заходи."
    dv "Нет, я по другому делу. Я, Марьпетровна, неожиданно в пионерский лагерь уезжаю на две недели. Пусть мои вещи у вас полежат?"
    "Потому что не хочется мне их в квартире оставлять. Маманя разных мужиков к себе водит. Раз в полгода новый «папа», и не каждый из «пап» безобидный тихий алкаш."
    "Вот уже наверное год, как я стала угадывать: что сейчас произойдет или о чем меня спросят, как-будто я уже проживала эту жизнь, как-будто всё уже было. Вот и сейчас Мария Петровна запахнёт халат потуже и непонимающе посмотрит на меня, а я объясню в чем дело."
    dv "Самой смешно. Семнадцать лет и пионерский лагерь. Туда, оказывается, до восемнадцати ездить можно. У завучихи дочка должна была поехать, но заболела. Шампанское холодное на выпускном оказалось. Вот, чтобы не пропала путевка, я и поехала."
    "Отдали мне путевку, потому что путевка в старший отряд. Иначе не увидела бы я ее — рылом не вышла. Ну не хотят старшие в пионерский лагерь ездить: тебе семнадцать лет, а тебя в шортики или юбочку наряжают и заставляют под барабан строем ходить!"
    "Лагерь то — пионерский. Вот и не хотят. А вот я, я согласилась — были на то причины. И мы еще посмотрим, кто там будет под барабан в красном галстуке маршировать. А я как знала, что мне путевку предложат, когда утром мимо школы пошла и на крыльце завучиху встретила."
    "Я же говорю — стала угадывать то, что должно случиться."
    d_mp "Понятно, Алисочка. Может тогда чаю попьем на прощание? Мама то дома? Знает что ты уезжаешь?"
    "Вот не надо про мать. Хотя, Марии Петровне можно."
    dv "Дома она, не проспалась еще, ничего она не знает. Записку на столе для неё оставила, прочитает, если захочет. И вы простите, Марьпетровна, некогда мне чай пить, правда-правда. А то на поезд опоздаю."
    "Мария Петровна хочет сказать что-то ещё, но только показывает на угол прихожей."
    d_mp "Ставь туда своё приданое, не пропадет. Потом в кладовку уберу."
    "Ставлю, куда показали, пакет с «приданым»: две пластинки, кое-какие документы, тетрадка со стихами и табами, золотая цепочка, письмо от Ленки — она, когда уезжала в Ленинград, семь лет назад, письмо написала, я ей ответила на новый адрес, и всё, и закончилась переписка."
    "Вот и всё моё приданое. Остальное везу с собой: спортивная сумка с вещами и гитара в чехле."
    d_mp "Может все-таки попьешь чаю-то?"
    dv "Марьпетровна, ну поезд же ждать не будет. А как приеду, так попьем обязательно. Я обещаю."
    "Мария Петровна обнимает меня, я обнимаю ее, даже слезинка подступила."
    "Что может быть общего у семнадцатилетней пацанки и семидесятидевятилетней бабушки, всю жизнь проработавшей (она говорит: прослужившей) на должности литературного редактора?"
    "Но вот уже пять, нет шесть лет мы общаемся. Началось с того, что она, не вынеся моих издевательств над гитарой, взяла меня за руку и затащила к себе домой, чтобы: «Хоть три аккорда тебе показать, а то уши отваливаются!»."
    "Всякое бывало: и орали друг на друга, и ночевала я у нее, и скорую к ней вызывала и в больницу к ней бегала, и она ко мне в больницу ходила… в больнице я всем врала, что мать работает, а ко мне бабушка ходит."
    dv "Марьпетровна, вы так прощаетесь со мной, будто я не на две недели, а навсегда уезжаю."
    d_mp "Беги на поезд, Алисочка. Для меня и две недели могут «навсегда» оказаться. И ты через две недели уже другая приедешь."
    "Меня разворачивают и легонько выталкивают на площадку. Слышу сзади всхлип."
    dv "Марьпетровна..."
    stop music fadeout 1
    d_mp "Беги-беги. Может ты и вовсе не приедешь."
        
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    $ set_mode_adv ()
    play sound_loop sfx_intro_bus_engine_loop
    show us fear sport close at fleft
    th "О, что-то начала вспоминать, нормально."
    "Рыжая орет на меня, но мне не до нее."
    th "Отстань, Мелкая. Я тебе в домике морду набью."
    stop sound_loop
    
    scene black with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "day"
    $ set_mode_nvl ()
    play music music_list["you_lost_me"]
    "На меня последний раз пахнуло смесью запахов лекарств, герани, книг и каких-то духов. И дверь за моей спиной мягко закрылась."
    "Ну вот, с единственным взрослым, который что-то для меня значит, я попрощалась. Но что-то было неправильное в этом прощании, не должна она была говорить, про то что я не приеду."
    "Стою спиной к двери Марьи Петровны и шагнуть к выходу не могу, а вместо этого разглядываю наш подъезд: сантиметровый слой масляной краски на стенах и лестнице: стены зеленые, деревянная лестница — коричневая. Ступеньки за пятьдесят лет вышарканые жильцами так, что на них углубления от ног остались, отполированные руками жильцов широкие перила, по которым так удобно скатываться."
    "И везде: на штукатурке стен, на перилах, на дверях в подъезд — выцарапаны надписи. Каждое поколение детей считает нужным здесь отметиться, оставляя свои имена, а ЖЭК только красит поверх выцарапанного, так что надписи остаются видны."
    "Вон и две моих: «Алиса» и «Алиса+Лена», а к последней надписи Алик дописал «=дуры», за что потом от меня по шапке получил. Один раз за меня, один раз за Ленку…"
    "Что-ж мне так идти то не хочется? Может вернуться и попить чаю у Марьи Петровны? Нет! Встряхиваюсь, поезд, действительно, ждать не будет."
    nvl clear
    "Вот и двор. Хороший двор, что бы там не говорили. Самое главное, что чужих здесь не бывает. Две двухэтажки и одна трехэтажка, стоящие буквой П, огораживают его с трех сторон, а с четвертой он закрыт от посторонних сараями. Когда-то в них дрова хранились, а в шестидесятых, еще до моего рождения, в дома газ провели. Газ провели, а сараи остались."
    "И теперь наш двор, это такой закрытый от посторонних мир: детская площадка у первого дома, перекладины для сушки белья у третьего, и аллея с двумя десятками старых тополей, которые все называют «парк» — посередине."
    "Наши должны уже собраться у крайнего сарая. Так и есть, вон они сидят и дымят: четверо в карты режутся, Миха с мотоциклом ковыряется, Миха-большой на турнике повис. Макс, как обычно, чуть в стороне и в книжку уткнулся."
    "Портвейн еще не доставали, ну правильно, светло еще, незачем народ дразнить, а то 02 звонить начнут. Сейчас спросят, куда я собралась."
    "Здороваюсь и сразу прощаюсь. Попутно вытаскиваю изо рта у Макса сигарету, рано ему еще, и вставляюю ее себе в рот."
    "Иду к остановке, а Макс увязывается со мной."
    "И опять то ощущение, что что-то идет не так: я думала мне в чувствах признаваться будут, а Макс уговаривает меня не возвращаться домой после лагеря." 
    "А на остановке произносит «Прощай» вместо «Пока»."
    $ set_mode_adv ()
    
    
    scene dct_int_liaz_day_rain with pixellate
    show dct_dream_veil:
        shiver
    d_dy "Следующая остановка — Вокзал!"
    stop music fadeout 1
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us fear sport close at fleft
    th "Почему мне кажется, что все было не так? И тогда казалось, и сейчас."
    th "Почему я ехала на вокзал?"
    th "Мне нужно было в кассу, в окошко администратора. Показать путевку в лагерь и забрать билет."
    th "Так, дальше что было? Вспоминай, Рыжая."
    stop sound_loop
    
    scene dct_int_corridor2 with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "day"
    show dct_dream_veil:
        shiver
    play music music_list["goodbye_home_shores"]
    "В коридоре ведущем к кассам я натыкаюсь на дядьку."
    "Я направо и он направо, я налево и он налево. И так несколько раз." 
    "Я помню, что колеблюсь: или обматерить его для начала, или сразу кастет доставать. Не люблю я таких дядечек с некоторых пор, не люблю аж до кастета, седина им в бороду."
    "Но дядька только улыбается и дорогу уступает, иди мол. А я сразу успокоилась, даже улыбнуться в ответ захотелось."
    d_dk "Проходите, барышня, а то до утра не разойдемся. Вот только касса то закрыта."
    "Дяденька окает, а я анекдот про окрестности Онежского озера сразу вспомнила. Мне, правда, самой захотелось улыбнуться в ответ, но я сдержалась."
    dv "Я слишком юна для тебя, дядя."
    stop music fadeout 1
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us fear sport close at fleft
    th "Вот, вспомнила. Тогда я тоже почувствовала, что что-то не так: почему в тоннеле ни души, что это за дядька, почему закрыта круглосуточная касса?"
    "Ульяна, ты меня уже достаёшь."
    stop sound_loop
    
    scene dct_int_corridor2 with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "day"
    show dct_dream_veil:
        shiver
    play music music_list["no_tresspassing"]
    "А касса и правда закрыта. Все пять окошечек, и в предбаннике никого. Только скучающий милиционер, сидит на скамье и дремлет над газетой."
    "Сначала стучусь в окошко к администратору, не дождавшись ответа начинаю стучать во все подряд."
    voice "Деточка, ты читать умеешь?"
    "Голос из-за спины. Милиционер проснулся."
    "А на окошечке записка, которой только что не было: {i}«Кассы закрыты до 9-00. Администрация.»{/i}." 
    "Я поворачиваюсь к милиционеру, чтобы отлаять его за «деточку», а того уже нету. Только пожелтевший «Советский спорт» на скамейке лежит."
    stop music fadeout 1
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us angry sport close at fleft
    th "Мне было не по себе от этой чертовщины, а стало просто страшно. И я бросилась бежать"
    "Не помню, как я проскочила корридор. Просто, вот я стояла перед кассами, и вот я уже в зале. Кажется — мгновенно перенеслась."
    "Нет, через зал я только проскочила, прямо в буфет. Почему в буфет? Потому что там были люди. Представляете? Зал ожидания на вокзале пустой и только в буфете кто-то есть." 
    th "Мне и сейчас страшно, а тогда, когда бежала от кассы, сама себя не помнила."
    "Мелкая вроде успокаиваться начала. Поселю с собой, точно. Но морду все равно набью."
    stop sound_loop
    
    scene dct_int_cafe with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "day"
    show dct_dream_veil:
        shiver
    play music music_list["you_won_t_let_me_down"]
    play ambience ambience_dining_hall_empty
    "В буфете никого, только давешний дядька. Перед ним гора пирожков на тарелке, несколько бутылок с лимонадом и минералкой и начатый стакан с чаем."
    "В дополнение к моим страхам еще и темнеет, откуда-то натянуло грозовые тучи, перекрывшие свет заходящего солнца. Желтые лампочки накаливания не могут до конца победить темноту, и в зале устанавливается полумрак." 
    
    scene dct_int_cafe_sunset_rain with dissolve
    $ prolog_time()
    $ persistent.sprite_time = "sunset"
    show dct_dream_veil:
        shiver
        
    "А я поняла, что во всем вокзале просто никого нет."
    "Никого, только дядька, я и стайка цыганок, испуганно жмущихся в тамбуре и не решающихся выйти на привокзальную площадь, под ливень, который вот-вот начнется."
    d_dk "Сейчас ливанет. Садись перекусишь перед дорогой, я и на тебя взял."
    "Я, чтобы не показывать свой страх, начинаю наступать на дядьку."
    dv "Ты! Что все это значит? Это ты все устроил!"
    d_dk "Что устроил?"
    "Дядька улыбаясь смотрит на меня снизу вверх."
    d_dk "Ты зачем к кассам поперлась? Тебе что было сказано? \"Автобус на привокзальной площади\"."
    d_dk "Ох уж эти ..."
    "Тут дядька произносит непонятное слово."
    d_dk "Вечно все путают и забывают. Да ты кушай." 
    "Дядя меняет тему, пододвинув ко мне тарелку с пирожками и бутылку с лимонадом."
    d_dk "Или, как хочешь, — девчонки съедят. Вон они, уже бегут. Славяна — та точно не откажется."
    stop ambience
    stop music fadeout 1
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us sad sport close at fleft
    th "Что-то тогда зашевелилось у меня в памяти, в ответ на имя «Славяна», но успокоилось."
    stop sound_loop
    
    scene dct_int_cafe_sunset_rain with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "sunset"
    show dct_dream_veil:
        shiver
    play ambience ambience_dining_hall_empty
    play sound dct_sfx_discharge
    "За окном загрохотало, тут же, как по приглашению, в буфет забегают две девушки, примерно мои ровесницы, только вот не моего круга."
    show d_mi smile casual at left behind dct_dream_veil
    show d_sl smile dress behind dct_dream_veil:
        xalign .7 yalign .1
    with moveinleft
    d_dk "Не смотри на них так, Алиса."
    stop sound
    stop ambience
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us sad sport close at fleft
    th "Дядька тогда назвал меня по имени, но вот это меня почему-то не совсем не напугало."
    "А Мелкая киснет. Да что с ней такое?"
    stop sound_loop
    
    scene dct_int_cafe_sunset_rain with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "sunset"
    show dct_dream_veil:
        shiver
    play music music_list["drown"]
    play ambience ambience_dining_hall_empty
    show d_mi smile casual at left behind dct_dream_veil
    show d_sl smile dress behind dct_dream_veil:
        xalign .7 yalign .1
    with dissolve
    d_dk "С ЭТИМИ девушками ты не знакома. Позволь официально представить тебе моих подруг по несчастью: Мику Хатсуне и Славяну Феоктистову. Девочки, это Алиса Двачевская, которая вот-вот окончательно проснется и покинет нас надолго, если не навсегда. Ну, это вы знаете, иначе нас бы сюда не выкинуло."
    hide d_mi
    hide d_sl
    with dissolve
    show d_mi smile casual at cleft behind dct_dream_veil
    show d_sl smile dress behind dct_dream_veil:
        xalign .99 yalign .1
    with dissolve
    mi "Здравствуйте, дядя Валера."
    show d_mi grin casual
    show d_sl normal dress
    with dissolve
    "Тут иностранка обращает на меня внимание."
    mi "Здравствуй, меня зовут Мику, Мику Хатсуне. Мику это имя, а Хатсуне это фамилия. Это японские имя и фамилия, потому что мама у меня…"
    show d_mi sad casual with dissolve
    "И тут Мику вздрагивает, шепчет что-то вроде: «Никак не отвыкну», — и внезапно замолкает, отвернувшись."
    show d_sl smile dress with dissolve
    d_sl "Еле спаслись от дождя, дядя Валера!"
    stop ambience
    stop music fadeout 1
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us cry sport close at fleft
    th "И опять. Мне тогда показалось, что имя «Мику» и эта манера тараторить мне знакома."
    th "Может быть, это всё-таки мне сон приснился, пока мы ехали?"
    "Ну, Мелкая, ну нельзя же так. Ну обозналась ты, ну бывает."
    stop sound_loop
    
    scene dct_int_cafe_sunset_rain with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "sunset"
    show dct_dream_veil:
        shiver
    play music music_list["trapped_in_dreams"]
    play ambience ambience_dining_hall_empty
    show d_mi sad casual at cleft behind dct_dream_veil
    show d_sl smile dress behind dct_dream_veil:
        xalign .99 yalign .1
    with dissolve
    hide d_mi
    show d_mi normal casual at fleft behind dct_dream_veil
    with dissolve
    "Девочки делят между собой пирожки и жадно накидываются на еду, при этом иностранка не отстает от колхозницы." 
    "Или у них у всех стальные желудки, или они самоубийцы. Что-то брать в вокзальном буфете."
    "Пока они едят и переговариваются о чем-то своем, я пью лимонад, закусывая его своим личным печеньем из сумки (надеюсь — лимонад безопасный) и разглядываю всех троих."
    hide d_mi
    show d_mi normal casual at cleft behind dct_dream_veil
    with dissolve
    mi "Дядя Валера." 
    "Я ожидала бесконечного потока слов от Мику, а она неожиданно меня обманула." 
    mi "Зачем вы так? Я понимаю, что вам нужно объяснить Алисе, почему мы трое вместе, но я себя несчастной не считаю, Славяна тоже. Да и вы тоже, не прибедняйтесь."
    "Вижу, что дядька отвлекся от тарелки и спрашиваю."
    dv "И где же ваш автобус?"
    stop ambience
    stop music fadeout 1
    
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us sad sport close at fleft
    th "Это все-таки был сон. В жизни я бы их всех послала, но во сне — почему бы и не поддержать разговор?"
    stop sound_loop
    
    scene dct_int_cafe_sunset_rain with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "sunset"
    show dct_dream_veil:
        shiver
    play music music_list["dance_of_fireflies"]
    play ambience ambience_dining_hall_empty
    show d_mi normal casual at cleft behind dct_dream_veil
    show d_sl smile dress behind dct_dream_veil:
        xalign .99 yalign .1
    hide d_mi
    show d_mi normal casual at fleft behind dct_dream_veil
    with dissolve
    d_dk "Как всегда, на той стороне площади."
    "Дядька пожимает плечами так, будто я у него спросила, какого цвета трава."
    hide d_sl
    show d_sl normal dress behind dct_dream_veil:
        xalign .6 yalign .1
    with dissolve
    d_sl "Дядь Валера, она же спит еще, она же место не может выбирать, ты ей хоть наводку дай какую. Где этот автобус, как на него сесть?"
    d_dk "Не ты нОходишь четырестОдесятый Овтобус, а четырестОдесятый Овтобус нОходит тебя!"
    "Дядька окает совсем уж преувеличено. И еще поднимает блестящий от пирожкового жира указательный палец кверху, чем портит все впечатление." 
    "Славяна ждет продолжения, но дядя Валера опять занялся пирожками и замолк, тогда Славяна берет инициативу в свои руки."
    show d_sl smile dress
    d_sl "Понимаешь, Алиса. Дядя Валера и есть водитель того самого автобуса." 
    "А дядя Валера, я уже мысленно так его называю, кивает в подтверждение."
    d_vl "Точно, и автобус, между прочим, уже час тебя дожидается. Какого… ты на вокзал поперлась?"
    "И оканье его куда-то пропало."
    stop ambience
    stop music fadeout 1
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us sad sport close at fleft
    th "А я совсем запуталась и потерялась между сном и воспоминаньями. И реальным кажутся только этот Икарус, соседка и пионерский лагерь, куда мы едем."
    "Хорошо хоть Мелкая реветь перестала."
    stop sound_loop
    
    scene dct_int_cafe_sunset_rain with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "sunset"
    show dct_dream_veil:
        shiver
    play ambience ambience_dining_hall_empty
    "А эти трое расправились с пирожками, Мику относит тарелку на мойку (за все время ни буфетчица так и не появилась, ни посетителей никого не было) и мы, обогнув цыганок, выходим на привокзальную площадь."
    stop ambience
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us sad sport close at fleft
    th "Дальнейшие события опять вспоминаются кусками."
    stop sound_loop
    
    scene dct_ext_city_scuare_sunset with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "sunset"
    show dct_dream_veil:
        shiver
    play music twinkling
    show d_mi smile casual at fleft behind dct_dream_veil
    show d_sl smile dress behind dct_dream_veil:
        xalign .99 yalign .1
    with pixellate
    "Вот девочки прячутся под зонтиками, потому что ливень хоть и закончился и небо почти очистилось, но мелкий дождик еще идет, а дядя Валера снимает с себя кожаную куртку и накидывает мне на плечи, оставаясь в одной рубашке." 
    dv "Дядя Валера, не надо, сами же промокнете."
    "Я сопротивляюсь, а он только отмахивается."
    d_vl "Алиска, фантомы не болеют."
    "От куртки слабо пахнет машинным маслом, бензином и табаком." 
    "На мгновение мы встречаемся взглядами и я вижу… тоску и что-то еще, даже не могу описать — что. Я не Достоевский, чтобы описывать." 
    "Дядя Валера извлекает из кармана куртки пачку сигарет и ключи от автобуса, закуривает и контакт теряется."
    "Но мне вдруг захотелось, чтобы тоски в глубине его карих глаз поубавилось. А еще жалею, что маман, в своих попытках устроить личную жизнь, скатывалась все ниже и ниже, не встретив вот такого дядю Валеру."
    "Я еще хочу спросить про то, что за фантомы он поминал, но забываю."
    "Вот мы идем по улице, Славяна оглядывается."
    hide d_sl
    show d_sl normal dress behind dct_dream_veil:
        xalign .6 yalign .1
    with dissolve
    d_sl "Идут за нами."
    hide d_sl
    show d_sl smile dress behind dct_dream_veil:
        xalign .99 yalign .1
    with dissolve
    "Тут уже оглядываюсь я. Все те же цыганки, что стояли в тамбуре вокзала, идут за нами метрах в пятидесяти, не отставая и не догоняя."
    d_vl "Я же говорил, что так и таскаются за мной от сна к сну. Где я их подцепил, ума не приложу."
    stop music fadeout 1
    
    scene int_bus_people_day with pixellate
    $ day_time()
    $ persistent.sprite_time = "day"
    play sound_loop sfx_intro_bus_engine_loop
    show us sad sport close at fleft
    th "Значит это все-таки сон."
    th "Да, я помню, тогда же и подумала, что по дольше бы не просыпаться. А то проснешься, а там беспутная маман и взрослая жизнь в общаге ткацкой фабрики."
    th "И, это, Ульяна, не дуйся ты так. Я тебя даже бить передумала."
    stop sound_loop
    
    scene dct_ext_city_scuare_sunset with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "sunset"
    show dct_dream_veil:
        shiver
    play music higekitekina
    show d_mi serious casual behind dct_dream_veil with dissolve
    "Вот Славяна с дядей Валерой вырвались вперед. И мы идем плечом к плечу с Мику."
    mi "Знаешь, Алисочка. Я хотела, пока мы были под крышей, попросить у тебя подержать гитару, а то так поиграть хочется, что пальцы болят."
    "И вдруг, без перехода Мику резко меняет тему."
    show d_mi angry casual with dissolve
    mi "Я тебя ненавижу, Алиса. За Сенечку. Ты убила его."
    "А голос спокойный, и бесцветный какой-то."
    "Я ничего не понимаю и только пожимаю плечами. Кто такой Сенечка? А Мику продолжает, Мику почему-то надо выговориться."
    mi "Хорошо, что все обошлось. Потому что иначе… Меня нельзя убить, я остаточный фантом, но случилось бы что-то нехорошее. Молчи, Алисочка. Просто молчи. Ты убила его и теперь за тобой долг." 
    mi "Ты мне его никогда не выплатишь, а я никогда не буду с тебя его требовать. Просто помни о нем. Я не сумасшедшая, я знаю, что ты не виновата, и сейчас ничего не помнишь и не понимаешь о чем речь, и в лагере мои слова забудешь. Но я тебя ненавижу."
    th "Ну молчи, так молчи. Я и молчу."
    hide d_mi with moveoutright
    show d_sl normal dress behind dct_dream_veil with moveinleft:
        xalign .5 yalign .1
    "Вот Мику убежала вперед всех, чтобы мы не видели, как она плачет. Вот дядя Валера догнал ее, чтобы она не плакала в одиночестве. А я оказалась вдвоем со Славяной."
    dv "Вы тоже в лагерь?"
    show d_sl sad dress
    d_sl "Нет! Нам нельзя. Мы всего лишь остаточные фантомы! И не спрашивай об этом больше никогда!"
    "Еще одна сумасшедшая."
    dv "А дядя Валера, он тоже фантом?"
    "Чтобы не беспокоить Славяну спрашиваю я."
    show d_sl serious dress
    d_sl "Почти. Дядя Валера, он застрял на полпути. Он говорит, что в его институте случилась авария и три человека-оригинала пострадало. Слишком поздно решились на запись подлинников, двоих успели переписать, а он умер в процессе записи. Поэтому для него у вас нет тела."
    "Произносится все это спокойно и как-то грустно, как будто о каком-то давнем несчастье рассказывают, так что я даже не знаю, как к этому относиться. Конечно это бред, но вдруг я что-то не знаю и не понимаю? И где это, У НАС?"
    stop music fadeout 1
    
    scene dct_ext_410bus with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "night"
    show dct_dream_veil:
        shiver
    play music adaytoremember
    "Вот мы стоим на проезжей части перед автобусом. Мику вдруг обнимает меня"
    show d_mi cry casual behind dct_dream_veil with dissolve
    mi "Прости меня, Алисочка. Забудь, что я тебе наговорила."
    hide d_mi
    show d_sl tender dress behind dct_dream_veil:
        xalign .5 yalign .1
    with dissolve
    "Следом Славяна."
    d_sl "Прощай, Алиса. Передай Семену, что он… что его… Ничего ему не передавай. Забудь."
    hide d_sl with dissolve
    "Тут автобус заводится, хлопает водительская дверь и из кабины выходит дядя Валера."
    d_vl "Всё, по машинам, Алиса. До встречи, девочки."
    "Славяна и Мику отходят на тротуар, я порываюсь стянуть с себя куртку, но дядя Валера меня останавливает."
    d_vl "Потом, Алиса. Все, поехали."
    "И засовывает что-то в карман куртки."
    "Я забираюсь в салон, вижу, как дядя Валера коротко обнимает девочек и бежит в кабину, под снова усиливающимся дождем. Что-то скрежещет под полом и мы трогаемся."
    
    scene dct_buswindov_rain with pixellate
    $ prolog_time()
    $ persistent.sprite_time = "night"
    show dct_dream_veil:
        shiver
    "Уже стемнело, мы не торопясь едем по городу сквозь дождь, останавливаясь на редких светофорах."
    "Окна запотели и я время от времени смахиваю лишнюю влагу со стекла ребром ладони. По ногам потянуло теплым воздухом — заработала печка."
    "Становится очень уютно, я поплотнее заворачиваюсь в куртку, вытягиваю ноги и прижимаюсь виском к прохладному стеклу, глядя на пробегающие за окном дома." 
    "Дядя Валера включает музыку, что-то хорошее и незнакомое, и я кричу во всё горло, чтобы он отмотал на начало и сделал погромче."
    "Какое-то время еще пытаюсь понять, почему на имя «Семён», что-то откликается внутри меня. Никого же не знаю и не помню, чтобы человека так звали."
    stop music fadeout 1

    scene int_bus_people_day with hpunch
    $ day_time()
    $ persistent.sprite_time = "day"
    play music anewbeginning
    show us sad sport close at fleft
    "Записка тает у меня в руках, а я вспоминаю: наконечник стрелы и спина, обтянутая пионерской рубашкой."
    "Вот я плавно тяну спуск, вот арбалет вздрагивает, и в этот момент Ульяна толкает меня. А я вижу, как стрела входит между лопатками Семена." 
    "Семен?" 
    "Ульяна?"
    hide us
    show us sad sport close at center
    with dissolve
    dv "Улька!" 
    "Кричу так, что те в автобусе, кто еще не проснулся — просыпаются, а те, кто уже проснулся — вздрагивают и оглядываются."
    "Я обнимаю Ульяну и начинаю плакать."


    scene black with blinds
    $ renpy.pause(3.0)
    stop music fadeout 2.0
    $ renpy.pause(2.0, hard=True)
    $ persistent.d_dra = d_dra + 1
    
    jump dct_clouds